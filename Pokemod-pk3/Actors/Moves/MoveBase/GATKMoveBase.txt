actor WepMasterBase : WepMasterBase0
{
States
{

AltFire:
TNT1 A 0 
TNT1 A 0 A_JumpifInventory("Recovery_B",1,"NoAmmo")
TNT1 A 0 A_JumpifInventory("FatigueMark",1,"NoAmmo")


TNT1 A 0 ACS_NamedExecuteAlways("PlayerState",0,1)
TNT1 A 0 A_JumpIfInventory("GATK_Pound",1,"AltFire.Pound")
TNT1 A 0 A_JumpIfInventory("GATK_Scratch",1,"AltFire.Scratch")
TNT1 A 0 A_JumpIfInventory("GATK_Jab",1,"AltFire.Jab")
TNT1 A 0 A_JumpIfInventory("GATK_Lash",1,"AltFire.Lash")
TNT1 A 0 A_Jump(256,"AltFire.Tackle")
Goto AltFire.Tackle


AltFire.Tackle:
"####" A 3 A_JumpIfInventory("ParalysisMark",1,"NoAmmo")
"####" AAA 0 A_SpawnItemEx("GroundDustFX", random(0,50), 0, -(z-floorz), 0, 0, 0, random(0, 359))
"####" A 0 A_PlaySoundEx("misc/KingSwing","Weapon")
"####" A 0 A_GiveInventory("TackleTimer")
"####" A 0 A_GiveInventory("TackleInitiate")
AltFire.Dash:
"####" A 1
"####" A 0 A_SpawnItemEX("GroundDustFX",-25,0,-(z-floorz),0)
"####" A 0 A_Jumpifinventory("DashBreaker",1,"AltFire.DashEnd")
"####" A 0 A_Jumpifinventory("TackleTimer",1,"AltFire.Dash")
goto AltFire.DashEnd

AltFire.DashEnd:
"####" B 0 A_ScaleVelocity(0.2)
"####" B 0 A_Jump(256,"AltFire.After")
Goto AltFire.After

AltFire.Pound:
TNT1 A 0 A_PlaySoundEx("weapon/ChargeKick","Weapon")
TNT1 A 0 A_FireCustomMissile("PoundMelee",0,0,0,0)
TNT1 A 0 A_Jump(256,"AltFire.After")
Goto AltFire.After

AltFire.Scratch:
TNT1 A 0 A_PlaySoundEx("weapon/SlashClaw","Weapon")
TNT1 A 0 A_FireCustomMissile("ScratchMelee",0,0,25,0,0,CallACS("PitchClamp",Pitch,55))
TNT1 A 0 A_FireCustomMissile("ScratchMelee",0,0,0,0,0,CallACS("PitchClamp",Pitch,55))
TNT1 A 0 A_FireCustomMissile("ScratchMelee",0,0,-25,0,0,CallACS("PitchClamp",Pitch,55))
TNT1 A 0 A_Jump(256,"AltFire.After")
Goto AltFire.After

AltFire.Jab:
TNT1 A 0 A_PlaySoundEx("weapon/SlashClaw","Weapon")
TNT1 A 0 A_FireCustomMissile("JabMelee",0,0,0,0)
TNT1 A 0 A_Jump(256,"AltFire.After")
Goto AltFire.After


AltFire.Lash:
TNT1 A 0 A_PlaySoundEx("misc/KingSwing","Weapon")
TNT1 A 17 A_FireCustomMissile("LashWatcher",0,0,0,0,0,0)
TNT1 A 0 A_Jump(256,"AltFire.After")
Goto AltFire.After

}
}

//G.Attack Growth rates
Const int GAG_Tackle = 10;

//[+]========================================================================[+]
//--------------------------------|Tackle|--------------------------------
//[+]========================================================================[+]

//#region 

actor TackleTimer : Powerup {inventory.amount 1 powerup.duration 5 }

Actor TackleDelay : powerprotection
{
Powerup.duration 5
Damagefactor "Tackle", 0
}

const int TCLK_Thrust = 55;

actor TackleBumper : BasicProjectile
{
Speed 55 
Damage (0)
radius 6
height 6
DamageType "Tackle"
scale 1.5
//+NOINTERACTION
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Terminate")
TNT1 A 1 
TNT1 A 1 A_Explode(MBP_StandardPower+CallACS("GenericAttackPower",5),RAMHITBOX,0,0,RAMHITBOX)
stop
Death:
TNT1 A 0 A_Explode(MBP_StandardPower+CallACS("GenericAttackPower",5),RAMHITBOX,0,0,RAMHITBOX)
TNT1 A 1 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_GiveToTarget("TackleCollision2",1)
TNT1 A 0 A_SpawnItemEx("BasicMeleeFX", 0, 0, 0, 0, 0, 0, 0,1)
Crash:
TNT1 A 0 A_Explode(MBP_StandardPower+CallACS("GenericAttackPower",5),RAMHITBOX,0,0,RAMHITBOX)
Terminate:
TNT1 A 1
stop
}
}

actor TackleWave
{
PROJECTILE
+FLOORCLIP
+THRUGHOST
+MTHRUSPECIES
+NOEXPLODEFLOOR
Radius 5
Height 5
scale 2.0
damage (0)
reactiontime 3
speed 0
States
{
Spawn:
BEWA B 0

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(ScaleX+0.3)
BEWA B 0 A_CountDown
loop
Death:
BEWA A 0
stop
}
}


actor TackleCollision : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_ScaleVelocity(0.2)
//TNT1 A 0 A_Stop
//TNT1 A 0 A_ChangeVelocity(cos(-pitch)*-35,0,sin(pitch)*35,CVF_RELATIVE)
//TNT1 A 0 ThrustThingZ(0,30,1,1)
stop
Nope:
TNT1 A 0
stop
}
}

actor TackleCollision2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_PlaySoundEx("move/Basic","SoundSlot5")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
stop
Nope:
TNT1 A 0
stop
}
}

actor TackleThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_PlaySoundEx("weapon/Trample","SoundSlot5")
TNT1 A 0 A_ChangeVelocity(TCLK_Thrust,0,Momz,CVF_RELATIVE|CVF_REPLACE)//cos(-pitch)* , sin(-pitch)*TCLK_Thrust
TNT1 A 1 A_FireCustomMissile("TackleBumper",0,0,0,15,0,Pitch)
//BASB A 1 A_SpawnitemEx("TackleWave",cos(-pitch)*30,0,(25),0,0,0,0,SXF_TRANSFERTRANSLATION) //+(sin(pitch)*30)
BASB A 1 A_SpawnItemEx("GroundDustFX", random(0,50), 0, 0, 0, 0, 0, random(0, 359))
stop
Nope:
TNT1 A 0
stop
}
}

actor TackleInitiate : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Spawnitemex("TackleWatcher")
stop
}
}

actor TackleWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Death")
TNT1 A 0 A_SetUserVar("user_DashTime",user_DashTime+1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 3 A_GiveToTarget("TackleThrust")
TNT1 A 0 A_JumpIfInTargetInventory("TackleTimer",1,"Spawn")
//TNT1 A 0 A_Jumpif(user_DashTime <= 6,"Spawn")
Goto Death
Check2:
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Spawn")
Goto Death
Death:
DeathTake:
TNT1 A 0 //A_TakeFromTarget("TackleActive",1)
stop
}
}

//#endregion

//[+]========================================================================[+]
//--------------------------------|Pound|--------------------------------
//[+]========================================================================[+]

//#region 


actor PoundMelee : BasicFastProjectile
{
Speed 48
Scale 5.0
DamageType "Melee"
Damage(MBP_StandardPower+CallACS("GenericAttackPower",5))
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 
TNT1 AAAA 1 A_SpawnItemEx("PoundTrail",-48,0,0,momx,momy,momz,0,9)
stop

Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("PoundFXTrail", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_SpawnItemEx("PoundFX1", 0, 0, -16, 0, 0, 0, 0,1)
TNT1 A 0 A_SpawnItemEx("PoundFX2", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 5
stop
}
}

actor PoundTrail : BasicExplosion
{
damagetype "Melee"
Args 10
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("PoundTrailFX1",0,0,0,0,0,0,0,9)
TNT1 A 0 A_SpawnItemEx("PoundTrailFX2",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("PoundTrailFX3",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("PoundTrailFX3",0,0,0,-momx*0.9,-momy*0.9,-momz*0.9,0,9)
stop
}
}

actor PoundTrailFX1 : BasicClientside
{
scale 2.5
speed 7
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
HARD C 1 
stop
}
}

actor PoundTrailFX2 : PoundTrailFX1
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
HARD D 1 
stop
}
}

actor PoundTrailFX3 : PoundTrailFX1
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
HARD E 1 A_Stop
HARD EEE 1 A_Fadeout(0.3)
stop
}
}

actor PoundFXTrail : BasicClientside
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
HARD CDE 1
HARD EEE 1 A_Fadeout(0.3)
stop
}
}


actor PoundFX1 : BasicClientside
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
NDFX A 5
NDFX AAAA 1 A_Fadeout(0.3)
stop
}
}

actor PoundFX2 : BasicClientside
{
scale 4.5
States
{
Spawn:
TNT1 A 0 
HITG FGH 1
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
stop
}
}
//#endregion


//[+]========================================================================[+]
//--------------------------------|Scratch|--------------------------------
//[+]========================================================================[+]

//#region 
actor ScratchMelee : BasicMeleeAttack
{
DamageType "Melee"
scale 1.5
Speed 48
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA GGGG 1 A_SpawnItemEx("ScratchTrail",-48,0,0,momx,momy,momz,0,9)
stop

Death:
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NDFX A 1 A_Explode(MBP_RapidLow+CallACS("GenericAttackPower",5),80,0,0,80)
NDFX A 1 A_SpawnItemEx("ScratchImpactFX",0,0,0,0,0,0,0,1)
stop
}
}

actor ScratchTrail : BasicExplosion
{
damagetype "Melee"
Args 10
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("ScratchTrailFX1",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("ScratchTrailFX2",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("ScratchTrailFX3",0,0,0,-momx*0.9,-momy*0.9,-momz*0.9,0,9)
stop
}
}

actor ScratchTrailFX1 : BasicClientside
{
scale 1.5
alpha 0.8
renderstyle "Translucent"
speed 7
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA G 1 
stop
}
}

actor ScratchTrailFX2 : ScratchTrailFX1
{
alpha 0.6
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA F 1 
stop
}
}

actor ScratchTrailFX3 : ScratchTrailFX1
{
alpha 0.4
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA E 1 
stop
}
}



actor ScratchImpactFX : BasicClientside
{
scale 2.0
States
{
Spawn:
TNT1 A 0
//MCLA ABCDE 1 
//MCLA E 5
//MCLA EZEZEZ 2
SCLA HIJKL 1
stop
}
}

//#endregion

//[+]========================================================================[+]
//--------------------------------|Jab|--------------------------------
//[+]========================================================================[+]

//#region 
actor JabMelee : BasicFastProjectile
{
Speed 80
Scale 5.0
DamageType "Melee"
Damage(MBP_StandardPower+CallACS("GenericAttackPower",5))
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 
NEDC AAA 1 A_SpawnItemEx("JabTrail",-48,0,0,momx,momy,momz,0,9)
stop

Death:
NEDC A 0 A_Stop
NEDC A 0 A_SpawnItemEx("JabFXTrail", 0, 0, 0, 0, 0, 0, 0,1)
NEDC A 0 A_GiveInventory("ProjectileDCode",1)
NEDC A 0 A_SpawnItemEx("JabFX", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 5
stop
}
}


actor JabTrail : BasicExplosion
{
damagetype "Melee"
Args 10
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("JabFXTrail1",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("JabFXTrail2",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("JabFXTrail3",0,0,0,-momx*0.9,-momy*0.9,-momz*0.9,0,9)
stop
}
}

actor JabFXTrail1 : BasicClientside
{
scale 5.0
Renderstyle "Translucent"
States
{
Spawn:
TNT1 A 0 
NEDC A 1
stop
}
}

actor JabFXTrail2 : JabFXTrail1{Alpha 0.7}
actor JabFXTrail3 : JabFXTrail1{Alpha 0.4}

actor JabFXTrail : BasicClientside
{
scale 5.0
States
{
Spawn:
TNT1 A 0 
NEDC A 1
NEDC AAA 1 A_Fadeout(0.3)
stop
}
}

actor JabFX : BasicClientside
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
NDFX A 1
NDFX AAAA 1 A_SetScale(ScaleX-0.5)
stop
}
}


//#endregion

//[+]========================================================================[+]
//--------------------------------|Lash|--------------------------------
//[+]========================================================================[+]

//#region 

actor LashProtect : PowerProtection
{
Powerup.Duration 10
damagefactor "Lash", 0.0
}

actor LashWatcher
{
    PROJECTILE
    +NOINTERACTION
    renderstyle none
    speed 32
    height 1
    radius 1
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
    Spawn1:
        TNT1 A 1 A_Warp(AAPTR_TARGET, cos(-pitch)*16,0,32+(sin(-pitch)*16), 0, WARPF_USECALLERANGLE)
        TNT1 A 0 A_SpawnItemEx("LashStart", 0, 0, 0, cos(pitch), 0, sin(-pitch), 90-(10*args[0]), SXF_MULTIPLYSPEED)
        //TNT1 A 0 A_SpawnItemEx("LashStart", 0, 0, 0, cos(pitch), 0, sin(-pitch), 80-(10*args[0]), SXF_MULTIPLYSPEED)
        //TNT1 A 0 A_SpawnItemEx("LashStart", 0, 0, 0, cos(pitch), 0, sin(-pitch), 70-(10*args[0]), SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("LashSlashFX1", 0, 0, 0, cos(pitch), 0, sin(-pitch), 90-(10*args[0]), SXF_MULTIPLYSPEED)
		TNT1 A 0 A_SetArg(0,args[0]+3)
		TNT1 A 0 A_JumpIf(args[0] < 18,"Spawn1")
		TNT1 A 0 A_SetArg(0,0)
        stop
    WindDown1:
        TNT1 A 0
        stop
    }
}


actor LashStart 
{
PROJECTILE
+RIPPER
damagetype "Lash"
damage (0)
height 5
radius 5
Speed 25
scale 4.5
XScale 6.0
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_Jumpif(pitch > 4,"Death")
TNT1 A 0 A_setPitch(Pitch+1)
TNT1 A 0 A_setScale(ScaleX-0.5,ScaleY-0.5)
TNT1 A 0 A_SpawnItemEx("LashStart",0,0,0,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPITCH|SXF_TRANSFERSCALE)
TNT1 A 1 A_SpawnItemEx("LashDamager",0,0,0,0,0,0,0,SXF_TRANSFERSCALE)
TNT1 A 0 A_SpawnItemEx("LashEdgeFX2",0,0,0,0,0,0,0,SXF_TRANSFERSCALE)
Goto Death

Death:
TNT1 A 0
stop
}
}

actor LashDamager : BasicProjectile
{
Speed 0
Damage (0)
radius 15
height 10
DamageType "Lash"
scale 1.5
+RIPPER
+SERVERSIDEONLY
States
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_Explode(MBP_StandardPower+CallACS("GenericAttackPower",5),64,0,0,64)
stop
}
}


actor LashEdgeFX : BasicNOTClientside
{
scale 2.5
XScale 4.0
Alpha 0.6
ReactionTime 8
renderstyle "Translucent"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
BASB U 1
Spawn2:
BASB U 1  A_CountDown
BASB U 0 A_Fadeout(0.3)
loop

Death:
TNT1 A 0
stop
}
}


actor LashEdgeFX2 : LashEdgeFX {ReactionTime 1 Alpha 1.0}

actor LashSlashFX1 : BasicHelper
{
+MISSILE
+CLIENTSIDEONLY
reactiontime 1
var int user_angle;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
TNT1 A 0 A_SetUserVar("user_angle", 10)
SpawnLoop:
"####" B 0 A_SpawnItemEx("LashEdgeFX", cos(pitch)*50, 0, 12-(sin(pitch)*150), 0, 0, 0, user_angle, SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH)
"####" B 0 A_SpawnItemEx("LashEdgeFX", cos(pitch)*75, 0, 12-(sin(pitch)*125), 0, 0, 0, user_angle, SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH)
"####" B 0 A_SpawnItemEx("LashEdgeFX", cos(pitch)*100, 0, 12-(sin(pitch)*200), 0, 0, 0, user_angle, SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH)
TNT1 A 0 A_SetUserVar("user_angle", user_angle - 5)
TNT1 A 0 A_Countdown
loop
Death:
TNT1 A 0
stop
}
}


actor LashImpactPuff : BasicClientside
{
scale 4.5
States
{
Spawn:
TNT1 A 0 
HITG FGH 1 
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
TNT1 A 6
stop
}
}

