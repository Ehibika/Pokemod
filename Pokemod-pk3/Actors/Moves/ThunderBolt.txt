actor ThunderboltMove : WepMasterBase
{
Tag "Thunderbolt"
obituary "$OB_Thunderbolt"
Weapon.AmmoUse 1
Weapon.AmmoUse2 2
Weapon.AmmoGive 20
weapon.ammotype "ThunderboltPP"
weapon.ammotype2 "ThunderboltPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo


Fire.Begin:
"####" B 0
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
"####" B 0 A_GunFlash("Flash.Sound")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet: //Take Ammo
"####" A 0 A_Jumpifinventory("ConShotTimer",1,"Fire.Bullet1")
"####" B 0 A_JumpIfNoAmmo("Fire.After2")
"####" B 0 A_FireCustomMissile("MoveThunderBoltDamager",0,1,0,0,0,0)
"####" B 0 A_GiveInventory("ThunderboltCST",1)
"####" B 0 A_Jump(256,"Fire.Bullet2")
Goto Fire.Bullet2

Fire.Bullet1: //Free
"####" B 0 A_FireCustomMissile("MoveThunderBoltDamager",0,0,0,0,0,0)
"####" B 0 A_Jump(256,"Fire.Bullet2")
Goto Fire.Bullet2

Fire.Bullet2:
"####" B 3 A_FireCustomMissile("MoveThunderBoltBurstStart",0,0,0,0,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After


Fire.After:
"####" C 0 A_REfire("Fire")
"####" C 0 //A_StopSoundEX("Weapon")
Fire.After2:
"####" B 0 A_TakeInventory("BasicWarperHold",99)
"####" B 0 A_TakeInventory("ConShotTimer",99)
"####" B 0 A_TakeInventory("WeaponCharge",999)
"####" B 1 A_GiveInventory("5Recovery_A")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End


AltFire:
"####" B 0 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==0,"NoAmmo")
"####" B 0 A_Jump(256,"AltFire.Aim")
Goto AltFire.Aim


AltFire.Aim:
"####" B 1 A_FireCustomMissile("ThunderBoltRingSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_REfire("AltFire.Aim")
"####" B 0 A_Jump(256,"AltFire.Bullet")
Goto AltFire.Bullet

AltFire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_FireCustomMissile("ThunderBoltSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"AltFire.After")
Goto AltFire.After

AltFire.After:
"####" B 1 A_GiveInventory("FullRecovery_A")
"####" C 0 A_REfire("AltFire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End



Flash.Sound:
TNT1 A 3 A_PlaySoundEx("weapon/electricshock","Weapon")
TNT1 A 0 A_Jumpifinventory("WeaponCharge",25,"Flash.Sound")
Stop
}
}

actor ThunderboltMove1 : ThunderboltMove {Weapon.SlotNumber 1}
actor ThunderboltMove2 : ThunderboltMove {Weapon.SlotNumber 2}
actor ThunderboltMove3 : ThunderboltMove {Weapon.SlotNumber 3}
actor ThunderboltMove4 : ThunderboltMove {Weapon.SlotNumber 4}

actor ThunderboltMove5 : ThunderboltMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("ThunderboltMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor ThunderboltPP : Ammo {inventory.amount 1 inventory.Maxamount 20}

Actor DoubleThunderbolt : Powerup{inventory.amount 1 powerup.duration 0x7FFFFFFF}


Actor ThunderboltProtect : powerprotection
{
Powerup.duration 8
Damagefactor "Thunderbolt", 0
}

actor ThunderboltCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 45
powerup.type "ConShotTimer"
}

actor ThunderBoltParaChance : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jump(24,"Paralyse")
stop
Paralyse:
TNT1 A 0 A_GiveInventory("Zap_ParalysisStatusProc2")
stop

Nope:
TNT1 A 0
stop
}
}


Const int ThunderBoltRad = 125;
Const int ThunderBoltRad2 = 50;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

//#region -=- Main Attack -=-

Const Int ThunderboltRange = 80;


actor MoveThunderBoltBurstStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("MoveThunderBoltBurst",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor MoveThunderBoltBurst : BasicFastProjectile//FastProj to match damager hitbox
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    RenderStyle None
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > ThunderboltRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("MoveThunderBoltBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("MoveThunderBoltFX",0,frandom(-9,9),frandom(-9,9),0,0,0,0,1)
        TNT1 A 0 A_SpawnItemEx("MoveThunderBoltBurst2",0,frandom(-16,16),frandom(-16,16),0,0,0,0,1)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 A_SpawnItemEx("MoveThunderBoltEndFX",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("MoveThunderBoltEndFX",0,0,0,0,0,0,0,1)
        stop
    }
}

actor MoveThunderBoltFX : BasicGraphicEffect
{
    +FORCEXYBILLBOARD
    +BRIGHT
    States
    {
    Spawn:
        ESPA Q 0
        ESPA Q 0 A_Jump(256,1,4)
        ESPA QRS 1
        stop
        ESPA XWV 1
        stop
    }
}

actor MoveThunderBoltEndFX : MoveThunderBoltFX
{
    States
    {
    Spawn:
        ESPA ZYZY 1
        ESPA Z 0 A_SetScale(1.25)
        ESPA ZY 1
        stop
    }
}

actor MoveThunderBoltBurst2
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    +BRIGHT
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(random(1,4) >= 4,"SpawnReal")
        stop
    SpawnReal:
        TNT1 A 10 A_SetTics(random(2,4)*3)
        TNT1 A 0 A_PlaySoundEx("weapon/electricshockspark","Auto")
        TNT1 A 0 A_SetPitch(random(12,18))
        TNT1 A 0 A_SpawnItemEx("MoveThunderBoltBurst3",0,0,0,0,0,0,0,544+1)//512+32
        stop
    Death:
        TNT1 A 0
        stop
    }
}

actor MoveThunderBoltBurst3 : MoveThunderBoltBurst2
{
    +BRIGHT
    Scale 0.5
    States
    {
    Spawn:
        ESPA T 0
        ESPA T 0 A_JumpIf(Pitch < 2,"Death")
        ESPA T 0 A_SetPitch(Pitch-1)
        ESPA T 0 A_SpawnItemEx("MoveThunderBoltBurst3",-2*random(0,1)+1, -2*random(0,1)+1, -2*random(0,1)+1,0,0,0,0,544+1)//512+32
        ESPA T 0 A_SetTics(random(0,2))
        ESPA T 0 A_Jump(256,1,2)
        ESPA T 1
        stop
        ESPA U 1
        stop
    }
}

actor MoveThunderBoltDamager : BasicFastProjectile
{
    PROJECTILE
    damagetype "Thunderbolt"
    Obituary "$OB_THUNDERBOLT"
    +SKYEXPLODE
    +EXPLODEONWATER
    RenderStyle None
    Damage (MBP_Thunderbolt1) 
    Radius 10
    Height 10
    speed 1200 //15 * ThunderboltRange(80)
    scale 2.5
    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1 A_Stop
        TNT1 A 0
        stop
    }
}

//#endregion

//[+]========================================================================[+]
//--------------------------------|Alt Attack |--------------------------------
//[+]========================================================================[+]

actor ThunderBoltRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("ThunderBoltGuideDot", ThunderBoltRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("ThunderBoltGuideDot", -ThunderBoltRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)

		//TNT1 A 0 A_SpawnItemEx("ThunderBoltGuideDot2", ThunderBoltRad2, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		//TNT1 A 0 A_SpawnItemEx("ThunderBoltGuideDot2", -ThunderBoltRad2, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor ThunderBoltRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 350
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("ThunderBoltRingGuide",0,0,0,0,0,0,0,1)
stop
}
}

actor ThunderBoltGuideDot : BasicGuideDot {}
actor ThunderBoltGuideDot2 : BasicGuideDot {scale 0.5}

actor ThunderBoltSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 350
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)

Spawn2:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 2 //A_RadiusGive("ThunderBolt_SweetSpot",ThunderBoltRad2,RGF_PLAYERS)
TNT1 A 0 A_SpawnItemEx("TBolt_Bolt",0,0,0,0,0,0,0,1)
TNT1 A 5
stop
}
}

actor TBolt_Bolt : LightningBolt
{
ReactionTime 1
DamageType "ThunderBolt"
obituary "$OB_ThunderBolt"
+OLDRADIUSDMG
states
{
Spawn:
Y_BC N 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 8
Y_BC N 1 A_SpawnItemEx("TBolt_Extend",0,0,355,0,0,0,0,1)
Y_BC N 0 A_PlaySoundEx("weapon/thunderbolt","Weapon")
Y_BC N 0 A_Explode(MBP_Thunderbolt2,ThunderBoltRad,0,0,ThunderBoltRad2)
Y_BC O 1
Y_BC P 0 A_SpawnItemEx("TBolt_BoltFX",Random(32,96),Random(32,96),32,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("TBolt_BoltFX",Random(-32,-96),Random(-32,-96),64,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("TBolt_BoltFX",Random(32,96),Random(32,96),96,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("TBolt_BoltFX",Random(-32,-96),Random(-32,-96),128,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("TBolt_BoltFX",Random(32,96),Random(32,96),160,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("TBolt_BoltFX",Random(-32,-96),Random(-32,-96),192,0,0,0,0,1)
Y_BC PQR 1
stop
}
}



Actor TBolt_Extend : LightningExtend
{
States
{
Spawn:
Y_BC S 0 
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
Y_BC S 0 A_JumpIf(ceilingz-z <= 32,"Death")
Y_BC S 0 A_SpawnItemEx("TBolt_Extend",0,0,160,0,0,0,0,1)
Y_BC SSTUV 1
stop
}
}


actor TBolt_BoltFX : BasicClientSide
{
Scale 2.0
States
{
Spawn:
Y_BC K 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
Y_BC KKLLMM 1 A_ScaleVelocity(frandom(1.025,1.100))
stop
}
}
