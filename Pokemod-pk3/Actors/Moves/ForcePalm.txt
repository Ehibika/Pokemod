actor ForcePalmMove : WepMasterBase
{
Tag "ForcePalm"
obituary "$OB_ForcePalm"
Weapon.AmmoUse 1
Weapon.AmmoGive 35
weapon.ammotype "ForcePalmPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop

Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Melee")
Goto Fire.Melee

Fire.Melee:
TNT1 A 0 A_PlaySoundEx("misc/Clap","Weapon")
TNT1 A 0 A_PlaySoundEx("weapon/napalm","SoundSlot6")
"####" B 0 A_FireCustomMissile("ForcePalmDamager",0,1,0,0,0,0)
"####" B 3 A_FireCustomMissile("ForcePalmStart",0,0,0,0,0,0)
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("ForcePalm_Recovery_C")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor ForcePalmMove1 : ForcePalmMove {Weapon.SlotNumber 1}
actor ForcePalmMove2 : ForcePalmMove {Weapon.SlotNumber 2}
actor ForcePalmMove3 : ForcePalmMove {Weapon.SlotNumber 3}
actor ForcePalmMove4 : ForcePalmMove {Weapon.SlotNumber 4}

actor ForcePalmMove5 : ForcePalmMove 
{
Weapon.SlotNumber 5
States
{
Fire:
TNT1 A 0 A_PlaySoundEx("weapon/SlashClaw","Weapon")
TNT1 A 0 A_FireCustomMissile("ForcePalmMelee",0,1,0,0)
Goto Fire.End

Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("ForcePalmMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor ForcePalmPP : Ammo {inventory.amount 1 inventory.Maxamount 35}

actor ForcePalmParaChance : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jump(64,"Paralyse")
stop
Paralyse:
TNT1 A 0 A_GiveInventory("ParalysisStatusProc2")
stop

Nope:
TNT1 A 0
stop
}
}

Actor ForcePalmProtect : powerprotection
{
Powerup.duration 17
Damagefactor "ForcePalm", 0
}

actor ForcePalm_Recovery_C : FullRecovery_C{Powerup.Duration 52} //87

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
Const Int ForcePalmRange = 80;
actor ForcePalmDamager : BasicFastProjectile
{
    PROJECTILE
    damagetype "ForcePalm"
    Obituary "$OB_ForcePalm"
    +SKYEXPLODE
    +EXPLODEONWATER
    RenderStyle None
    Damage (0) 
    Radius 10
    Height 10
    speed 1200 //15 * ForcePalmRange(80)
    scale 2.5
    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1 A_Stop
        TNT1 A 0
        stop
		Death:
		TNT1 A 1 A_Explode(MBP_ForcePalm,32,0,0,32)
		Stop
    }
}


actor ForcePalmStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,15,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("ForcePalmBeam",0,0,0,momx,momy,momz,0,8)
		TNT1 A 0 A_SpawnItemEx("ForcePalmFX",0,0,0,0,0,0,0,1061) //1061
        stop
    }
}

actor ForcePalmBeam : BasicFastProjectile//FastProj to match damager hitbox
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    RenderStyle None
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > ForcePalmRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("ForcePalmBeam",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("ForcePalmRayFX",0,0,0,0,0,0,0,1)
        TNT1 A 0 A_SpawnItemEx("ForcePalmRayFX2",0,0,0,random2(2),random2(2),random2(2),0,1)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 A_SpawnItemEx("ForcePalmFX",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("ForcePalmFX",0,0,0,0,0,0,0,1)
        stop
    }
}


actor ForcePalmMelee : BasicMeleeAttack
{
Speed 44
Scale 0.5
+RIPPER
DamageType "ForcePalm"
Obituary "$OB_ForcePalm"
Damage(0)
ReactionTime 6
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 1 A_SpawnItemEx("ForcePalmFXTrail", 0, 0, 0, 0, 0, 0, 0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE)
TNT1 A 0 A_Explode(MBP_ForcePalm,64,0,0,64)
TNT1 A 0 A_SetScale(Scalex+0.3)
TNT1 A 0 A_CountDown
loop

Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 AAA 3 A_SpawnItemEx("ForcePalmFX", 0, 0, 0, 0, 0, 0, 0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE)
TNT1 A 5
stop
}
}


actor ForcePalmFXTrail : BasicClientside
{
scale 5.0
renderstyle "add"
States
{
Spawn:
TNT1 A 0 
BGHF ABC 1
BGHF CCCCC 1 A_Fadeout(0.3)
stop
}
}

actor ForcePalmFX : BasicClientside
{
scale 3.5
renderstyle "add"
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
BGHF ABC 1
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
stop
}
}

actor ForcePalmRayFX : BasicClientside
{
scale 2.0
renderstyle "Add"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
HARD BCD 1
stop
}
}

actor ForcePalmRayFX2 : BasicClientside
{
scale 2.0
renderstyle "Add"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
BGHF CBA 1
BGHF A 1 A_SetScale(ScaleX-0.5)
BGHF A 0 A_Fadeout(0.3)
BGHF A 1 A_SetScale(ScaleX-0.5)
BGHF A 0 A_Fadeout(0.3)
BGHF A 1 A_SetScale(ScaleX-0.5)
BGHF A 0 A_Fadeout(0.3)
stop
}
}