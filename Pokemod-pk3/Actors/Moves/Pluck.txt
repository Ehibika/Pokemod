actor PluckMove : WepMasterBase
{
Tag "Pluck"
obituary "$OB_PLUCK"
Weapon.AmmoUse 1
Weapon.AmmoGive 30
weapon.ammotype "PluckPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Dash")
Goto Fire.Dash

Fire.Dash:
"####" A 0 A_GiveInventory("DashSet_B")
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_Pluck,1)
"####" B 0 A_PlaySoundEx("weapon/ChargeKick","Soundslot6")
"####" A 0 A_GiveInventory("PluckInitiate")
"####" A 0 A_FireCustomMissile("GroundDustFX",0,0,0,0)
"####" AAAAAAA 1 A_Jumpifinventory("DashBreaker",1,"Fire.DashEnd")
"####" B 0 A_ScaleVelocity(0.2)
goto Fire.DashEnd

Fire.DashEnd:
"####" B 0 A_GiveInventory("DashSetOff")
"####" B 1 A_GiveInventory("SemiRecovery_B")
"####" B 1 A_GiveInventory("MoveCooldown_1Half")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor PluckMove1 : PluckMove {Weapon.SlotNumber 1}
actor PluckMove2 : PluckMove {Weapon.SlotNumber 2}
actor PluckMove3 : PluckMove {Weapon.SlotNumber 3}
actor PluckMove4 : PluckMove {Weapon.SlotNumber 4}

actor PluckMove5 : PluckMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("PluckMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor PluckPP : Ammo {inventory.amount 1 inventory.Maxamount 15}
Actor PluckDelay : powerprotection
{
Powerup.duration 5
Damagefactor "Pluck", 0
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor PluckBumper : BasicProjectile
{
Speed 80
Damage (0)
radius 12
height 12
DamageType "Pluck"
Obituary "$OB_PLUCK"
scale 1.5
//+NOINTERACTION
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Terminate")
TNT1 A 1 
TNT1 A 1 A_Explode(MBP_Pluck,RAMHITBOX,0,0,RAMHITBOX)
stop

Death:
XDeath:
TNT1 A 0 A_Explode(MBP_Pluck,RAMHITBOX,0,0,RAMHITBOX)
TNT1 A 1 A_GiveToTarget("PluckCollision2",1)
stop

Crash:
TNT1 A 0 A_Explode(MBP_Pluck,RAMHITBOX,0,0,RAMHITBOX)
Terminate:
TNT1 A 1
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor PluckWave
{
PROJECTILE
+FLOORCLIP
+THRUGHOST
+MTHRUSPECIES
+NOEXPLODEFLOOR
Radius 5
Height 5
scale 2.0
damage (0)
reactiontime 5
DamageType "Pluck"
obituary "$OB_Pluck"
speed 0
States
{
Spawn:
BEWA B 0

DRLP A 2 A_Fadeout
DRLP A 0 A_SetScale(2.6,2.6)

DRLP B 2 A_Fadeout
DRLP B 0 A_SetScale(2.8,2.8)

DRLP C 2 A_Fadeout
DRLP C 0 A_SetScale(3.0,3.0)

DRLP D 2 A_Fadeout
DRLP D 0 A_SetScale(3.2,3.2)

DRLP A 2 A_Fadeout
DRLP A 0 A_SetScale(2.6,2.6)

DRLP B 2 A_Fadeout
DRLP B 0 A_SetScale(2.8,2.8)
stop
Death:
BEWA A 0
stop
}
}


actor PluckCollision : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*-35,0,sin(pitch)*35,CVF_RELATIVE)
TNT1 A 0 ThrustThingZ(0,30,1,1)
stop
Nope:
TNT1 A 0
stop
}
}

actor PluckCollision2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
stop
Nope:
TNT1 A 0
stop
}
}

actor PluckThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*80,0,sin(-pitch)*80,CVF_RELATIVE|CVF_REPLACE)
TNT1 A 1 A_FireCustomMissile("PluckBumper",0,0,0,15)
BASB A 1 A_SpawnitemEx("PluckWave",cos(-pitch)*30,0,(25)+(sin(pitch)*30),0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
Nope:
TNT1 A 0
stop
}
}

actor PluckInitiate : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Spawnitemex("PluckWatcher")
stop
}
}

actor PluckWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Death")
TNT1 A 0 A_SetUserVar("user_DashTime",user_DashTime+1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1 A_GiveToTarget("PluckThrust")
TNT1 A 0 A_Jumpif(user_DashTime <= 6,"Spawn")
Goto Death
Check2:
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Spawn")
Goto Death
Death:
DeathTake:
TNT1 A 0 //A_TakeFromTarget("PluckActive",1)
stop
}
}