actor FutureSightMove : WepMasterBase
{
Tag "FutureSight"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "FutureSightPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("FutureSightRingSpawner",0,0,0,Aim_OverHead)
//"####" B 0 A_FireCustomMissile("FutureSightGraphicSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/Usemove","Weapon")
"####" B 0 A_FireCustomMissile("FutureSightSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("FullRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End


AltFire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Altfire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Altfire.Begin:
"####" B 1
"####" C 0 A_REfire("Altfire.Begin")
"####" A 0 A_GiveInventory("RemoteMoveLock")
"####" A 0 A_GiveInventory("FutureSightRCFlag")
"####" A 0 A_Stop
"####" A 0 A_SpawnItemEX("FutureSight_Remote")
"####" B 0 A_Jump(256,"Altfire.Remote")
Goto Altfire.Remote


Altfire.Remote:
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Altfire.After")
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_RELOAD)==1,"Altfire.RemoteReload")
"####" B 0 A_JumpifInventory("FutureSightRCFlag",1,"Altfire.Remote")
Goto Altfire.After
Altfire.RemoteReload:
TNT1 A 0 A_GiveInventory("PPRecoveryItem") 
Goto Altfire.Remote

Altfire.After:
"####" B 1 A_GiveInventory("FutureSight_Recovery_C")
"####" A 0 A_GiveInventory("RemoteMoveUnLock")
Altfire.After2:
"####" B 1
"####" B 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Altfire.After2")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor FutureSightMove1 : FutureSightMove {Weapon.SlotNumber 1}
actor FutureSightMove2 : FutureSightMove {Weapon.SlotNumber 2}
actor FutureSightMove3 : FutureSightMove {Weapon.SlotNumber 3}
actor FutureSightMove4 : FutureSightMove {Weapon.SlotNumber 4}

actor FutureSightMove5 : FutureSightMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("FutureSightMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor FutureSightPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor FutureSightRCFlag : Powerup {inventory.amount 1 powerup.duration 35 }

Const int FutureSightRad = 150;
Const int FutureSightEntiTID = 11;
Const int FutureSight_Forward = 150;
Const int FutureSight_Range = 3000;

actor FutureSightPPTaker : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_FutureSight,1)
stop
}
}

actor FutureSight_Recovery_C : FullRecovery_C{Powerup.Duration 52}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor FutureSight_Remote 
{
Speed 5
+MISSILE
+THRUACTORS
+NOCLIP
+FLOORHUGGER
+NOEXPLODEFLOOR
//+SLIDESONWALLS
//+BOUNCEONWALLS 
+NOGRAVITY
Health 5
Damage (0)
DamageType "FutureSight"
obituary "$OB_FutureSight"
radius 5
Reactiontime 50
height 16
scale 3.5
Args 0, 60
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 ACS_NamedExecuteAlways("CamGrabObject",0)
TNT1 A 0 ACS_NamedExecuteAlways("RemoteControlActor",0,15.0,FutureSight_Range)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_DEFAULT)
Spawn2:
TNT1 A 1 A_JumpIfCloser(FutureSight_Range-1,"Spawn3")
TNT1 A 0 A_SpawnItemEx("FutureSightRingGuide2",FutureSight_Forward,0,0,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("FutureSightRCFlag",1)
TBLA A 0
loop
Spawn3:
TNT1 A 1 A_SpawnItemEx("FutureSightRingGuide",FutureSight_Forward,0,0,0,0,0,0,1)

TNT1 A 0 A_GiveToTarget("FutureSightRCFlag",1)
TBLA A 0
goto Spawn2

Trigger: //Check PP
TNT1 A 0 A_JumpifInTargetInventory("FutureSightPP",1,"Trigger2")
goto Decline
Trigger2: // Check Recovery Frame
TNT1 A 0 A_JumpifInTargetInventory("Recovery_C",1,"Decline")
goto Detonate

Detonate: // Cast the attack!
TNT1 A 0 A_GiveToTarget("FutureSightPPTaker",1)
TNT1 A 0 A_SpawnItemEx("FutureSightTrap",FutureSight_Forward,0,-(z-floorz)+60,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("FutureSight_Recovery_C",1)
goto PostTrigger

Decline: //Cannot cast the attack
TNT1 A 0 A_GiveToTarget("DeclineSoundCue",1)
goto PostTrigger

PostTrigger:
TNT1 AAAAA 1 A_SpawnItemEx("FutureSightRingGuide",FutureSight_Forward,0,0,0,0,0,0,1)
Goto Spawn2

Death:
BSTG AAAAAA 0 A_SpawnItemEx("RazorLeafParticle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
SPKH ABCD 2
stop
}
}


actor FutureSightRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("FutureSightGuideDot", FutureSightRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("FutureSightGuideDot", -FutureSightRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor FutureSightRingGuide2 : BasicHelper
{
	+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("FutureSightGuideDot2", FutureSightRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("FutureSightGuideDot2", -FutureSightRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor FutureSightRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 800
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("FutureSightRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}


actor FutureSightGuideDot : BasicGuideDot {}
actor FutureSightGuideDot2 : BasicGuideDot {StencilColor "#F42A0C"}

actor FutureSightSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 800
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("FutureSightTrap",0,0,-(z-floorz),0,0,0,0,1)
TNT1 A 1
stop

Terminate:
TNT1 A 0 
stop
}
}

actor FutureSightTrap : BasicHelper
{
DamageType "FutureSight"
obituary "$OB_FutureSight"
scale 3.5
ReactionTime 5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,4,3)
TNT1 A 0 A_PlaySoundEx("Move/FutureSight","voice")
TNT1 AAA 5 A_SpawnItemEx("FutureSight_FX2",0,0,-(z-floorz)+24,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 5 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_DEFAULT)

Scan:
TNT1 A 0 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
Scan1:
TNT1 A 0 A_SetArg(0,Args[0]+1)
TNT1 A 4 A_Jumpif(Args[0]>5,"Scan2")
TNT1 A 1  A_LookEx(LOF_NOSOUNDCHECK, 1, FutureSightRad, FutureSightRad, 360, "Targetcheck")
Goto Scan1
Scan2: //Blink
TNT1 A 0 A_SpawnItemEx("FutureSight_FX",0,0,-(z-floorz)+24,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetArg(0,0)
Goto Scan1
Targetcheck:
TNT1 A 1 A_Jumpifcloser(FutureSightRad,"Targetcheck2")
Goto Scan
Targetcheck2:
TNT1 A 1 A_Jumpif(CallACS("TrapSensorIdentify")==1,"Scan") //You Are A Teammate
Goto Explode

Explode:
TNT1 A 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
TNT1 A 0 A_SpawnItemEx("FutureSightHitbox",0,0,-(z-floorz)+60,0,0,0,0,1)

Death:
SEDG D 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 3
stop
}
}


actor FutureSightHitbox : BasicExplosion2
{
Damage(0)
DamageType "FutureSight"
obituary "$OB_FutureSight"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NAPC A 0 A_PlaySoundEx("Move/Confusion", "Weapon")
TNT1 A 2 A_SpawnItemEx("ConfusionFX_A1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("ConfusionFX_A2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 8

TNT1 A 0 A_SpawnItemEx("ConfusionFX_B",250,0,0,-50,0,0,45+(90*0),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("ConfusionFX_B",250,0,0,-50,0,0,45+(90*1),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("ConfusionFX_B",250,0,0,-50,0,0,45+(90*2),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("ConfusionFX_B",250,0,0,-50,0,0,45+(90*3),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 5
NAPC A 0 A_PlaySoundEx("Move/Extrasensory", "Weapon")
TNT1 A 0 A_Explode(MBP_FutureSight,FutureSightRad,0,0,FutureSightRad)
//NAPC A 0 A_PlaySoundEx("weapon/Napalm", "Weapon")
TNT1 A 0 A_Quake(4,12,0,350)
TNT1 AAA 3 A_SpawnItemEx("ConfusionFX_C",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 35 
NAPC A 0 A_StopSoundEx("Weapon")
stop

Death:
TNT1 A 0 
stop
}
}

actor FutureSight_FX : BasicClientside
{
scale 0.5
Renderstyle "Add"
ReactionTime 10
speed 7
States
{
Spawn:
TNT1 A 0
PSIK A 1
RFLC A 0 A_SetScale(ScaleX+0.05)
RFLC A 0 A_Fadeout
RFLC A 0 A_CountDown
loop

Death:
RFLC A 0
stop
}
}

actor FutureSight_FX2 : BasicClientside
{
scale 2.0
reactiontime 3
Renderstyle "Add"
States
{
Spawn:
TNT1 A 0 
PSIK BCDE 1 A_SetScale(ScaleX-0.15)
TNT1 A 0 A_CountDown
loop
}
}