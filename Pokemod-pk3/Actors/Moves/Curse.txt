actor CurseMove : WepMasterBase
{
Tag "Curse"
obituary "$OB_CURSE"
Weapon.AmmoUse 1
Weapon.AmmoGive 25
weapon.ammotype "CursePP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_JumpIfInventory("GhostSTAB",1,"Fire.Ghost") //Ghost curse
"####" B 0 A_JumpIfInventory("STF_Curse",1,"Fire.Off")
"####" B 0 A_Jump(256,"Fire.On")
Goto Fire.On

Fire.On:
"####" B 0 A_PlaySoundEx("Move/Curse1","Soundslot6")
"####" BBBBBB 3 A_GiveInventory("CurseParticleSpawn")
"####" B 0 A_PlaySoundEx("Support/Statup","Soundslot7")
"####" B 0 A_GiveInventory("CurseBuffInitiate")
"####" B 0 A_TakeInventory("CursePP",1)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Off:
"####" B 0 A_TakeInventory("Curse_Active",99)
"####" B 0 A_GiveInventory("PartRecovery_C")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Ghost:
"####" B 1 A_JumpIf(IsPointerEqual(AAPTR_TRACER,AAPTR_NULL) == true, "Fire.GhostFail")
"####" B 0 A_PlaySoundEx("Misc/UseMove","Soundslot7")
"####" B 2 A_FireCustomMissile("GhostCurseSeeker",0,1,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.GhostFail:
"####" B 0 A_GiveInventory("CurseFX3_Spawn",1)
"####" B 0 A_Jump(256,"NoAmmo2")
Goto NoAmmo2

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor CurseMove1 : CurseMove {Weapon.SlotNumber 1}
actor CurseMove2 : CurseMove {Weapon.SlotNumber 2}
actor CurseMove3 : CurseMove {Weapon.SlotNumber 3}
actor CurseMove4 : CurseMove {Weapon.SlotNumber 4}

actor CurseMove5 : CurseMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("CurseMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
Const int CURSE_COST = 50;
Const int CURSE_POWER = 25;

actor CursePP : Ammo {inventory.amount 1 inventory.Maxamount 25}

//#region -=- normal curse -=-
actor STF_Curse : BaseSTatusFlag2 {}
actor Curse_Active : Inventory {inventory.amount 1 inventory.Maxamount 1}

actor G_cursed : Inventory {inventory.amount 1 inventory.Maxamount 1}


actor CurseBuffInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("Curse_Active",1,"Sustain")
TNT1 A 0 A_GiveInventory("Curse_Active",1)
TNT1 A 0 A_SpawnitemEX("CurseBuffWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_Curse",1)
stop
Nope:
TNT1 A 0
stop
}
}


actor CurseBuffWatcher : BasicHelper 
{
Var int user_TickTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("HazeFlag",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("TauntFlag",1,"Death")
TNT1 A 5 A_GiveToTarget("STF_Curse")
TNT1 A 0 A_GiveToTarget("SPDUpFXSpawn")
TNT1 A 0 A_SetUserVar("user_TickTime",user_TickTime+5)
TNT1 A 0 A_JumpIf(user_TickTime>=35,"SpawnTick")
TNT1 A 0 A_JumpIfInTargetInventory("Curse_Active",1,"Spawn")
Goto Death

SpawnTick:
TNT1 A 0 A_SetUserVar("user_TickTime",0)
TNT1 A 0 A_TakeFromTarget("CursePP",1)
TNT1 A 0 A_JumpIfInTargetInventory("CursePP",1,"Spawn")
Goto Death

Death:
TNT1 A 0 A_GiveToTarget("StatUpdator")
TNT1 A 0 A_GiveToTarget("BuffLossSoundCue")
TNT1 A 0 A_TakeFromTarget("STF_Curse",9)
TNT1 A 0 A_TakeFromTarget("Curse_Active",99)
TNT1 A 2 
stop
}
}


actor CurseParticle : BasicClientside
{
scale 3.5
speed 7
States
{
Spawn:
TNT1 A 0
GHOL A 5
GHOL AAAA 2 A_Fadeout(3.0)
stop
}
}

actor CurseParticleSpawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 AA 0 A_SpawnItemEx ("CurseParticle", 40, 0, 0, 0, 0, 20, random(0,360),SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 AA 0 A_SpawnItemEx ("CurseParticle", 40, 0, 120, 0, 0, -20, random(0,360),SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
stop
Nope:
TNT1 A 0
stop
}
}

//#endregion

//#region -=- Ghost Curse -=-

actor GhostCurseSeeker : BasicExplosion
{
Damage(0)
DamageType "Curse"
obituary "$OB_Curse"
Reactiontime 17
Args 6, 0, 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
//==
TNT1 A 1 
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",AAPTR_TRACER,STATUS_MINOR),"Death") //Blocked by protect|Detect|Substitute|Safeguard
TNT1 A 0 A_JumpIfInventory("G_cursed",1,"Death",AAPTR_TRACER)
TNT1 A 1 A_JumpIfInventory("MegaDrainProtect",1,"Death",AAPTR_TRACER)
TNT1 A 1 A_JumpIf(IsPointerEqual(AAPTR_TRACER,AAPTR_NULL) == False, "PreCurse")
Goto Death

PreCurse:
TNT1 A 8 A_GiveInventory("CurseFX1_Spawn",1,AAPTR_TARGET)
TNT1 A 1 A_GiveInventory("CurseSelfHarm",1,AAPTR_TARGET)
TNT1 A 0 A_GiveInventory("CurseFX2_Spawn",1,AAPTR_TRACER)
TNT1 A 0 A_ChangeFlag("NOINTERACTION",True)
TNT1 A 0 A_ChangeFlag("NOCLIP",True)
TNT1 A 0 A_ChangeFlag("DOOMBOUNCE",False)
TNT1 A 0 //A_SetArg(2,CallACS("LeechSeedHPGet",MBP_LeechSeed))
TNT1 A 0 A_SetArg(2,CallACS("PKMNACS_HealthPercent",AAPTR_TRACER,1,CURSE_POWER))
TNT1 A 0 A_SetArg(1,10)
Goto GCurse

GCurse:
TNT1 A 0 A_GiveInventory("G_cursed",1,AAPTR_TRACER)
TNT1 A 7 
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Death",AAPTR_TRACER)
TNT1 A 0 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
TNT1 A 0 A_SetArg(1,Args[1]-1)
TNT1 A 0 A_JumpIf(Args[1] > 0, "GCurse")

TNT1 A 8 A_GiveInventory("CurseFX1_Spawn",1,AAPTR_TRACER)
TNT1 A 0 A_Warp(AAPTR_TRACER,1,0,45,0,WARPF_COPYINTERPOLATION)
TNT1 A 0 A_Explode(Args[2],35,0,0,35)
TNT1 A 1
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Death",AAPTR_TRACER)
TNT1 A 0 A_JumpIfInventory("IsDead",1,"Death",AAPTR_TRACER)
TNT1 A 0 A_SetArg(1,10)
TNT1 A 0 A_SetArg(0,Args[0]-1)
TNT1 A 0 A_JumpIf(Args[0] > 0, "GCurse")
Goto Death

Death:
TNT1 A 0 A_TakeInventory("G_cursed",99,AAPTR_TRACER)
TNT1 A 0 
stop
}
}

actor CurseSelfHarm : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ACS_NamedExecuteAlways("CurseHarm",0,CallACS("PKMNACS_HealthPercent",0,1,CURSE_COST))

stop
Nope:
TNT1 A 0
stop
}
}


actor CurseFX_1 : BasicGraphicEffect
{
scale 2.5
ReactionTime 10
speed 7
Args 0
States
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_Warp(AAPTR_TARGET,0,0,28,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
Spawn2:
GCRS AAAAAAAA 1 A_Warp(AAPTR_TARGET,0,0,28,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
TNT1 A 0 A_PlaySoundEx("Move/Curse2","Weapon")
GCRS BBBBCCCCCCCCZZCCZZCCZZCC 1 A_Warp(AAPTR_TARGET,0,0,28,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
stop
}
}

actor CurseFX1_Spawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SpawnItemEX("CurseFX_1")
Nope:
TNT1 A 0
stop
}
}


actor CurseFX_2 : BasicGraphicEffect
{
scale 1.5
ReactionTime 10
speed 7
Args 0
States
{
Spawn:
TNT1 A 0 
GCRS DEFGHI 2 
GCRS I 17
GCRS IZIZIZ 2
stop
}
}

actor CurseFX2_Spawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_PlaySoundEx("Move/Curse2","Weapon")
TNT1 A 0 A_SpawnItemEX("CurseFX_2",0,0,36,0,0,1)
Nope:
TNT1 A 0
stop
}
}

actor CurseFX_3 : BasicGraphicEffect
{
scale 2.5
ReactionTime 10
speed 7
Args 0
States
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_Warp(AAPTR_TARGET,0,0,28,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
Spawn2:
GCRS AAAAAAAAZZAAZZAAZZAAZZ 1 A_Warp(AAPTR_TARGET,0,0,28,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
stop
}
}

actor CurseFX3_Spawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SpawnItemEX("CurseFX_3")
Nope:
TNT1 A 0
stop
}
}
//#endregion