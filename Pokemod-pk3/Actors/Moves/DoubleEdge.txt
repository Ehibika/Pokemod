actor DoubleEdgeMove : WepMasterBase
{
Tag "DoubleEdge"
obituary "$OB_DOUBLEEDGE"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "DoubleEdgePP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" B 0 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==0,"NoAmmo")
"####" B 0 A_Jump(256,"Fire.DashWarmup")
Goto Fire.DashWarmup

Fire.DashWarmup:
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_DoubleEdge,1)
"####" A 0 A_GiveInventory("DoubleEdgeWarmupTimer")
"####" A 0 SetPlayerProperty(0,1,0)
"####" A 0 A_Stop
Fire.DashWarmup1:
"####" A 3
"####" AAA 0 A_SpawnItemEx("GroundDustFX", random(0,50), 0, -(z-floorz), 0, 0, 0, random(0, 359))
"####" A 0 A_PlaySoundEx("weapon/Trample","Weapon")
"####" A 0 A_Jumpifinventory("DoubleEdgeWarmupTimer",1,"Fire.DashWarmup1")
"####" B 0 A_Jump(256,"Fire.DashStart")
Goto Fire.DashWarmup

Fire.DashStart:
"####" A 0 SetPlayerProperty(0,1,4)
"####" A 0 A_GiveInventory("DoubleEdgeTimer")
"####" A 0 A_GiveInventory("DoubleEdgeInitiate")
Fire.Dash:
"####" A 1
"####" A 0 A_SpawnItemEX("GroundDustFX",-25,0,-(z-floorz),0)
"####" A 0 A_Jumpifinventory("DashBreaker",1,"Fire.DashEnd")
"####" A 0 A_Jumpifinventory("DoubleEdgeTimer",1,"Fire.Dash")
goto Fire.DashEnd

Fire.DashEnd:
"####" B 0 A_ScaleVelocity(0.2)
"####" B 0 SetPlayerProperty(0,0,4)
"####" A 0 SetPlayerProperty(0,0,0)
"####" B 1 A_GiveInventory("HalfRecovery_B")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End




}
}

actor DoubleEdgeMove1 : DoubleEdgeMove {Weapon.SlotNumber 1}
actor DoubleEdgeMove2 : DoubleEdgeMove {Weapon.SlotNumber 2}
actor DoubleEdgeMove3 : DoubleEdgeMove {Weapon.SlotNumber 3}
actor DoubleEdgeMove4 : DoubleEdgeMove {Weapon.SlotNumber 4}

actor DoubleEdgeMove5 : DoubleEdgeMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("DoubleEdgeMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor DoubleEdgePP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor DoubleEdgeTimer : Powerup {inventory.amount 1 powerup.duration 35 }
actor DoubleEdgeWarmupTimer : Powerup {inventory.amount 1 powerup.duration 10 }

Actor DoubleEdgeDelay : powerprotection
{
Powerup.duration 5
Damagefactor "DoubleEdge", 0
}

const int DBLEDG_Thrust = 25;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor DoubleEdgeBumper : BasicProjectile
{
Speed 25 
Damage (0)
radius 6
height 6
DamageType "DoubleEdge"
Obituary "$OB_DOUBLEEDGE"
scale 1.5
//+NOINTERACTION
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Terminate")
TNT1 A 1 
TNT1 A 1 A_Explode(120,80,0,0,80)
stop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_GiveToTarget("DoubleEdgeCollision2",1)
TNT1 A 0 A_SpawnItemEx("BasicMeleeFX", 0, 0, 0, 0, 0, 0, 0,1)
Crash:
Terminate:
TNT1 A 1
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

actor DoubleEdgeWave
{
PROJECTILE
+FLOORCLIP
+THRUGHOST
+MTHRUSPECIES
+NOEXPLODEFLOOR
Radius 5
Height 5
scale 2.0
damage (0)
reactiontime 3
DamageType "DoubleEdge"
obituary "$OB_DoubleEdge"
speed 0
States
{
Spawn:
BEWA B 0

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(ScaleX+0.3)
BEWA B 0 A_CountDown
loop
Death:
BEWA A 0
stop
}
}


actor DoubleEdgeCollision : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*-35,0,sin(pitch)*35,CVF_RELATIVE)
TNT1 A 0 ThrustThingZ(0,30,1,1)
stop
Nope:
TNT1 A 0
stop
}
}

actor DoubleEdgeCollision2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_PlaySoundEx("weapon/Rebound","SoundSlot5")
TNT1 A 0 DamageThing(100*(CallACS("StatValueRamp",STAT_PATK,1,400)*0.01))
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
stop
Nope:
TNT1 A 0
stop
}
}

actor DoubleEdgeThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_PlaySoundEx("weapon/Trample","SoundSlot5")
TNT1 A 0 A_ChangeVelocity(DBLEDG_Thrust,0,Momz,CVF_RELATIVE|CVF_REPLACE)//cos(-pitch)* , sin(-pitch)*DBLEDG_Thrust
TNT1 A 1 A_FireCustomMissile("DoubleEdgeBumper",0,0,0,15,0,Pitch)
BASB A 1 A_SpawnitemEx("DoubleEdgeWave",cos(-pitch)*30,0,(25),0,0,0,0,SXF_TRANSFERTRANSLATION) //+(sin(pitch)*30)
stop
Nope:
TNT1 A 0
stop
}
}

actor DoubleEdgeInitiate : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Spawnitemex("DoubleEdgeWatcher")
stop
}
}

actor DoubleEdgeWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Death")
TNT1 A 0 A_SetUserVar("user_DashTime",user_DashTime+1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 3 A_GiveToTarget("DoubleEdgeThrust")
TNT1 A 0 A_JumpIfInTargetInventory("DoubleEdgeTimer",1,"Spawn")
//TNT1 A 0 A_Jumpif(user_DashTime <= 6,"Spawn")
Goto Death
Check2:
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Spawn")
Goto Death
Death:
DeathTake:
TNT1 A 0 //A_TakeFromTarget("DoubleEdgeActive",1)
stop
}
}
