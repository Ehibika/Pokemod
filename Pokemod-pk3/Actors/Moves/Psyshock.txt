actor PsyshockMove : WepMasterBase
{
Tag "Psyshock"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "PsyshockPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_D)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("PsyshockRingSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_JumpIfInventory("IsBot",1,"Fire.Bullet")
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/Usemove","Weapon")
"####" B 0 A_FireCustomMissile("PsyshockSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("Psyshock_Recovery_D")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End


AltFire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Altfire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Altfire.Begin:
"####" B 1
"####" C 0 A_REfire("Altfire.Begin")
"####" A 0 A_GiveInventory("RemoteMoveLock")
"####" A 0 A_GiveInventory("PsyshockRCFlag")
"####" A 0 A_Stop
"####" A 0 A_SpawnItemEX("Psyshock_Remote")
"####" B 0 A_Jump(256,"Altfire.Remote")
Goto Altfire.Remote


Altfire.Remote:
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Altfire.After")
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_RELOAD)==1,"Altfire.RemoteReload")
"####" B 0 A_JumpifInventory("PsyshockRCFlag",1,"Altfire.Remote")
Goto Altfire.After
Altfire.RemoteReload:
TNT1 A 0 A_GiveInventory("PPRecoveryItem") 
Goto Altfire.Remote

Altfire.After:
"####" B 1 A_GiveInventory("Psyshock_Recovery_D")
"####" A 0 A_GiveInventory("RemoteMoveUnLock")
Altfire.After2:
"####" B 1
"####" B 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Altfire.After2")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor PsyshockMove1 : PsyshockMove {Weapon.SlotNumber 1}
actor PsyshockMove2 : PsyshockMove {Weapon.SlotNumber 2}
actor PsyshockMove3 : PsyshockMove {Weapon.SlotNumber 3}
actor PsyshockMove4 : PsyshockMove {Weapon.SlotNumber 4}

actor PsyshockMove5 : PsyshockMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("PsyshockMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor PsyshockPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor PsyshockRCFlag : Powerup {inventory.amount 1 powerup.duration 35 }

Const int PsyshockRad = 150;
Const int Psyshock_Forward = 150;
Const int Psyshock_Range = 800;

actor PsyshockPPTaker : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_Psyshock,1)
stop
}
}


Actor PsyshockProtect : powerprotection
{
Powerup.duration 5
Damagefactor "Psyshock", 0
}

Actor PsyshockBuffer : powerprotection
{
Powerup.duration 10
Damagefactor "Psyshock", 0.1
}

actor Psyshock_Recovery_D : FullRecovery_D{Powerup.Duration 52}


//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor Psyshock_Remote 
{
Speed 5
+MISSILE
+THRUACTORS
+NOCLIP
+FLOORHUGGER
+NOEXPLODEFLOOR
//+SLIDESONWALLS
//+BOUNCEONWALLS 
+NOGRAVITY
Health 5
Damage (0)
DamageType "Psyshock"
obituary "$OB_Psyshock"
radius 5
Reactiontime 50
height 16
scale 3.5
Args 0, 60
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 ACS_NamedExecuteAlways("CamGrabObject",0)
TNT1 A 0 ACS_NamedExecuteAlways("RemoteControlActor",0,15.0,Psyshock_Range)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_DEFAULT)
Spawn2:
TNT1 A 1 A_JumpIfCloser(Psyshock_Range-1,"Spawn3")
TNT1 A 0 A_SpawnItemEx("PsyshockRingGuide2",Psyshock_Forward,0,0,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("PsyshockRCFlag",1)
TBLA A 0
loop
Spawn3:
TNT1 A 1 A_SpawnItemEx("PsyshockRingGuide",Psyshock_Forward,0,0,0,0,0,0,1)

TNT1 A 0 A_GiveToTarget("PsyshockRCFlag",1)
TBLA A 0
goto Spawn2

Trigger: //Check PP
TNT1 A 0 A_JumpifInTargetInventory("PsyshockPP",1,"Trigger2")
goto Decline
Trigger2: // Check Recovery Frame
TNT1 A 0 A_JumpifInTargetInventory("Recovery_D",1,"Decline")
goto Detonate

Detonate: // Cast the attack!
TNT1 A 0 A_GiveToTarget("PsyshockPPTaker",1)
TNT1 A 0 A_SpawnItemEx("PsyshockBurst",Psyshock_Forward,0,-(z-floorz),0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("Psyshock_Recovery_D",1)
goto PostTrigger

Decline: //Cannot cast the attack
TNT1 A 0 A_GiveToTarget("DeclineSoundCue",1)
goto PostTrigger

PostTrigger:
TNT1 AAAAA 1 A_SpawnItemEx("PsyshockRingGuide",Psyshock_Forward,0,0,0,0,0,0,1)
Goto Spawn2

Death:
BSTG AAAAAA 0 A_SpawnItemEx("RazorLeafParticle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
SPKH ABCD 2
stop
}
}

actor PsyshockRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("PsyshockGuideDot", PsyshockRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("PsyshockGuideDot", -PsyshockRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor PsyshockRingGuide2 : BasicHelper
{
	+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("PsyshockGuideDot2", PsyshockRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("PsyshockGuideDot2", -PsyshockRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor PsyshockRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 450
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_SpawnItemEx("PsyshockRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}

/*
actor PsyshockGraphicSpawner : PsyshockRingSpawner
{
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_SpawnItemEx("Psyshock_Graphic1",0,0,-(z-floorz)+60,0,0,0,0,0)
stop

Terminate:
TNT1 A 0 
stop
}
}
*/
actor PsyshockGuideDot : BasicGuideDot {}

actor PsyshockGuideDot2 : BasicGuideDot {StencilColor "#F42A0C"}

actor PsyshockSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 450
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("PsyshockBurst",0,0,-(z-floorz),0,0,0,0,1)
TNT1 A 1
stop

Terminate:
TNT1 A 0 
stop
}
}

actor PsyshockBurst : BasicExplosion
{
translation "202:202=210:210"//90
Damagetype "Psyshock"
Obituary "$OB_Psyshock"
scale 2.5
ReactionTime 5
Var int user_angle;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_PlaySoundEx("misc/astroplatformappear", "Weapon")
Spawn2:
TNT1 A 1 
TNT1 AAA 0 A_SpawnItemEx("PsyshockBullet1",random(0,PsyshockRad),0,random(0,PsyshockRad),0,0,1,random(0,360),1)
TNT1 A 1 A_Countdown
loop
Death:
TNT1 A 0
stop
}
}


actor PsyshockBullet1 : BasicProjectile
{
Speed 0
Damage (0)
Damagetype "Psyshock"
Obituary "$OB_Psyshock"
radius 12
Reactiontime 3
height 12
scale 1.0
+RIPPER
+NOINTERACTION
+SEEKERMISSILE
+NOCLIP
+SPECTRAL //Spectral Breeching
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_PlaySoundEx("misc/astroplatformraise", "Weapon")
Spawn2:
TNT1 A 0 
TBOL MN 2 A_SeekerMissile (90, 90, SMF_LOOK|SMF_PRECISE, 256, 2)
TNT1 A 0 A_Countdown
loop

Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_FaceTracer
TNT1 A 0 A_PlaySoundEx("misc/devilfire", "Weapon")
TNT1 A 0 A_SpawnItemEx("PsyshockBullet2", 0, 0, 0, cos(pitch), 0, sin(-pitch), 0, SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
BSTG A 1 A_Spawnitemex("PsyshockPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

actor PsyshockBullet2 : BasicProjectile
{
Speed 32
Damage (MBP_Psyshock)
Damagetype "Psyshock"
Obituary "$OB_Psyshock"
radius 12
Reactiontime 4
height 12
scale 1.0
+SEEKERMISSILE
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0 
TBOL M 0 A_SeekerMissile (90, 90, SMF_PRECISE, 256, 2)
TBOL M 2 A_Spawnitemex("PsyshockTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
TNT1 A 0 A_Countdown
TBOL M 0 A_SeekerMissile (90, 90, SMF_PRECISE, 256, 2)
TBOL M 2 A_Spawnitemex("PsyshockTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
TNT1 A 0 A_Countdown
loop

Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
BSTG A 1 A_Spawnitemex("PsyshockPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}




actor PsyshockPuff1 : BasicClientside
{
scale 2
States
{
Spawn:
DWRA CBA 2
stop
}
}

actor PsyshockTrail : BasicClientside
{
scale 1.0
Alpha 0.9
Renderstyle "add"
States
{
Spawn:
CRBM A 0
TNT1 A 1
DWRA CBA 1
DWRA AA 1 A_Fadeout
stop
}
}