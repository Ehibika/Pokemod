actor RapidSpinMove : WepMasterBase
{
Tag "RapidSpin"
obituary "$OB_RAPIDSPIN"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "RapidSpinPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_JumpifInventory("ConShotTimer",1,"Fire.Bullet2")
"####" B 0 A_JumpifInventory("RapidSpinPower",1,"Fire.Bullet")
"####" B 0 A_FireCustomMissile("RapidSpinMelee",0,0,0,0)
"####" B 0 A_Gunflash("Flash.Sound")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" A 1 ACS_NamedExecuteAlways("TakePP",0,Move_RapidSpin,1)
"####" B 0 A_GiveInventory("RapidSpinCST")
"####" B 0 A_GiveInventory("RapidSpinPower")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Bullet2:
"####" B 1  
"####" B 0 A_GiveInventory("RapidSpinBuffInitiate")
"####" B 0 A_GiveInventory("RapidSpinPower")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1
"####" C 0 A_REfire("Fire")
"####" B 1 A_GiveInventory("SemiRecovery_B")
"####" B 0 A_TakeInventory("ConShotTimer",99)
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

Flash.Sound:
TNT1 A 17 A_PlaySoundEx("weapon/TopSpin", "SoundSlot5",0)
TNT1 A  0 A_JumpifInventory("RapidSpinPower",1,"Flash.Sound")
stop
}
}

actor RapidSpinMove1 : RapidSpinMove {Weapon.SlotNumber 1}
actor RapidSpinMove2 : RapidSpinMove {Weapon.SlotNumber 2}
actor RapidSpinMove3 : RapidSpinMove {Weapon.SlotNumber 3}
actor RapidSpinMove4 : RapidSpinMove {Weapon.SlotNumber 4}

actor RapidSpinMove5 : RapidSpinMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("RapidSpinMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor RapidSpinPP : Ammo {inventory.amount 1 inventory.Maxamount 20}


actor STF_RapidSpin : Powerup {inventory.amount 1 Powerup.Duration 17}
Actor RapidSpinPower: powerUp //Repells grapples and lingering effects
{
Powerup.duration 17
}


actor RapidSpinCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 35
powerup.type "ConShotTimer"
}

Actor RapidSpinProtect : powerprotection
{
Powerup.duration 5
Damagefactor "RapidSpin", 0
}

actor RapidSpinBuffInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("STF_RapidSpin",1,"Sustain")
TNT1 A 0 A_GiveInventory("STF_RapidSpin",1)
TNT1 A 0 A_SpawnitemEX("RapidSpinBuffWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_RapidSpin",1)
stop
Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor RapidSpinMelee : BasicProjectile
{
+NOINTERACTION
Speed 35
Damage (0)
DamageType "RapidSpin"
obituary "$OB_RapidSpin"
height 16
scale 2.5
Args 35, 0
ReactionTime 12
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1
TNT1 A 0 A_Stop
Goto Spawn.Spin

Spawn.Spin:
TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,15)
TNT1 A 0 A_SpawnItemEx("RapidSpinFX",60,0,0,8,0,0,Args[0],1)
TNT1 A 0 A_SpawnItemEx("RapidSpinFX",60,0,0,8,0,0,Args[0]+180,1)
TNT1 A 0 A_SetArg(0,args[0]+20)
TNT1 A 0 A_SetArg(1,args[1]-1)
Goto Spawn.SpinHit

Spawn.SpinHit:
TNT1 A 0 A_Explode(MBP_RapidSpin,90,0,0,90)
TNT1 A 1 //A_CountDown
TNT1 A 0 A_Jumpif(args[1] <= 0,"Spawn.HazardScrub")
Goto Spawn.SpinCheck

Spawn.HazardScrub:
TNT1 A 0 ACS_NamedExecuteAlways("HazardScrub",0,150)
TNT1 A 0 A_SetArg(1,3)
Goto Spawn.SpinCheck

Spawn.SpinCheck:
TNT1 AAA 1 A_JumpIfInTargetInventory("RapidSpinPower",1,"Spawn.Spin")
Goto Death

Death:
TNT1 A 0
stop
}
}

actor RapidSpinFX : BasicClientside
{
Scale 1.0
States
{
Spawn:
DUOR T 1 A_FadeOut(0.05)
DUOR T 0 A_ScaleVelocity(0.6)
loop
}
}


actor RapidSpinBuffWatcher : BasicHelper 
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("HazedFlagEnemy",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("HazedFlagEnemy",1,"Death")
TNT1 A 5 A_GiveToTarget("SPDUpFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_RapidSpin",1,"Spawn")
Goto Death

Death:
TNT1 A 0 A_GiveToTarget("StatUpdator")
TNT1 A 2 
stop
}
}