
actor DragonDartsMove : WepMasterBase
{
Tag "DragonDarts"
obituary "$OB_DragonDarts"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "DragonDartsPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
TNT1 AAA 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
TNT1 A 0 A_Jump(256,"NoAmmo")
Goto NoAmmo


Fire.Begin:
TNT1 A 0 //A_SpawnItemEx("DragonDartsFX")
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_DragonDarts,1)
TNT1 A 0 A_Jump(256,"Fire.Charge2")
Goto Fire.Charge2

Fire.Charge1: //Preparing a stack
TNT1 A 0 A_JumpIfInventory("MultiShotCounter",2,"Fire.Charge3")
TNT1 A 0 A_JumpIfInventory("WeaponCharge",17,"Fire.Charge2")
TNT1 A 0 A_GiveInventory("WeaponCharge",1)
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
TNT1 A 0 //A_PlaySoundEx("Weapon/Heat1","Soundslot6")
TNT1 A 1
TNT1 A 0 A_REfire("Fire.Charge1")
TNT1 A 0 A_Jump(256,"Fire.Launch")
Goto Fire.Launch

Fire.Charge2: //Make a stack
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
TNT1 A 0 A_GiveInventory("MultiShotCounter",1)
TNT1 A 0 A_TakeInventory("WeaponCharge",99)
TNT1 A 0 A_PlaySoundEx("Weapon/rminestick","Soundslot6")

TNT1 A 1
TNT1 A 0 A_REfire("Fire.Charge1")
TNT1 A 0 A_Jump(256,"Fire.Launch")
Goto Fire.Launch

Fire.Charge3: //Maximum stacks
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
TNT1 A 1
TNT1 A 0 A_REfire("Fire.Charge3")
TNT1 A 0 A_Jump(256,"Fire.Launch")
Goto Fire.Launch


Fire.Launch:
TNT1 A 0
TNT1 A 0 A_Jumpifinventory("MultiShotCounter",1,"Fire.Bullet")
TNT1 A 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Bullet:
TNT1 A 0 A_PlaySoundEx("Weapon/MagnetMissile","Weapon")
TNT1 A 0 A_FireCustomMissile("DragonDart",0,0,0,0,0,0)
TNT1 A 8 A_TakeInventory("MultiShotCounter",1)
TNT1 A 0 A_Jump(256,"Fire.Launch")
Goto Fire.Launch

Fire.After:
TNT1 A 1 A_GiveInventory("FullRecovery_A")
TNT1 A 0 A_REfire("Fire")
TNT1 A 0 A_TakeInventory("ConShotTimer",99)
TNT1 A 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor DragonDartsMove1 : DragonDartsMove {Weapon.SlotNumber 1}
actor DragonDartsMove2 : DragonDartsMove {Weapon.SlotNumber 2}
actor DragonDartsMove3 : DragonDartsMove {Weapon.SlotNumber 3}
actor DragonDartsMove4 : DragonDartsMove {Weapon.SlotNumber 4}

actor DragonDartsMove5 : DragonDartsMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("DragonDartsMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor DragonDartsPP : Ammo {inventory.amount 1 inventory.Maxamount 20}

actor DragonDartsCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 35
powerup.type "ConShotTimer"
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor DragonDart : BasicProjectile
{
DamageType "DragonDarts"
obituary "$OB_DragonDarts"
+SEEKERMISSILE
+SCREENSEEKER
+FLOAT
Speed 32
Damage (MBP_DragonDarts)
radius 5
height 5
ReactionTime 12
scale 1.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Spawnitemex("DragonDartsSounder")

Spawn2: // -=- Seeking Target -=-
P037 F 1 A_SeekerMissile (2, 30, SMF_LOOK|SMF_PRECISE, 256, 2)
P037 F 1 A_SpawnItemEx("DragonDartsTrailFX",0,0,18,0,0,0,0,1)

TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TRACER, AAPTR_NULL) == TRUE, "Spawn2")
Goto Spawn3

Spawn3: // -=- target acquired -=-
TNT1 A 0 A_SeekerMissile (2, 30, SMF_PRECISE)
P037 F 2 A_SpawnItemEx("DragonDartsTrailFX",0,0,18,0,0,0,0,1)
loop

Death:
TNT1 A 0 A_PlaySoundEx("Move/Basic","Weapon")
TNT1 A 1 A_Spawnitemex("DragonDartsImpact",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_Spawnitemex("DragonDartsDeathFX",0,0,0,-8,0,8,0,SXF_TRANSFERTRANSLATION)
stop
}
}




actor DragonDartsDeathFX : BasicClientside
{
scale 2.0
-NOGRAVITY
-NOINTERACTION
+RIPPER
+THRUACTORS
States
{
Spawn:
TNT1 A 0 
P037 H 10
P037 HZHZHZH  3 
stop
}
}

actor DragonDartsTrailFX : BasicClientside
{
scale 2.0
Renderstyle "Add"
States
{
Spawn:
TNT1 A 0 
TNT1 A 1
FIRE DEF 2
stop
}
}

actor DragonDartsImpact : BasicClientside
{
scale 4.5
States
{
Spawn:
TNT1 A 0 
NDFX A 1
NDFX AAAA 1 A_SetScale(ScaleX-0.5)
stop
}
}