actor UTurnMove : WepMasterBase
{
Tag "UTurn"
obituary "$OB_UTURN"
Weapon.AmmoUse 1
Weapon.AmmoGive 30
weapon.ammotype "UTurnPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Dash")
Goto Fire.Dash

Fire.Dash:
"####" A 1 A_GiveInventory("DashSet_B")
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_UTurn,1)
"####" B 0 A_PlaySoundEx("weapon/ChargeKick","Soundslot6")
"####" A 0 A_GiveInventory("UTurnInitiate")
"####" A 0 A_FireCustomMissile("GroundDustFX",0,0,0,0)
"####" AAAAAAA 1 A_Jumpifinventory("DashBreaker",1,"Fire.DashEnd")
"####" B 0 A_ScaleVelocity(0.2)
goto Fire.DashEnd

Fire.DashEnd:
"####" B 0 A_GiveInventory("DashSetOff")
"####" B 1 A_GiveInventory("SemiRecovery_B")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor UTurnMove1 : UTurnMove {Weapon.SlotNumber 1}
actor UTurnMove2 : UTurnMove {Weapon.SlotNumber 2}
actor UTurnMove3 : UTurnMove {Weapon.SlotNumber 3}
actor UTurnMove4 : UTurnMove {Weapon.SlotNumber 4}

actor UTurnMove5 : UTurnMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("UTurnMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor UTurnPP : Ammo {inventory.amount 1 inventory.Maxamount 15}

Actor UTurnDelay : powerprotection
{
Powerup.duration 5
Damagefactor "UTurn", 0
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor UTurnBumper : BasicProjectile
{
Speed 80
Damage (0)
radius 12
height 12
DamageType "UTurn"
Obituary "$OB_UTURN"
scale 1.5
+NOINTERACTION
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Terminate")
TNT1 A 1 A_Explode(MBP_UTurn,RAMHITBOX,0,0,RAMHITBOX)
TNT1 A 1 
stop

Death:
XDeath:
Crash:
TNT1 A 1 A_GiveToTarget("UTurnCollision2",1)
TNT1 A 0 A_Explode(MBP_UTurn,RAMHITBOX,0,0,RAMHITBOX)
stop

//
//TNT1 A 0 A_Explode(MBP_UTurn,RAMHITBOX,0,0,RAMHITBOX)
Terminate:
TNT1 A 1
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

actor UTurnParticle : BasicClientside
{
scale 1.5
renderstyle "add"
Reactiontime 8
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SetScale(Frandom(1.5,3.0))
MMFX BCDE 1 
stop
}
}


actor UTurnWave : BasicExplosion
{
scale 2.0
damage (0)
reactiontime 5
DamageType "UTurn"
obituary "$OB_UTurn"
speed 0
States
{
Spawn:
TNT1 A 0
SpawnLoop:
TNT1 A 0 A_SetArg(0,random(-180,180))
TNT1 A 0 A_SpawnitemEx("UTurnParticle",0,cos(-args[0])*30,(25)+(sin(-args[0])*30),0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_CountDown
TNT1 A 1 A_Jump(256,"SpawnLoop")
loop

Death:
BEWA A 0
stop
}
}


actor UTurnCollision : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*-85,0,sin(pitch)*85,CVF_RELATIVE)
TNT1 A 0 ThrustThingZ(0,80,1,1)
stop
Nope:
TNT1 A 0
stop
}
}

actor UTurnCollision2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
stop
Nope:
TNT1 A 0
stop
}
}

actor UTurnThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*80,0,sin(-pitch)*80,CVF_RELATIVE|CVF_REPLACE)
TNT1 A 0 A_FireCustomMissile("UTurnBumper",15,0,0,0)
TNT1 A 0 A_FireCustomMissile("UTurnBumper",-15,0,0,0)
TNT1 A 1 A_FireCustomMissile("UTurnBumper",0,0,0,0)
BASB A 1 A_SpawnitemEx("UTurnWave",cos(-pitch)*30,0,(25)+(sin(pitch)*30),0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
Nope:
TNT1 A 0
stop
}
}

actor UTurnInitiate : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Spawnitemex("UTurnWatcher")
stop
}
}

actor UTurnWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Death")
TNT1 A 0 A_SetUserVar("user_DashTime",user_DashTime+1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1 A_GiveToTarget("UTurnThrust")
TNT1 A 0 A_Jumpif(user_DashTime < 6,"Spawn")
Goto Death
Check2:
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Spawn")
Goto Death
Death:
DeathTake:
TNT1 A 0 //A_TakeFromTarget("UTurnActive",1)
stop
}
}