actor GrowthMove : WepMasterBase
{
Tag "Growth"
obituary "$OB_GROWTH"
Weapon.AmmoUse 1
Weapon.AmmoGive 25
weapon.ammotype "GrowthPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" B 0 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==0,"NoAmmo")
"####" B 0 A_JumpIfInventory("STF_Growth",1,"Fire.Off")
"####" B 0 A_Jump(256,"Fire.On")
Goto Fire.On

Fire.On:
"####" B 5 A_PlaySoundEx("Weapon/Heat1","Soundslot6")
"####" BBBBBBB 0 A_GiveInventory("GrowthParticleSpawn")
"####" B 5 A_PlaySoundEx("Weapon/Heat1","Soundslot6")
"####" BBBBBBB 0 A_GiveInventory("GrowthParticleSpawn")
"####" B 5 A_PlaySoundEx("Weapon/Heat1","Soundslot6")
"####" BBBBBBB 0 A_GiveInventory("GrowthParticleSpawn")
"####" B 0 A_PlaySoundEx("Support/Statup","Soundslot7")
"####" B 0 A_GiveInventory("GrowthBuffInitiate")
"####" B 0 A_TakeInventory("GrowthPP",1)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Off:
"####" B 0 A_TakeInventory("Growth_Active",99)
"####" B 0 A_GiveInventory("PartRecovery_C")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor GrowthMove1 : GrowthMove {Weapon.SlotNumber 1}
actor GrowthMove2 : GrowthMove {Weapon.SlotNumber 2}
actor GrowthMove3 : GrowthMove {Weapon.SlotNumber 3}
actor GrowthMove4 : GrowthMove {Weapon.SlotNumber 4}

actor GrowthMove5 : GrowthMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("GrowthMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor GrowthPP : Ammo {inventory.amount 1 inventory.Maxamount 25}
actor STF_Growth : Powerup {inventory.amount 1 Powerup.Duration 40}
actor Growth_Active : Inventory {inventory.amount 1 inventory.Maxamount 1}

actor GrowthBuffInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("Growth_Active",1,"Sustain")
TNT1 A 0 A_GiveInventory("Growth_Active",1)
TNT1 A 0 A_SpawnitemEX("GrowthBuffWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_Growth",1)
stop
Nope:
TNT1 A 0
stop
}
}


actor GrowthBuffWatcher : BasicHelper 
{
Var int user_TickTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 5 A_GiveToTarget("STF_Growth")
TNT1 A 0 A_GiveToTarget("PatkUpFXSpawn")
TNT1 A 0 A_GiveToTarget("SatkUpFXSpawn")
TNT1 A 0 A_SetUserVar("user_TickTime",user_TickTime+5)
TNT1 A 0 A_JumpIf(user_TickTime>=35,"SpawnTick")
TNT1 A 0 A_JumpIfInTargetInventory("Growth_Active",1,"Spawn")
Goto Death

SpawnTick:
TNT1 A 0 A_SetUserVar("user_TickTime",0)
TNT1 A 0 A_TakeFromTarget("GrowthPP",1)
TNT1 A 0 A_JumpIfInTargetInventory("GrowthPP",1,"Spawn")
Goto Death

Death:
TNT1 A 0 A_GiveToTarget("StatUpdator")
TNT1 A 0 A_GiveToTarget("BuffLossSoundCue")
TNT1 A 0 A_TakeFromTarget("STF_Growth",9)
TNT1 A 0 A_TakeFromTarget("Growth_Active",99)
TNT1 A 2 
stop
}
}


actor GrowthParticle : Basicprojectile
{
scale 1.2
+SeekerMissile
speed 7
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_TARGET)
TMPK DCCBBAAA 2 A_Seekermissile(90,90,SMF_PRECISE)
stop
}
}

actor GrowthParticleSpawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SpawnItemEx ("GrowthParticle", random(-90,90), random(-90,90), random(20,80), momx, momy, momz, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
stop
Nope:
TNT1 A 0
stop
}
}


actor BuffLossSoundCue : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("IsDead",1,"Nope")
TNT1 A 0 A_PlaySoundEX("Support/StatDown","SoundSlot8")
Nope:
TNT1 A 0
stop
}
}