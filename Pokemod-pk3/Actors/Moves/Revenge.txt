actor RevengeMove : WepMasterBase
{
Tag "Revenge"
obituary "$OB_REVENGE"
Weapon.AmmoUse 1
Weapon.AmmoGive 15
weapon.ammotype "RevengePP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.PreCharge")
Goto Fire.PreCharge

Fire.PreCharge:
"####" B 0 A_TakeInventory("RevengePowerCount",999)
"####" B 0 A_GiveInventory("WeaponCharge",1)
"####" B 0 A_FireCustomMissile("RevengeWatcher",0,0)
"####" B 0 A_Jump(256,"Fire.Charge1")
Goto Fire.Charge1

Fire.Charge1:
"####" B 0 A_Jumpif(CallACS("CheckWeaponFire",RC_A)!=1,"NoAmmo")
"####" B 0 A_JumpIfInventory("RevengePowerCount",50,"Fire.Charge2")
//"####" B 0 A_GiveInventory("WeaponCharge",1)
"####" B 0 //A_GiveInventory("ChannelingSpeed")  //Firing Slow
"####" B 0 A_PlaySoundEx("Weapon/Heat1","Soundslot6")
"####" B 0 A_GiveInventory("RevengeFlag")
"####" B 1
"####" C 0 A_REfire("Fire.Charge1")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.After2

Fire.Charge2:
"####" B 0 //A_GiveInventory("ChannelingSpeed")  //Firing Slow
"####" B 0 A_PlaySoundEx("Weapon/Heat3","Soundslot6")
"####" B 0 A_GiveInventory("RevengeFlag")
"####" B 1
"####" C 0 A_REfire("Fire.Charge1")
"####" B 0 A_Jump(256,"Fire.Bullet2")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("weapon/SlashClaw","Weapon")
"####" B 0 A_FireCustomMissile("RevengeMelee",0,1,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Bullet2:
"####" B 0 A_PlaySoundEx("weapon/ChargeKick","Weapon")
"####" B 0 A_FireCustomMissile("RevengeMelee2",0,1,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 0 A_GiveInventory("HalfRecovery_A")
"####" B 0 A_TakeInventory("WeaponCharge",999)
"####" B 0 A_TakeInventory("RevengeFlag",999)
"####" B 0 A_TakeInventory("ChannelingSpeed",9) 
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor RevengeMove1 : RevengeMove {Weapon.SlotNumber 1}
actor RevengeMove2 : RevengeMove {Weapon.SlotNumber 2}
actor RevengeMove3 : RevengeMove {Weapon.SlotNumber 3}
actor RevengeMove4 : RevengeMove {Weapon.SlotNumber 4}

actor RevengeMove5 : RevengeMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("RevengeMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor RevengePP : Ammo {inventory.amount 1 inventory.Maxamount 15}

actor RevengeSensor : Powerup {inventory.amount 1 Powerup.Duration 35}
actor RevengeFlag : Powerup {inventory.amount 1 Powerup.Duration 35}

actor RevengePowerCount : Inventory {inventory.amount 1 inventory.MAxamount 999}


Const int RevengeRad = 64;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor RevengeWatcher
{
    PROJECTILE
    +NOINTERACTION
    renderstyle "ADD"
    speed 32
    height 1
    radius 1
    Scale 2.5
    ReactionTime 17
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
    Spawn2:
        TNT1 A 1 A_Warp(AAPTR_TARGET, 0,0,32, 0, 0)
        TNT1 A 5 A_SpawnItemEx("AcidVaporFX", random2(32), random2(32), 0, random(2,4), 0, random(1,4), random(0, 359),1)
        TCLA A 0 A_JumpIfInTargetInventory("RevengePowerCount",50,"Spawn3")
        TCLA A 0 A_JumpIfInTargetInventory("WeaponCharge",1,"Spawn2")
        stop
    Spawn3:
        TNT1 A 1 A_Warp(AAPTR_TARGET, 0,0,32, 0, 0)
        TNT1 AAAAA 1 A_SpawnItemEx("AcidVaporFX", random2(32), random2(32), 0, random(2,4), 0, random(1,4), random(0, 359),1)
        TCLA A 0 A_JumpIfInTargetInventory("WeaponCharge",1,"Spawn2")
        stop
    Death:
        TNT1 A 0 
        stop
    }
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor RevengeMelee : BasicMeleeAttack
{
Speed 80
Scale 4.0
DamageType "Revenge"
Obituary "$OB_Revenge"
Damage(0)
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 
HITG KKKKK 1 A_SpawnItemEx("DynamicPunchFXTrail", 0, 0, 0, 0, 0, 0, 0,1)
stop

Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Explode(CallACS("RevengePower",MBP_Revenge1),RevengeRad,0,0,RevengeRad)
//TNT1 A 0 A_SpawnItemEx("DynamicPunchFX1", 0, 0, 0, 0, 0, 0, 0,1)
//TNT1 A 0 A_SpawnItemEx("DynamicPunchFX2", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_PlaySoundEx("Misc/HardDrop", "SoundSlot6",0)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
TNT1 A 0 A_SpawnItemEx("RevengeMeleeFX", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 5
stop
}
}


actor RevengeMelee2 : RevengeMelee
{
States
{
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Explode(CallACS("RevengePower",MBP_Revenge2),RevengeRad,0,0,RevengeRad)
TNT1 A 0 A_SpawnItemEx("RevengeMeleeFX", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_PlaySoundEx("Misc/HardDrop", "SoundSlot6",0)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
TNT1 AAAAA 3 A_SpawnItemEx("RevengeMeleeFX", random2(64), random2(64), random2(64), 0, 0, 0, 0,1)
TNT1 A 5
stop
}
}

actor RevengeMeleeFX : BasicClientside
{
scale 1.5
ReactionTime 3
States
{
Spawn:
CRBM A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
BGHF ABCC 1 A_SetScale(Scalex+0.5)
Spawn2:
BGHF C 0 A_SetScale(Scalex+0.5)
BGHF C 1 A_Fadeout(0.3)
BGHF C 0 A_CountDown
loop
Death:
BGHF C 1
stop
}
}