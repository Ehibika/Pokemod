actor CharmMove : WepMasterBase
{
Tag "Charm"
obituary "$OB_CHARM"
Weapon.AmmoUse 1
Weapon.AmmoGive 25
weapon.ammotype "CharmPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1 A_Jumpif(CallACS("CheckDebuffAura")==1,"Fire.Off")
"####" B 0 A_Jump(256,"Fire.On")
Goto Fire.On

Fire.On:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 1 A_GiveInventory("CharmAuraFlag")
"####" B 0 A_GiveInventory("CharmPPTimer")
"####" B 0 A_SpawnItemEX("CharmPPWatcher")
"####" B 0 A_FireCustomMissile("CharmAura",0,0,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Off:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_GiveInventory("AuraPurge") // in Charm.txt
"####" B 17 
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor CharmMove1 : CharmMove {Weapon.SlotNumber 1}
actor CharmMove2 : CharmMove {Weapon.SlotNumber 2}
actor CharmMove3 : CharmMove {Weapon.SlotNumber 3}
actor CharmMove4 : CharmMove {Weapon.SlotNumber 4}

actor CharmMove5 : CharmMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("CharmMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor CharmPP : Ammo {inventory.amount 1 inventory.Maxamount 25}

actor STF_Charm : BaseSTatusFlag {}

actor CharmAuraFlag : inventory {inventory.amount 1 inventory.Maxamount 1}
actor CharmPPTimer : Powerup {inventory.amount 1 Powerup.Duration 35}

actor AuraPurge : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_TakeInventory("CharmAuraFlag",99)
TNT1 A 0 A_TakeInventory("CaptivateAuraFlag",99)
TNT1 A 0 A_TakeInventory("ScreechAuraFlag",99)
TNT1 A 0 A_TakeInventory("FakeTearsAuraFlag",99)
TNT1 A 0 A_TakeInventory("ScaryFaceAuraFlag",99)
stop
}
}

actor CharmNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",0,STATUS_STATS),"Nope") //Blocked by Protect|Detect|Substitute|Mist
TNT1 A 0 A_Jumpifinventory("STF_Charm",1,"Sustain")
TNT1 A 0 A_PlaySoundEx("support/StatDown","Weapon")
TNT1 A 0 A_GiveInventory("STF_Charm",1)
TNT1 A 0 A_SpawnItemEX("CharmNerfWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_Charm",1)
stop
Nope:
TNT1 A 0
stop
}
}

Const int Charm_Item = 18;
Const int Charm_RAD = 250;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]


actor CharmAura //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2
    Args 7
    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 ACS_NamedExecuteAlways("GeneralWarper2",0)    
    Warp:
        TNT1 A 1 A_Warp(2,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("CharmAuraFX",0,0,-(z-floorz)+24,0,0,0,0,8)
        TNT1 A 1 A_Warp(2,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("CharmAuraFX",0,0,-(z-floorz)+24,0,0,0,0,8)
        TNT1 A 1 A_Warp(2,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("CharmAuraFX",0,0,-(z-floorz)+24,0,0,0,0,8)
        TNT1 A 1 A_Warp(2,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("CharmAuraFX",0,0,-(z-floorz)+24,0,0,0,0,8)
        TNT1 A 1 A_Warp(2,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("CharmAuraFX",0,0,-(z-floorz)+24,0,0,0,0,8)
        TNT1 A 0 A_GiveToTarget("CharmFXSpawn")
        TNT1 A 0 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,Charm_Item,Charm_RAD,RG_ENEMY)
        loop
    Death:
    TNT1 A 0 A_GiveToTarget("DebuffOffSoundCue")
    TNT1 A 0
    stop

    }
}

actor CharmPPWatcher : BasicHelper 
{
States
{
Spawn:
TNT1 A 0
TNT1 A 5 
TNT1 A 0 A_JumpIfInTargetInventory("CharmPPTimer",1,"Spawn")
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"Death")
TNT1 A 0 A_GiveToTarget("CharmPPTimer")
Goto Check1

Check1:
TNT1 A 0 A_JumpIfInTargetInventory("CharmAuraFlag",1,"Check2")
TNT1 A 0
Goto Death

Check2:
TNT1 A 0 A_TakeFromTarget("CharmPP",1)
TNT1 A 0 A_JumpIfInTargetInventory("CharmPP",1,"Spawn")
TNT1 A 0
Goto Death

Death:
TNT1 A 0 A_TakeFromTarget("CharmAuraFlag",99)
TNT1 A 0 
stop
}
}

actor CharmAuraFX : BasicHelper
{
+CLIENTSIDEONLY
translation "198:198=220:220", "192:192=229:229"
	reactiontime 60
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
        TNT1 A 0// A_SetUserVar("user_angle", random(0, 30))
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("CharmGuideDot", Charm_RAD, 0, 0, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("CharmGuideDot", -Charm_RAD, 0, 0, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 6)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor CharmGuideDot : BasicGuideDot {Renderstyle Add
 scale 1.5
    States
    {
    Spawn:
    BASB U 0
    BASB U 1 //A_Warp(0,0,0,-(z-floorz)+24,0,0)
    stop
    }
}

//#region 

actor CharmShot : BasicProjectile
{
PROJECTILE
+RIPPER
Radius 4
Height 4
scale 2.5
damage (0)
+FORCEXYBILLBOARD
speed 45
reactiontime 3
Args 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("CharmWaveFX",0,0,0,0,0,0,0,16385)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SpawnItemEx("CharmShot_A",0,0,0,MomX,Momy,Momz,0,17929)
Stop

Death:
TNT1 A 0 
stop
}
}

actor CharmShot_A : CharmShot
{
Args 0,0,0
Var Int User_Step;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("CharmWaveFX",0,0,0,0,0,0,0,16385)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SetScale(ScaleX+0.5)
TNT1 A 0 A_SetUserVar("User_Step",Pitch % 3)
TNT1 A 0 A_Jumpif(User_Step == 0,"Spawn2")
TNT1 A 0 A_SpawnItemEx("CharmShot_A",0,0,0,MomX,Momy,Momz,0,17929)
stop

Spawn2:
TNT1 A 0 A_SetArg(0,MomX)
TNT1 A 0 A_SetArg(1,MomY)
TNT1 A 0 A_SetArg(2,MomZ)
TNT1 A 0 A_Stop
TNT1 A 0 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,Charm_Item,150,RG_ENEMY)
TNT1 A 3 
TNT1 A 0 A_SpawnItemEx("CharmShot_B",0,0,0,Args[0],Args[1],Args[2],0,17929)
TNT1 A 2
stop
}
}

actor CharmShot_B : CharmShot
{
Args 0,0,0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("CharmWaveFX",0,0,0,0,0,0,0,16385)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SetScale(ScaleX+0.5)
TNT1 A 0 A_Jumpif(Pitch >= reactiontime*3,"Death")
TNT1 A 0 A_SpawnItemEx("CharmShot_A",0,0,0,MomX,Momy,Momz,0,17929)
stop

}
}
//#endregion

actor CharmFXSpawn : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 AAAA 0 A_SpawnItemEx("CharmWaveFX",random(Charm_RAD/2,Charm_RAD),0,-(z-floorz)+24,0,0,4,random(0,360))
stop
}
}

actor CharmWaveFX : NoiseCrushTrail1
{
ReactionTime 5
Scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
LHRT AAABBBCCCDDD 1 A_ScaleVelocity(0.9)
LHRT EEDDEEDD 3 A_Fadeout
stop
}
}


actor CharmNerfWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_GiveToTarget("PatkDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_Charm",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}


actor DebuffOffSoundCue : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_PlaySoundEx("Move/ImbueOff","SoundSlot8")
Nope:
TNT1 A 0
stop
}
}