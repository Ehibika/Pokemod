actor RockPolishMove : WepMasterBase
{
Tag "RockPolish"
obituary "$OB_SYNTHESIS"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "RockPolishPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop

Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Channel")
Goto Fire.Channel

Fire.Channel:
"####" B 0 A_GiveInventory("WeaponCharge",5)
"####" B 0 A_GiveInventory("ChannelingSpeed")  //Firing Slow
"####" B 0 A_JumpIfInventory("WeaponCharge",25,"Fire.Initiate")
"####" B 0 A_PlaySoundEx("weapon/oilslider","Soundslot6")
"####" B 5 A_FireCustomMissile("RockPolishSwipe",0,0,0,0,Pitch)
"####" C 0 A_REfire("Fire.Channel")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Initiate:
"####" B 0 A_GiveInventory("RockPolishBuffInitiate")
"####" B 0 A_JumpIfInventory("ConShotTimer",1,"Fire.After")

"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_RockPolish,1)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 0 A_TakeInventory("WeaponCharge",999)
"####" C 0 A_REfire("Fire")
"####" B 0 A_TakeInventory("ChannelingSpeed",9)
"####" B 1 A_GiveInventory("SemiRecovery_C")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor RockPolishMove1 : RockPolishMove {Weapon.SlotNumber 1}
actor RockPolishMove2 : RockPolishMove {Weapon.SlotNumber 2}
actor RockPolishMove3 : RockPolishMove {Weapon.SlotNumber 3}
actor RockPolishMove4 : RockPolishMove {Weapon.SlotNumber 4}

actor RockPolishMove5 : RockPolishMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("RockPolishMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor RockPolishPP : Ammo {inventory.amount 1 inventory.Maxamount 20}

actor STF_RockPolish1 : PowerUp { inventory.amount 1 Powerup.Duration -3}
actor STF_RockPolish2 : PowerUp { inventory.amount 1 Powerup.Duration -6}
actor STF_RockPolish3 : PowerUp { inventory.amount 1 Powerup.Duration -9}

actor RockPolishBuffInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpIF(CallACS("GetRockPolishLVL") == 1,"RockPolish2")
TNT1 A 0 A_JumpIF(CallACS("GetRockPolishLVL") == 2,"RockPolish3")
TNT1 A 0 A_JumpIF(CallACS("GetRockPolishLVL") == 3,"RockPolish3")
goto RockPolish0

RockPolish0:
TNT1 A 0 A_GiveInventory("STF_RockPolish1",1)
Goto Pickup2

RockPolish1:
TNT1 A 0 A_GiveInventory("RockPolishPurge",1)
TNT1 A 0 A_GiveInventory("StatUpdator",1)
Goto Nope

RockPolish2:
TNT1 A 0 A_GiveInventory("RockPolishPurge",1)
TNT1 A 0 A_GiveInventory("STF_RockPolish1",1)
TNT1 A 0 A_GiveInventory("STF_RockPolish2",1)
TNT1 A 0 A_GiveInventory("StatUpdator",1)
Goto Nope

RockPolish3:
TNT1 A 0 A_GiveInventory("RockPolishPurge",1)
TNT1 A 0 A_GiveInventory("STF_RockPolish1",1)
TNT1 A 0 A_GiveInventory("STF_RockPolish2",1)
TNT1 A 0 A_GiveInventory("STF_RockPolish3",1)
TNT1 A 0 A_GiveInventory("StatUpdator",1)
Goto Nope

Pickup2:
TNT1 A 0 A_SpawnitemEX("RockPolishBuffWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Nope:
TNT1 A 0
stop
}
}


actor RockPolishPurge : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_TakeInventory("STF_RockPolish1",9)
TNT1 A 0 A_TakeInventory("STF_RockPolish2",9)
TNT1 A 0 A_TakeInventory("STF_RockPolish3",9)
stop
}
}


actor RockPolishBuffWatcher : BasicHelper 
{
Var int user_TickTime;
+SERVERSIDEONLY
Args 1
States
{
Spawn:
TNT1 A 0
Cycle.Start:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("HazeFlag",1,"Death")
TNT1 A 0 A_GiveToTarget("SpawnStatusParticles")
TNT1 A 0 A_JumpIF(CallACS("GetRockPolishLVL",AAPTR_TARGET) > 0,"Cycle.Mode")
goto Death

Cycle.Mode:
TNT1 A 5 
TNT1 A 1 A_JumpIF(CallACS("GetRockPolishLVL",AAPTR_TARGET) == Args[0],"Cycle.Start")
TNT1 A 0 A_SetArg(0,CallACS("GetRockPolishLVL",AAPTR_TARGET))
TNT1 A 0 A_GiveToTarget("StatUpdator")
goto Cycle.Start


Death:
TNT1 A 0 A_GiveToTarget("StatUpdator")
TNT1 A 0 A_GiveToTarget("BuffLossSoundCue")
TNT1 A 0 A_TakeFromTarget("STF_RockPolish1",9)
TNT1 A 0 A_TakeFromTarget("STF_RockPolish2",9)
TNT1 A 0 A_TakeFromTarget("STF_RockPolish3",9)
TNT1 A 2 
stop
}
}

actor RockPolishSwipe : BasicProjectile
{
+NOINTERACTION
damage(0)
scale 2.0
Speed 10
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_GiveInventory("ProjectileCode",1)
TNT1 AAA 1 A_SpawnItemEx("RockPolishFX",random2(48),random2(48),random2(48),0,0,0,0,1)
stop
}
}

actor RockPolishFX : BasicClientside
{
scale 1.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("RockPolishParticle",0,0,0,random2(16),random2(16),random2(16),0,1)
TNT1 A 0 A_Jump(256,"Spawn_A","Spawn_B")
Spawn_A:
FLEX FFFF 1 A_SetScale(ScaleX + 2.3,Scaley*0.5)
stop
Spawn_B:
FLEX FFFF 1 A_SetScale(ScaleX*0.5,Scaley + 2.3)
stop
}
}

actor RockPolishParticle : BasicClientside
{
scale 2.5
ReactionTime 2
Renderstyle "ADD"
Alpha 0.9
States
{
Spawn:
TNT1 A 0
Spawn2:
TMPK DCCBBA 2 A_ScaleVelocity(0.6)
TNT1 A 5
TNT1 A 0 A_CountDown
loop

Death:
TNT1 A 0
stop
}
}