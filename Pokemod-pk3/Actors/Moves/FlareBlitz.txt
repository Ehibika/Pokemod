actor FlareBlitzMove : WepMasterBase
{
Tag "FlareBlitz"
obituary "$OB_FLAREBLITZ"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "FlareBlitzPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.DashWarmup")
Goto Fire.DashWarmup

Fire.DashWarmup:
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_FlareBlitz,1)
"####" A 0 A_GiveInventory("FlareBlitzWarmupTimer")
"####" A 0 A_GiveInventory("DashSet_A")
"####" A 0 A_ChangeFlag("NOGRAVITY",1)
"####" A 0 A_GiveInventory("PKMN_WhiteFlagDropper",1) //For objective combo
"####" A 0 A_Stop
Fire.DashWarmup1:
"####" A 3
"####" AAA 0 A_SpawnItemEx("FireFangParticle2", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359))
"####" A 0 A_PlaySoundEx("weapon/Heat2","Weapon")
"####" A 0 A_Jumpifinventory("FlareBlitzWarmupTimer",1,"Fire.DashWarmup1")
"####" B 0 A_Jump(256,"Fire.DashStart")
Goto Fire.DashWarmup

Fire.DashStart:
"####" A 0 A_GiveInventory("DashSet_B")
"####" A 0 A_ChangeFlag("NOGRAVITY",0)
"####" A 0 A_GiveInventory("FlareBlitzTimer")
"####" A 0 A_GiveInventory("FlareBlitzInitiate")
Fire.Dash:
"####" A 1
"####" A 0 A_SpawnItemEX("GroundDustFX",-25,0,-(z-floorz),0)
"####" A 0 A_Jumpifinventory("DashBreaker",1,"Fire.DashEnd")
"####" A 0 A_Jumpifinventory("FlareBlitzTimer",1,"Fire.Dash")
goto Fire.DashEnd

Fire.DashEnd:
"####" B 0 A_ScaleVelocity(0.2)
"####" B 0 A_GiveInventory("DashSetOff")
"####" A 0 A_GiveInventory("DashSetOff")
"####" B 1 A_GiveInventory("HalfRecovery_B")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End




}
}

actor FlareBlitzMove1 : FlareBlitzMove {Weapon.SlotNumber 1}
actor FlareBlitzMove2 : FlareBlitzMove {Weapon.SlotNumber 2}
actor FlareBlitzMove3 : FlareBlitzMove {Weapon.SlotNumber 3}
actor FlareBlitzMove4 : FlareBlitzMove {Weapon.SlotNumber 4}

actor FlareBlitzMove5 : FlareBlitzMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("FlareBlitzMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor FlareBlitzPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor FlareBlitzWarmupTimer : Powerup {inventory.amount 1 powerup.duration 10 }
actor FlareBlitzTimer : Powerup {inventory.amount 1 powerup.duration 35 }

Actor FlareBlitzDelay : powerprotection
{
Powerup.duration 5
Damagefactor "FlareBlitz", 0
}

const int FLRBTZ_Thrust = 30;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor FlareBlitzBumper : BasicProjectile
{
Speed 30 
Damage (0)
radius 10
height 20
DamageType "FlareBlitz"
Obituary "$OB_FLAREBLITZ"
scale 1.5
//+NOINTERACTION
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Terminate")
TNT1 A 1 
TNT1 A 1 A_Explode(MBP_FlareBlitz,80,0,0,80)
FWVE W 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_GiveToTarget("FlareBlitzCollision2",1)
TNT1 A 0 A_SpawnItemEx("BasicMeleeFX", 0, 0, 0, 0, 0, 0, 0,1)
Crash:
Terminate:
TNT1 A 1
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

actor FlareBlitzWave
{
PROJECTILE
+FLOORCLIP
+THRUGHOST
+MTHRUSPECIES
+NOEXPLODEFLOOR
Radius 5
Height 5
scale 2.0
damage (0)
reactiontime 3
DamageType "FlareBlitz"
obituary "$OB_FlareBlitz"
speed 0
States
{
Spawn:
BEWA B 0

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(ScaleX+0.3)
BEWA B 0 A_CountDown
loop
Death:
BEWA A 0
stop
}
}


actor FlareBlitzCollision : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*-35,0,sin(pitch)*35,CVF_RELATIVE)
TNT1 A 0 ThrustThingZ(0,30,1,1)
stop
Nope:
TNT1 A 0
stop
}
}


actor FlareBlitzCollision2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_PlaySoundEx("weapon/Rebound","SoundSlot5")
TNT1 A 0 ACS_NamedExecuteAlways("PKMN_CollisionDamager",0,MBP_FlareBlitz)
//TNT1 A 0 DamageThing(MBP_FlareBlitz*(CallACS("StatValueRamp",STAT_PATK,1,400)*0.01))
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
stop
Nope:
TNT1 A 0
stop
}
}


actor FlareBlitzThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_PlaySoundEx("weapon/Trample","SoundSlot5")
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*FLRBTZ_Thrust,0,sin(-pitch)*FLRBTZ_Thrust,CVF_RELATIVE|CVF_REPLACE)//cos(-pitch)* , sin(-pitch)*FLRBTZ_Thrust
TNT1 A 1 A_FireCustomMissile("FlareBlitzBumper",0,0,0,15,0,0)
BASB A 1 A_SpawnitemEx("FlareBlitzWave",cos(-pitch)*30,0,(25),0,0,0,0,SXF_TRANSFERTRANSLATION) //+(sin(pitch)*30)
BASB AAA 0 A_SpawnItemEx("FireFangParticle1", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359))
BASB AAA 0 A_SpawnItemEx("FireFangParticle2", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359))
stop
Nope:
TNT1 A 0
stop
}
}

actor FlareBlitzInitiate : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Spawnitemex("FlareBlitzWatcher")
stop
}
}

actor FlareBlitzWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Death")
TNT1 A 0 A_SetUserVar("user_DashTime",user_DashTime+1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 3 A_GiveToTarget("FlareBlitzThrust")
TNT1 A 0 A_JumpIfInTargetInventory("FlareBlitzTimer",1,"Spawn")
//TNT1 A 0 A_Jumpif(user_DashTime <= 6,"Spawn")
Goto Death
Check2:
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Spawn")
Goto Death
Death:
DeathTake:
TNT1 A 0 //A_TakeFromTarget("FlareBlitzActive",1)
stop
}
}