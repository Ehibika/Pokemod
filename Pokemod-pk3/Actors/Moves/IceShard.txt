actor IceShardMove : WepMasterBase
{
Tag "IceShard"
obituary "$OB_ICESHARD"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "IceShardPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 //A_Jumpif(CallACS("CheckWeaponFire",RC_A)==0,"NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/HardBuster1","Weapon")
"####" B 0 A_FireCustomMissile("IceShardBullet",0,1,0,0,0,2.5)
"####" B 0 A_FireCustomMissile("IceShardBullet",3,0,0,0,0,-2.5)
"####" B 0 A_FireCustomMissile("IceShardBullet",-3,0,0,0,0,-2.5)
"####" B 0 A_FireCustomMissile("IceShardBullet",6,0,0,0,0,2.5)
"####" B 0 A_FireCustomMissile("IceShardBullet",-6,0,0,0,0,2.5)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor IceShardMove1 : IceShardMove {Weapon.SlotNumber 1}
actor IceShardMove2 : IceShardMove {Weapon.SlotNumber 2}
actor IceShardMove3 : IceShardMove {Weapon.SlotNumber 3}
actor IceShardMove4 : IceShardMove {Weapon.SlotNumber 4}

actor IceShardMove5 : IceShardMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("IceShardMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor IceShardPP : Ammo {inventory.amount 1 inventory.Maxamount 20}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor IceShardBullet : BasicProjectile
{
Obituary "$OB_ICESHARD"
DamageType "IceShard"
obituary "$OB_IceShard"

damage (MBP_IceShard)
Radius 15
Height 15
speed 64
Scale 3.5
ReactionTime 4
States
{
Spawn:
IMAC D 0 
IMAC D 0 A_GiveInventory("ProjectileCode",1)
IMAC D 1 A_Jump(50,"SpawnA","SpawnB","SpawnC","SpawnD")
wait

SpawnA:
IMAC D 8 
Goto Death

SpawnB:
IMAC E 8 
Goto Death

SpawnC:
IMAC F 8 
Goto Death

SpawnD:
IMAC G 8 
Goto Death

Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 AAA 1 A_SpawnItemEx("IceShardDripFX",random(-5,5),0,random(0,6),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
stop

Crash:
XDeath:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("IceShardImpactFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
stop
}
}


//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor IceShardImpactFX : BasicClientside
{
+MISSILE
translation "192:192=218:218", "198:198=221:221"
scale 2.0
Alpha 1.0
States
{
Spawn:
AQBA A 0
TNT1 A 0 A_playSoundEx("weapon/icewavebreak","Weapon")
HITG HHH 1 A_SpawnItemEx("IceBeamParticle1",random2(32),random2(32),random2(32),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
HITG HHHH 1 A_Fadeout(0.3)
stop
}
}


actor IceShardDripFX : BasicClientside
{
translation "192:192=218:218", "198:198=221:221"
-NOGRAVITY
-NOINTERACTION
+THRUACTORS
-NOCLIP
+MISSILE
scale 1.0
Alpha 0.8
ReactionTime 35
States
{
Spawn:
AQBA A 0
MWSZ MNO 2 A_CountDown
stop
}
}

