actor PsychicMove : WepMasterBase
{
Tag "Psychic"
Weapon.AmmoUse 1
Weapon.AmmoGive 5
weapon.ammotype "PsychicPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 1
"####" C 0 A_REfire("Fire.Begin")
"####" A 0 A_GiveInventory("DashSet_A")
"####" A 0 A_GiveInventory("PsychicRCFlag")
"####" A 0 A_Stop
"####" A 0 A_SpawnItemEX("Psychic_Remote")
"####" B 0 A_Jump(256,"Fire.Remote")
Goto Fire.Remote


Fire.Remote:
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After")
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_RELOAD)==1,"Fire.RemoteReload")
"####" B 0 A_JumpifInventory("PsychicRCFlag",1,"Fire.Remote")
Goto Fire.After
Fire.RemoteReload:
TNT1 A 0 A_GiveInventory("PPRecoveryItem") 
Goto Fire.Remote

Fire.After:
"####" B 1 A_GiveInventory("fullRecovery_A")
"####" A 0 A_GiveInventory("DashSetOff")
Fire.After2:
"####" B 1
"####" B 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After2")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor PsychicMove1 : PsychicMove {Weapon.SlotNumber 1}
actor PsychicMove2 : PsychicMove {Weapon.SlotNumber 2}
actor PsychicMove3 : PsychicMove {Weapon.SlotNumber 3}
actor PsychicMove4 : PsychicMove {Weapon.SlotNumber 4}

actor PsychicMove5 : PsychicMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("PsychicMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor PsychicPP : Ammo {inventory.amount 1 inventory.Maxamount 5}

actor PsychicRCFlag : Powerup {inventory.amount 1 powerup.duration 35 }

Const int PsychicRad = 175;
Const int Psychic_Forward = 150;
Const int Psychic_Range = 1000;

actor PsychicPPTaker : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_Psychic,1)
stop
}
}


//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor Psychic_Remote 
{
Speed 5
+MISSILE
+THRUACTORS
+NOCLIP
+FLOORHUGGER
+NOEXPLODEFLOOR
//+SLIDESONWALLS
//+BOUNCEONWALLS 
+NOGRAVITY
Health 5
Damage (0)
DamageType "Psychic"
obituary "$OB_Psychic"
radius 5
Reactiontime 50
height 16
scale 3.5
Args 0, 60
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 ACS_NamedExecuteAlways("CamGrabObject",0)
TNT1 A 0 ACS_NamedExecuteAlways("RemoteControlActor",0,15.0,Psychic_Range)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_DEFAULT)
Spawn2:
TNT1 A 1 A_JumpIfCloser(Psychic_Range-1,"Spawn3")
TNT1 A 0 A_SpawnItemEx("PsychicRingGuide2",Psychic_Forward,0,0,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("PsychicRCFlag",1)
TBLA A 0
loop
Spawn3:
TNT1 A 1 A_SpawnItemEx("PsychicRingGuide",Psychic_Forward,0,0,0,0,0,0,1)
//TNT1 A 0 A_SpawnItemEx("PsychicRingGuideA",Psychic_Forward,0,0,0,0,0,0,1)
//TNT1 A 0 A_SpawnItemEx("PsychicRingGuideB",Psychic_Forward,0,0,0,0,0,0,1)
//TNT1 A 0 A_SpawnItemEx("PsychicRingGuideC",Psychic_Forward,0,0,0,0,0,0,1)
//TNT1 A 0 A_SpawnItemEx("PsychicRingGuideD",Psychic_Forward,0,0,0,0,0,0,1)

TNT1 A 0 A_GiveToTarget("PsychicRCFlag",1)
TBLA A 0
goto Spawn2

Trigger: //Check PP
TNT1 A 0 A_JumpifInTargetInventory("PsychicPP",1,"Trigger2")
goto Decline
Trigger2: // Check Recovery Frame
TNT1 A 0 A_JumpifInTargetInventory("Recovery_A",1,"Decline")
goto Detonate

Detonate: // Cast the attack!
TNT1 A 0 A_GiveToTarget("PsychicPPTaker",1)
TNT1 A 0 A_SpawnItemEx("PsychicHitbox",Psychic_Forward,0,-(z-floorz)+60,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("fullRecovery_A",1)
goto PostTrigger

Decline: //Cannot cast the attack
TNT1 A 0 A_GiveToTarget("DeclineSoundCue",1)
goto PostTrigger

PostTrigger:
TNT1 AAAAA 1 A_SpawnItemEx("PsychicRingGuide",Psychic_Forward,0,0,0,0,0,0,1)
Goto Spawn2

Death:
BSTG AAAAAA 0 A_SpawnItemEx("RazorLeafParticle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
SPKH ABCD 2
stop
}
}


actor PsychicHitbox : BasicExplosion
{
Damage(0)
DamageType "Psychic"
obituary "$OB_Psychic"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NAPC A 0 A_PlaySoundEx("Move/Psychic1", "Weapon")
TNT1 A 2 A_SpawnItemEx("PsychicFX_A1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PsychicFX_A2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 22

TNT1 A 0 A_SpawnItemEx("PsychicFX_B",250,0,0,-50,0,0,90+(90*0),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PsychicFX_B",250,0,0,-50,0,0,90+(90*1),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PsychicFX_B",250,0,0,-50,0,0,90+(90*2),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PsychicFX_B",250,0,0,-50,0,0,90+(90*3),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 3
TNT1 A 0 A_SpawnItemEx("PsychicFX_B",250,0,0,-50,0,0,45+(90*0),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PsychicFX_B",250,0,0,-50,0,0,45+(90*1),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PsychicFX_B",250,0,0,-50,0,0,45+(90*2),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PsychicFX_B",250,0,0,-50,0,0,45+(90*3),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 5

TNT1 A 0 A_Explode(MBP_Psychic,PsychicRad,0,0,PsychicRad)
NAPC A 0 A_PlaySoundEx("Move/Psychic2", "Weapon")
TNT1 A 3 A_SpawnItemEx("PsychicFX_E",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Quake(4,18,0,350)
TNT1 AAAA 3 A_SpawnItemEx("PsychicFX_C",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Quake(8,18,0,350)

TNT1 A 35 
NAPC A 0 A_StopSoundEx("Weapon")
stop

Death:
TNT1 A 0 
stop
}
}

actor PsychicFX_A1 : BasicClientside
{
scale 0.5
Renderstyle "Add"
States
{
Spawn:
TNT1 A 0 
PSIK A 2 
stop
}
}

actor PsychicFX_A2 : BasicClientside
{
scale 0.05
reactiontime 8
Renderstyle "Add"
States
{
Spawn:
TNT1 A 0 
PSIK BCDE 1 A_SetScale(ScaleX+0.08)
TNT1 A 0 A_CountDown
loop
}
}

actor PsychicFX_B : BasicClientside
{
scale 1.0
Renderstyle "Add"
States
{
Spawn:
TNT1 A 0 
PSIK F 4
stop 
}
}

actor PsychicFX_C : BasicClientside
{
scale 1.0
XScale 0.9
reactiontime 5
Renderstyle "Add"
States
{
Spawn:
TNT1 A 0 
PSIK IJKLMN 1 
Spawn2:
PSIK N 1 A_SetScale(ScaleX+0.1,ScaleY+0.1)
PSIK N 0 A_Fadeout(0.3)
TNT1 A 0 A_CountDown
loop 
}
}

actor PsychicFX_D : BasicClientside
{
scale 1.0
reactiontime 5
Renderstyle "Add"
States
{
Spawn:
TNT1 A 0 
PSIK GHGHGH 3 A_ScaleVelocity(0.4)
Spawn2:
PSIK GGGHHH 3 A_Fadeout(0.4)
loop
}
}

actor PsychicFX_E : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 6
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("PsychicFX_D", 32, 0, 0, 36, 0, 15, angle+user_angle, 1061)
        TNT1 A 0 A_SpawnItemEx("PsychicFX_D", -32, 0, 0, -36, 0, 15, angle+user_angle, 1061)
        
        TNT1 A 0 A_SpawnItemEx("PsychicFX_D", 64, 0, 0, 36, 0, 0, angle+user_angle, 1061)
        TNT1 A 0 A_SpawnItemEx("PsychicFX_D", -64, 0, 0, -36, 0, 0, angle+user_angle, 1061)
        
        TNT1 A 0 A_SpawnItemEx("PsychicFX_D", 32, 0, 0, 36, 0, -15, angle+user_angle, 1061)
        TNT1 A 0 A_SpawnItemEx("PsychicFX_D", -32, 0, 0, -36, 0, -15, angle+user_angle, 1061)
        
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 30)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor PsychicImpactFX : BasicClientside
{
scale 4.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_Quake(4,12,0,250)
HITG FGH 1
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
TNT1 A 6
stop
}
}

actor PsychicRingGuide : BasicHelper
{
+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("PsychicGuideDot", PsychicRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("PsychicGuideDot", -PsychicRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor PsychicRingGuide2 : BasicHelper
{
	+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("PsychicGuideDot2", PsychicRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("PsychicGuideDot2", -PsychicRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor PsychicRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 150
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 175,"Terminate")
TNT1 A 0 A_SpawnItemEx("PsychicRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}


actor PsychicGuideDot : BasicGuideDot {}

actor PsychicGuideDot2 : BasicGuideDot {StencilColor "#F42A0C"}