actor SingMove : WepMasterBase
{
Tag "Sing"
obituary "$OB_Sing"
Weapon.AmmoUse 1
Weapon.AmmoGive 15
weapon.ammotype "SingPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
TNT1 A 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
TNT1 A 0
TNT1 A 0 A_Jumpifinventory("SingSXF_Buffer",1,2)
TNT1 A 0 A_GunFlash("Flash.Sound")
TNT1 A 0
TNT1 A 0 A_GiveInventory("SingSXF_Buffer",1)
TNT1 A 0 A_Jumpifinventory("ConShotTimer",1,"Fire.Bullet2")
TNT1 A 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
TNT1 A 0 A_FireCustomMissile("SingHitbox",0,1,0,0,0,0)
TNT1 A 0 A_FireCustomMissile("SingLaunchFX",Frandom(-2.5,2.5),0,0,0,0,Frandom(-2.5,2.5))
TNT1 A 0 A_GiveInventory("SingCST",1)
TNT1 A 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Bullet2:
TNT1 A 0 A_FireCustomMissile("SingHitbox",0,0,0,0,0,0)
TNT1 A 0 A_FireCustomMissile("SingLaunchFX",Frandom(-2.5,2.5),0,0,0,0,Frandom(-2.5,2.5))
TNT1 A 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
TNT1 A 1 A_GiveInventory("3Recovery_A")
TNT1 A 0 A_REfire("Fire")
TNT1 A 0 A_GiveInventory("MoveCooldown_3")
TNT1 A 0
TNT1 A 0 A_TakeInventory("ConShotTimer",99)
TNT1 A 0 A_Jump(256,"Fire.End")
Goto Fire.End

Flash.Sound:
TNT1 A 0 A_PlaySoundEx("Move/Sing","Weapon")
Flash.Sound2:
TNT1 A 5
TNT1 A 0 A_Jumpifinventory("SingSXF_Buffer",1,"Flash.Sound2")
TNT1 A 0 A_StopSoundEX("Weapon")
Stop

}
}

actor SingMove1 : SingMove {Weapon.SlotNumber 1}
actor SingMove2 : SingMove {Weapon.SlotNumber 2}
actor SingMove3 : SingMove {Weapon.SlotNumber 3}
actor SingMove4 : SingMove {Weapon.SlotNumber 4}

actor SingMove5 : SingMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("SingMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}
//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor SingPP : Ammo {inventory.amount 1 inventory.Maxamount 15}

actor SingSXF_Buffer : Powerup {inventory.amount 1 Powerup.Duration 7}


actor SingCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 70
powerup.type "ConShotTimer"
}

Const int SingRad = 300;
Const int Sing_Item = 31;


actor Singifier : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",0,STATUS_MAJOR),"Nope") //Blocked by protect|Detect|Substitute|Safeguard
//==
TNT1 A 0 A_Jumpifinventory("HypnoMark",1,"Sustain")
TNT1 A 0 A_GiveInventory("HypnoMark",1)
TNT1 A 0 A_GiveInventory("HypnoRate",15)
TNT1 A 0 A_SpawnItemEX("SingEffectWatcher")
TNT1 A 0 A_Playsoundex("support/Negative","Soundslot5")
stop

Sustain:
TNT1 A 0 A_GiveInventory("HypnoMark",1)
TNT1 A 0 A_GiveInventory("HypnoRate",10)
stop

Nope:
TNT1 A 0
stop
}
}

actor SingStop : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("ConShotTimer",1,"Nope")
TNT1 A 0 A_StopSoundEX("Weapon")
stop

Nope:
TNT1 A 0
stop
}
}
    
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor SingLaunchFX : BasicProjectile
{
Speed 10
+RIPPER
Damage (0)
DamageType "Sing"
obituary "$OB_Sing"
radius 16
Reactiontime 15
height 16
scale 1.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
//==
TNT1 A 1 
TNT1 A 0 A_SpawnItemEx("GrassWhistleParticle", 16, random2(16), 0, 0, 0, random(4,8), random(0, 359),1)
Goto Death

Death:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 4
stop
}
}

actor SingHitbox : BasicExplosion
{
Damage(0)
speed 1
DamageType "Sing"
obituary "$OB_Sing"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,Sing_Item,SingRad,RG_ENEMY)
TNT1 A 0 A_SpawnItemEx("SingFXRing", 0, 0, 28, 0, 0, 0, random(0, 60),1)
TNT1 A 5
stop

Death:
TNT1 A 0 
stop
}
}

actor SingTagPickup : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("SingSleepParticle", random2(256/24), random2(256/24), 45, 2, 2, 2, random(0, 359))
stop
}
}

actor SingSleepParticle : BasicClientside
{
scale 1.0
Renderstyle "Translucent"
alpha 0.8
speed 7
States
{
Spawn:
TNT1 A 0
PSBB FFFFFGH 2
stop
}
}

actor SingEffectWatcher //: CustomInventory
{
+MISSILE
+RIPPER
+NOGRAVITY
+NOINTERACTION
Reactiontime 5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 0 A_JumpIfInTargetInventory("CureAll",1,"DeathTake")
TNT1 A 0 A_GiveToTarget("HypnoMark")
TNT1 A 5 

TNT1 A 0 A_JumpIfInTargetInventory("HypnoRate",100,"FullHypno")
Goto PartHypno

PartHypno:
TNT1 A 0 A_GiveToTarget("SingTagPickup")
TNT1 A 0 A_JumpIfInTargetInventory("HypnoMark",1,"HypnoCycle")
Goto Death

FullHypno:
TNT1 A 0 A_GiveToTarget("SleepStatusProc")
Goto Death

HypnoCycle:
TNT1 A 1 A_TakefromTarget("HypnoRate",3)
TNT1 A 0 A_JumpIfInTargetInventory("CureAll",1,"DeathTake")
TNT1 A 0 A_JumpIfInTargetInventory("HypnoRate",1,"Spawn")
Goto Death

Death:
DeathTake:
TNT1 A 0
TNT1 A 1 A_TakefromTarget("HypnoMark",999)
TNT1 A 1 A_TakefromTarget("HypnoRate",999)
stop
}
}

actor SingFXRing //: BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 3
    var int user_angle;
    PROJECTILE
    +NOINTERACTION
    +NOCLIP
    +CLIENTSIDEONLY
    +NOTARGETSWITCH
	States
	{
		Spawn:
		TNT1 A 0
        TNT1 A 0 A_SetUserVar("user_angle", angle)
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        SpawnLoop:
        TNT1 A 0 A_SpawnItemEx("GrassWhistleParticle",SingRad-25,0, 0, 0, 0, random(4,8),user_angle,1061|SXF_TRANSFERPOINTERS,0,tid)
        TNT1 A 0 A_SpawnItemEx("GrassWhistleParticle",-SingRad+25,0, 0, 0, 0, random(4,8),user_angle,1061|SXF_TRANSFERPOINTERS,0,tid)
		TNT1 A 1 A_SetUserVar("user_angle", user_angle + 60)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}