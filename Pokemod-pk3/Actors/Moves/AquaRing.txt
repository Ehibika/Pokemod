actor AquaRingMove : WepMasterBase
{
Tag "AquaRing"
obituary "$OB_AQUARING"
Weapon.AmmoUse 1
Weapon.AmmoGive 25
weapon.ammotype "AquaRingPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_JumpIfInventory("AquaRingFlag",1,"Fire.Off")
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("AquaRingRingSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_FireCustomMissile("AquaRingSpawner",0,1,0,0)

"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Off:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_TakeInventory("AquaRingFlag",99)
"####" B 17 
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("SemiRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor AquaRingMove1 : AquaRingMove {Weapon.SlotNumber 1}
actor AquaRingMove2 : AquaRingMove {Weapon.SlotNumber 2}
actor AquaRingMove3 : AquaRingMove {Weapon.SlotNumber 3}
actor AquaRingMove4 : AquaRingMove {Weapon.SlotNumber 4}

actor AquaRingMove5 : AquaRingMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("AquaRingMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor AquaRingPP : Ammo {inventory.amount 1 inventory.Maxamount 25}

actor AquaRingFlag : inventory {inventory.amount 1 inventory.Maxamount 1}
actor AquaRingPPTimer : Powerup {inventory.amount 1 Powerup.Duration 35}

Const int AQRG_RAD = 150;
Const int AQRG_Item = 7;
Const int AQRGEntiTID = 3;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor AquaRingRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("AquaRingGuideDot", AQRG_RAD, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("AquaRingGuideDot", -AQRG_RAD, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor AquaRingRingSpawner : BasicFastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 175
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("AquaRingRingGuide",0,0,0,0,0,0,0,1)
stop
}
}

actor AquaRingGuideDot : BasicGuideDot {}

actor AquaRingSpawner : BasicFastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 175
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("AquaRingField",0,0,-(z-floorz)+24,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 1
stop
}
}


actor AquaRingField : BasicHelper
{
translation "202:202=248:248", "199:199=41:41"
	reactiontime 35
    var int user_angle;
    var int user_Tick;
	States
	{
		Spawn:
		TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 ACS_NamedExecuteAlways("GiveEntiTID",0,AQRGEntiTID)
        TNT1 A 0 A_PlaySoundEx("weapon/pharaoh1","Weapon")
        TNT1 A 0 A_GiveToTarget("AquaRingFlag")
        TNT1 A 0 A_GiveToTarget("AquaRingPPTimer")
        TNT1 A 0 A_SpawnItemEX("AquaRingPPWatcher")


        TNT1 A 1
        SpawnLoop:
        TNT1 A 2
		TNT1 A 0 A_SpawnItemEx("AquaRingParticleSpawn", AQRG_RAD, 0, 0, 0, 0, 0, user_angle, SXF_TRANSFERPOINTERS|SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SpawnItemEx("AquaRingParticleSpawn", AQRG_RAD, 0, 0, 0, 0, 0, user_angle+120, SXF_TRANSFERPOINTERS|SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SpawnItemEx("AquaRingParticleSpawn", AQRG_RAD, 0, 0, 0, 0, 0, user_angle+240, SXF_TRANSFERPOINTERS|SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_SetUserVar("user_angle", user_angle + 6)
        TNT1 A 0 A_SetUserVar("user_Tick", user_Tick + 1)
		TNT1 A 0 A_JumpIf(user_Tick < 7, "SpawnLoop")
		TNT1 A 0 ACS_NamedExecuteAlways("MoveAreaHeal",0,CallACS("StatValueRamp2",STAT_SDEF,5,35),AQRG_Rad,Move_AquaRing)
        //TNT1 A 0 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,AQRG_Item,AQRG_Rad,RG_TEAM|RG_SELF)
        TNT1 A 0 A_SetUserVar("user_Tick", 0)
        //TNT1 A 0 A_Countdown
        TNT1 A 0 A_JumpIfInTargetInventory("AquaRingFlag",1,"SpawnLoop")
		goto Death
		Death:
		TNT1 A 0
		stop
	}
}


actor AquaRingHealFX : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpIf(CallACS("PKMNACS_HealthPercent",0)>=100,"Nope")
LASH A 0 A_JumpIf(ACS_NamedExecuteWithResult("PKMNACS_ActorClassifier",0,2) == TRUE, "HealMe")
stop

HealMe:
TNT1 A 0 A_Giveinventory("HealEffectSpawner",1)
TNT1 A 0
stop

Nope:
TNT1 A 0
stop
}
}

actor AquaRingPPWatcher : BasicHelper 
{
States
{
Spawn:
TNT1 A 0
TNT1 A 5 
TNT1 A 0 A_JumpIfInTargetInventory("AquaRingPPTimer",1,"Spawn")
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"Death")
TNT1 A 0 A_GiveToTarget("AquaRingPPTimer")
Goto Check1

Check1:
TNT1 A 0 A_JumpIfInTargetInventory("AquaRingFlag",1,"Check2")
TNT1 A 0
Goto Death

Check2:
TNT1 A 0 A_TakeFromTarget("AquaRingPP",1)
TNT1 A 0 A_JumpIfInTargetInventory("AquaRingPP",1,"Spawn")
TNT1 A 0
Goto Death

Death:
TNT1 A 0 A_TakeFromTarget("AquaRingFlag",99)
TNT1 A 0 
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor AquaRingBubble  
{
PROJECTILE
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
height 2
Radius 2
damage (0)
reactiontime 1
scale 2
Alpha 1.0
States
{
Spawn:
BMBU D 0 Thrustthingz(0,random(1,4),0,0)
BMBU D 0 A_SetScale(Frandom(0.2,0.8))
BMBU DDD 3 A_ScaleVelocity(0.8)
BMBU D 17
BMBU C 5 A_SpawnItemEx("WaterGunDripFX",0,0,0,random(2,5),0,0,random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
stop
}
}


actor AquaRingFX1 : BasicClientside
{
scale 2.5
ReactionTime 1
Renderstyle "ADD"
Alpha 0.9
+FLOORHUGGER
States
{
Spawn:
TNT1 A 0
WWAV ABCDDCCBBA 3 
TNT1 A 5
TNT1 A 0 A_CountDown
loop

Death:
TNT1 A 0
stop
}
}

actor AquaRingSparkle : BasicClientside
{
scale 1.2
ReactionTime 3
Renderstyle "ADD"
Alpha 0.9
States
{
Spawn:
TNT1 A 0
BMBU D 0 Thrustthingz(0,random(2,6),0,0)
Spawn2:
TMPK DCCBBA 2 
TNT1 A 5
TNT1 A 0 A_CountDown
loop

Death:
TNT1 A 0
stop
}
}

actor AquaRingParticleSpawn : BasicHelper
{
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 1 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_SpawnItemEx("AquaRingFX1",0,0,-(z-floorz),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 AA 0 A_SpawnItemEx("AquaRingSparkle",random(-10,-10),random(-10,-10),random(-10,-10),0,0,0,random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        //TNT1 AAA 0 A_SpawnItemEx("AquaRingBubble",0,0,0,random(-1,1),0,0,random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        stop
    }

}

actor AquaRingHeal : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpIf(CallACS("PKMNACS_HealthPercent",0)>=100,"Nope")
LASH A 0 A_JumpIf(ACS_NamedExecuteWithResult("PKMNACS_ActorClassifier",0,2) == TRUE, "WaterCheck")
stop
WaterCheck:
LASH A 0 A_JumpIfInventory("WaterSTAB",1,"HealMe")
stop
HealMe:
TNT1 A 0 //HealThing(CallACS("StatValueRamp",STAT_SATK,1,80))
TNT1 A 0 ACS_NamedExecuteAlways("PKMNACS_Basic(Over)Heal",0,0,(CallACS("PKMNACS_HealthPercent",0,1,5)))
TNT1 A 0 A_Giveinventory("HealEffectSpawner",1)
TNT1 A 0 A_PlaySoundEx("item/energyup","SoundSlot7")
TNT1 A 0
stop

Nope:
TNT1 A 0
stop
}
}