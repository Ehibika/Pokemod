actor SelfDestructMove : WepMasterBase
{
Tag "SelfDestruct"
obituary "$OB_SELFDESTRUCT"
Weapon.AmmoUse 1
Weapon.AmmoGive 30
weapon.ammotype "SelfDestructPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1 A_JumpifInventory("ExplosionTrigger",1,"Fire.Off")
"####" B 1 A_JumpifInventory("SelfDestructTrigger",1,"Fire.Off")
"####" B 0 A_Jump(256,"Fire.On")
Goto Fire.On

Fire.On:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 1 A_GiveInventory("SelfDestructTrigger")
"####" B 0 A_GiveInventory("SelfDestructPPTimer")
"####" B 0 A_SpawnItemEX("SelfDestructPPWatcher")
"####" B 0 A_SpawnItemEX("SelfDestructWatcher",48,0,0,0,0,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Off:
"####" B 1 A_JumpifInventory("IsBot",1,"Fire.After")
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_TakeInventory("ExplosionTrigger",99)
"####" B 0 A_TakeInventory("SelfDestructTrigger",99)
"####" B 17 
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor SelfDestructMove1 : SelfDestructMove {Weapon.SlotNumber 1}
actor SelfDestructMove2 : SelfDestructMove {Weapon.SlotNumber 2}
actor SelfDestructMove3 : SelfDestructMove {Weapon.SlotNumber 3}
actor SelfDestructMove4 : SelfDestructMove {Weapon.SlotNumber 4}

actor SelfDestructMove5 : SelfDestructMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("SelfDestructMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor SelfDestructPP : Ammo {inventory.amount 1 inventory.Maxamount 30}


actor SelfDestructTrigger : inventory {inventory.amount 1 inventory.Maxamount 1}

actor SelfDestructPPTimer : Powerup {inventory.amount 1 Powerup.Duration 35}

actor SelfDestructGiver : BasicHelper 
{
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_JumpIfInTargetInventory("SuicideFlag",1,"Nope")
TNT1 A 1 A_JumpIfInTargetInventory("SelfDestructTrigger",1,"Give")
stop

Give:
TNT1 A 1 
TNT1 A 0 A_GiveToTarget("SelfDestructSensor")
stop

Nope:
TNT1 A 0
stop
}
}

actor SelfDestructSensor : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop

Pickup:
TNT1 A 0 A_SpawnItemEX("SelfDestruct_Blast",0,0,0,0,0,0,0)
stop

Nope:
TNT1 A 0
stop
}
}


Const int SelfDestruct_RAD = 500;
Const int SelfDestruct_RAD2 = 350;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]


actor SelfDestructWatcher : BasicHelper
{
    States
    {
    Spawn:
        TNT1 A 0 
        TNT1 A 5 A_GiveToTarget("SelfDestructParticleSpawn")
        TNT1 A 0 A_JumpIfInTargetInventory("SelfDestructTrigger",1,"Spawn")
    Death:
        TNT1 A 0
        stop

    }
}

actor SelfDestructPPWatcher : BasicHelper 
{
States
{
Spawn:
TNT1 A 0
TNT1 A 5 
TNT1 A 0 A_JumpIfInTargetInventory("SelfDestructPPTimer",1,"Spawn")
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"Death")
TNT1 A 0 A_GiveToTarget("SelfDestructPPTimer")
Goto Check1

Check1:
TNT1 A 0 A_JumpIfInTargetInventory("SelfDestructTrigger",1,"Check2")
TNT1 A 0
Goto Death

Check2:
TNT1 A 0 A_TakeFromTarget("SelfDestructPP",1)
TNT1 A 0 A_JumpIfInTargetInventory("SelfDestructPP",1,"Spawn")
TNT1 A 0
Goto Death

Death:
TNT1 A 0 A_TakeFromTarget("SelfDestructTrigger",99)
TNT1 A 0 
stop
}
}


actor SelfDestructPrimeParticle : BasicClientSide
{
+FORCEXYBILLBOARD
+BRIGHT
renderstyle "add"
scale 1.0
States
{

Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_Jump(256,"Spawn1","Spawn2")
Goto Spawn1


Spawn1:
HARD BCDE 2
stop

Spawn2:
HARD EDCB 2
stop

}
}

actor SelfDestructPrimeParticle2 : BasicClientSide
{
+FORCEXYBILLBOARD
+BRIGHT
renderstyle "Translucent"
scale 1.5
States
{

Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4")
Goto Spawn1


Spawn1:
MUDB MMMMM 2 A_Fadeout
stop

Spawn2:
MUDB NNNNN 2 A_Fadeout
stop

Spawn3:
MUDB OOOOO 2 A_Fadeout
stop

Spawn4:
MUDB PPPPP 2 A_Fadeout
stop
}
}

actor SelfDestructParticleSpawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 AAAAAA 0 A_SpawnItemEx ("SelfDestructPrimeParticle", random2(8), random2(8), 64, 0, 0, random(3,6), 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 AA 0 A_SpawnItemEx ("SelfDestructPrimeParticle2", random2(8), random2(8), 64, 0, 0, random(3,6), 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
stop
Nope:
TNT1 A 0
stop
}
}

actor SelfDestruct_Blast : BasicExplosion
{
damagetype "SelfDestruct"
Obituary "$OB_SELFDESTRUCT"
States
{
Spawn:
TNT1 A 0
//TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H0",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H2",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H3",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H4",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H5",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H6",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",0,0,2,0,0,17,0,33)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",0,0,-2,0,0,-17,0,33)
//110
TNT1 A 0 A_Explode(MBP_SelfDestruct,SelfDestruct_RAD,0,0,SelfDestruct_RAD2)
TNT1 A 0 A_Playsoundex("weapon/TimeBombSplode","Soundslot5")
TNT1 A 17 A_Quake(6,17,0,300+SelfDestruct_RAD)
stop
}
}

actor SelfDestructBlastFX_H0 : BasicClientSide
{
renderstyle none
Reactiontime 12//Sections
Args 20,6
//Speed/lines
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetArg(3,Args[1]+1)
TNT1 A 0 A_SetArg(4,Reactiontime)
SpawnL:
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",Cos(90-180/Args[3]*Args[1])*Args[0],0,Sin(90-180/Args[3]*Args[1])*Args[0],Cos(90-180/Args[3]*Args[1])*Args[0],0,Sin(90-180/Args[3]*Args[1])*Args[0],360/Args[4]*Reactiontime,33)
TNT1 A 0 A_SetArg(1,Args[1]-1)
TNT1 A 0 A_JumpIf(Args[1]<=0,1)
loop
TNT1 A 0 A_Countdown
TNT1 A 0 A_SetArg(1,Args[3]-1)
loop
Death:
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",0,0,Args[0],0,0,Args[0],0,33)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",0,0,-Args[0],0,0,-Args[0],0,33)
TNT1 A 1
stop
}
}

actor SelfDestructBlastFX_H1 : BasicClientSide
{
var int user_RT;
var int user_A;

Mass 100
MeleeRange 0
accuracy 6
renderstyle none
reactiontime 15//18
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_RT",360/reactiontime)
TNT1 A 0 A_SetUserVar("user_A",random(0,user_RT))
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",Mass/accuracy,0,0,Mass/accuracy,0,MeleeRange/accuracy,user_A,37)
TNT1 A 0 A_SetUserVar("user_A",user_A+user_RT)
TNT1 A 0 A_Countdown
goto Spawn+3
Death:
TNT1 A 1
stop
}
}
actor SelfDestructBlastFX_H2 : SelfDestructBlastFX_H1
{
Mass 96
MeleeRange 26
reactiontime 12//15
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_RT",360/reactiontime)
TNT1 A 0 A_SetUserVar("user_A",random(0,user_RT))
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",Mass/accuracy,0,MeleeRange/accuracy,Mass/accuracy,0,MeleeRange/accuracy,user_A,37)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",Mass/accuracy,0,MeleeRange/-accuracy,Mass/accuracy,0,MeleeRange/-accuracy,user_A,37)
TNT1 A 0 A_SetUserVar("user_A",user_A+user_RT)
TNT1 A 0 A_Countdown
goto Spawn+3
Death:
TNT1 A 1
stop
}
}

actor SelfDestructBlastFX_H3 : SelfDestructBlastFX_H2
{
Mass 87
MeleeRange 50
reactiontime 10//12
}
actor SelfDestructBlastFX_H4 : SelfDestructBlastFX_H2
{
Mass 70
MeleeRange 70
reactiontime 8//9
}
actor SelfDestructBlastFX_H5 : SelfDestructBlastFX_H2
{
Mass 50
MeleeRange 87
reactiontime 6
}
actor SelfDestructBlastFX_H6 : SelfDestructBlastFX_H2
{
Mass 26
MeleeRange 96
reactiontime 4//3
}

actor SelfDestructBlastFX : BasicClientSide
{
+FORCEXYBILLBOARD
+BRIGHT
renderstyle "add"
States
{

    Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
HARD CBA 1
ASEX ABBCD 1
ASEX EEFFF 1 A_Fadeout
stop

    /*
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
MMFX S 0
MMFX S 1
MMFX TJKD 1
MMFX S 1
MMFX TJKD 1
stop
*/
}
}