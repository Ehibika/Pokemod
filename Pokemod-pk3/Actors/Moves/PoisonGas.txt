actor PoisonGasMove : WepMasterBase
{
Tag "PoisonGas"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "PoisonGasPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("PoisonGasRingSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_FireCustomMissile("PoisonGasSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("FullRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor PoisonGasMove1 : PoisonGasMove {Weapon.SlotNumber 1}
actor PoisonGasMove2 : PoisonGasMove {Weapon.SlotNumber 2}
actor PoisonGasMove3 : PoisonGasMove {Weapon.SlotNumber 3}
actor PoisonGasMove4 : PoisonGasMove {Weapon.SlotNumber 4}

actor PoisonGasMove5 : PoisonGasMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("PoisonGasMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor PoisonGasPP : Ammo {inventory.amount 1 inventory.Maxamount 10}
actor PsnGasFieldKiller : Powerup {inventory.amount 1 Powerup.Duration 7}

actor STF_PoisonGas : Powerup {inventory.amount 1 Powerup.Duration 35 } //Speed 0.4

actor PoisonGasNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",0,STATUS_STATS),"Nope") //Blocked by Protect|Detect|Substitute|Mist
TNT1 A 0 A_Jumpifinventory("STF_PoisonGas",1,"Sustain")
TNT1 A 0 A_GiveInventory("STF_PoisonGas",1)
TNT1 A 0 A_SpawnItemEX("PoisonGasNerfWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_PoisonGas",1)
stop
Nope:
TNT1 A 0
stop
}
}

Const int PsnGasRad = 150;
Const int PsnGas_Item = 3;
Const int PsnGasEntiTID = 1;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor PoisonGasRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("PoisonGasGuideDot", PsnGasRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("PoisonGasGuideDot", -PsnGasRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor PoisonGasRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 600
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("PoisonGasRingGuide",0,0,0,0,0,0,0,1)
stop
}
}

actor PoisonGasGuideDot : BasicGuideDot {}

actor PoisonGasSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 600
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("PoisonGasField",0,0,0,0,0,0,0,1)
stop
}
}


actor PoisonGasField : BasicHelper
{
ReactionTime 25
DamageType "PoisonGas"
obituary "$OB_PoisonGas"
var int user_angle;
var int user_Pitch;
var int user_Tick;
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
TNT1 A 0 //ACS_NamedExecuteAlways("GiveEntiTID",0,PsnGasEntiTID)

//TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
//TNT1 A 0 A_GiveToTarget("PsnGasFieldKiller",1)
//TNT1 A 10
//TNT1 A 0 A_TakeFromTarget("PsnGasFieldKiller",1)

SpawnLoop:
TNT1 A 1
TNT1 A 0 A_SetUserVar("user_angle", random(1, 360))
TNT1 A 0 A_SetUserVar("user_Pitch", random(-90, 90))
TNT1 A 0 A_SpawnItemEx("PoisonGasPetal",cos(user_Pitch)*-PsnGasRad,0,(sin(user_Pitch)*PsnGasRad),0,0,2,user_angle,SXF_TRANSFERPITCH|SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("PoisonSmoke",cos(user_Pitch)*-PsnGasRad,0,(sin(user_Pitch)*PsnGasRad),0,0,0,user_angle,SXF_TRANSFERPITCH|SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetUserVar("user_angle", random(1, 360))
TNT1 A 0 A_SetUserVar("user_Pitch", random(-90, 90))
TNT1 A 0 A_SpawnItemEx("PoisonSmoke",cos(user_Pitch)*-PsnGasRad,0,(sin(user_Pitch)*PsnGasRad),0,0,0,user_angle,SXF_TRANSFERPITCH|SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetUserVar("user_angle", random(1, 360))
TNT1 A 0 A_SetUserVar("user_Pitch", random(-90, 90))
TNT1 A 0 A_SpawnItemEx("PoisonSmoke",cos(user_Pitch)*-PsnGasRad,0,(sin(user_Pitch)*PsnGasRad),0,0,0,user_angle,SXF_TRANSFERPITCH|SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)

TNT1 A 0 A_SetUserVar("user_Tick", user_Tick+1)
TNT1 A 0 A_jumpIfInTargetInventory("PsnGasFieldKiller",1,"Death")
TNT1 A 0 A_JumpIf(user_Tick < 7, "SpawnLoop")
TNT1 A 1 A_Explode(1,PsnGasRad+25,0,0,PsnGasRad+25)
//TNT1 A 0 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,PsnGas_Item,PsnGasRad,RG_ENEMY)
TNT1 A 0 A_SetUserVar("user_Tick", 0)

TNT1 A 0 A_CountDown
Loop 

Death:
TNT1 A 1 
stop
}
}


actor PoisonSmoke : BasicClientside
{
scale 2.5
RenderStyle "Translucent"
Alpha 0.5
States
{
Spawn:
TNT1 A 0 
TNT1 A random(0,10)
TNT1 A 1 A_Changevelocity(random(-2,2),random(-2,2),random(2,2),CVF_RELATIVE)
TOXI ABC 3 A_ScaleVelocity(0.5)
TOXI CDEFG 3
TOXI GGGG 2 A_Fadeout
stop
}
}

actor PoisonGasPetal : BasicClientside
{
scale 0.5
RenderStyle "Translucent"
Alpha 0.5
ReactionTime 3
-NOINTERACTION
States
{
Spawn:
TNT1 A 0 
WAVB GGGGG 2
WAVB H 3 A_Stop
stop
}
}

actor PoisonGasNerfWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_GiveToTarget("SpdDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_PoisonGas",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}