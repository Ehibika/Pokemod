actor SandTombMove : WepMasterBase
{
Tag "SandTomb"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "SandTombPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_JumpifInventory("SandTombBuffer",1,"NoAmmo2")
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("SandTombRingSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_FireCustomMissile("SandTombSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("FullRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor SandTombMove1 : SandTombMove {Weapon.SlotNumber 1}
actor SandTombMove2 : SandTombMove {Weapon.SlotNumber 2}
actor SandTombMove3 : SandTombMove {Weapon.SlotNumber 3}
actor SandTombMove4 : SandTombMove {Weapon.SlotNumber 4}

actor SandTombMove5 : SandTombMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("SandTombMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor SandTombPP : Ammo {inventory.amount 1 inventory.Maxamount 10}
Actor SandTombBuffer : Powerup {inventory.amount 1 Powerup.duration 17}

Const int SndTmbRad1 = 200;
Const int SndTmbRad2 = 125;
Const int SndTmb_Pull = 7;
Const int SndTmbEntiTID = 6;
Const int SndTmb_Time = 40;

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor SandTombRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("SandTombGuideDot", SndTmbRad1, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("SandTombGuideDot", -SndTmbRad1, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor SandTombRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 300
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("SandTombRingGuide",0,0,0,0,0,0,0,1)
stop
}
}

actor SandTombGuideDot : BasicGuideDot {}

actor SandTombSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 300
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("SandTombTunnel",0,0,-(z-floorz)+1,0,0,0,0,1)
stop
}
}


actor SandTombTunnel
{
PROJECTILE
+NOINTERACTION
+FORCEXYBILLBOARD
+RIPPER
damage (0)
Obituary "$OB_SANDTOMB"
Height 1
Radius 1
damagetype "SandTomb"
reactiontime 12
Args 25
Scale 4.2

Renderstyle "Translucent"
Alpha 0.1

States
{
Spawn:
TBLO E 0
TBLO E 0 A_SetArg(0,SndTmb_Time)
TBLO E 0 A_GiveInventory("ProjectileCode",1)
TBLO E 0 A_PlaySoundEx("misc/commandosandstorm", "body", 1)
TBLO FFF 0 A_SpawnItemEx("SandTombWhirlProjectile", 0, 0, 45, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
TBLO FGH 1 A_FadeIn(0.3)
TBLO FFF 0 A_SpawnItemEx("SandTombWhirlProjectile", 0, 0, 45, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
TBLO FGH 1 A_FadeIn(0.3)
Spawn2:
TBLO E 0 A_Explode(MBP_SandTomb,SndTmbRad2,0,0,SndTmbRad2)
TBLO FFF 0 A_SpawnItemEx("SandTombWhirlProjectile", 0, 0, 45, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
TBLO F 0 A_GiveToTarget("SandTombBuffer")
TBLO F 1 ACS_ExecuteAlways(247, 0, SndTmb_Pull, SndTmbRad1,0)
TBLO F 1 A_Countdownarg(0,"Death")

TBLO G 1 ACS_ExecuteAlways(247, 0, SndTmb_Pull, SndTmbRad1,0)
TBLO G 1 A_Countdownarg(0,"Death")

TBLO H 1 ACS_ExecuteAlways(247, 0, SndTmb_Pull, SndTmbRad1,0)
TBLO H 1 A_Countdownarg(0,"Death")
goto Spawn2
Death:
//TNT1 A 3//Finish the sound effect before stopping
TBLO FGHFGH 1 A_FadeOut(0.2)
TNT1 A 0 A_StopSound
stop
}
}


actor SandTombWhirlProjectile : BasicProjectile
{  //Knew this would come in handy!
-SERVERSIDEONLY
reactiontime 40
DamageType "SandTomb"
obituary "$OB_SandTomb"
Renderstyle "Translucent"
Alpha 0.5
scale 1.2
speed 7
+RIPPER
+NOINTERACTION
+NOCLIP
var int user_speed;
var int user_maxturn;
var int user_turndivisor;
var int user_turndivisor_add;
var int user_Rad;
States
{
Spawn:
POWS A 0
TNT1 A 0 A_SetUserVar("user_Rad", random(SndTmbRad2,SndTmbRad1))
TNT1 A 0 A_SetUserVar("user_turndivisor", 9)
TNT1 A 0 A_SetUserVar("user_speed", ((2*(3.14)*user_Rad)/360)*user_turndivisor)
TNT1 A 0 A_Warp(AAPTR_DEFAULT,user_Rad,0,0,random(0,360),WARPF_NOCHECKPOSITION)
TNT1 A 0 A_Setangle(angle-90)
TNT1 A 0 A_SetScale(Frandom(1.5,3.5))
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4")
Goto Spawn1

Spawn1:

MUDB M 0
Goto Spawn.Orbit

Spawn2:
MUDB N 0
Goto Spawn.Orbit

Spawn3:
MUDB O 0
Goto Spawn.Orbit

Spawn4:
MUDB P 0
Goto Spawn.Orbit

Spawn.Orbit:
"####" "#" 0 A_Setangle(angle-user_turndivisor)
"####" "#" 1 A_ChangeVelocity(user_speed,0,1,CVF_RELATIVE|CVF_REPLACE)
"####" "#" 0 A_CountDown
loop
Death:
POWS A 0
stop
}
}