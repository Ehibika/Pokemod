actor TwisterMove : WepMasterBase
{
Tag "Twister"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "TwisterPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_D)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("TwisterRingSpawner",0,0,0,Aim_OverHead)
//"####" B 0 A_FireCustomMissile("TwisterGraphicSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/Usemove","Weapon")
"####" B 0 A_FireCustomMissile("TwisterSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("Twister_Recovery_D")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor TwisterMove1 : TwisterMove {Weapon.SlotNumber 1}
actor TwisterMove2 : TwisterMove {Weapon.SlotNumber 2}
actor TwisterMove3 : TwisterMove {Weapon.SlotNumber 3}
actor TwisterMove4 : TwisterMove {Weapon.SlotNumber 4}

actor TwisterMove5 : TwisterMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("TwisterMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor TwisterPP : Ammo {inventory.amount 1 inventory.Maxamount 10}


Const int TwisterRad = 75;
Const int Twister_Item = 23;


Actor TwisterProtect : powerprotection
{
Powerup.duration 5
Damagefactor "Twister", 0
}

actor Twister_Recovery_D : FullRecovery_D{Powerup.Duration 87} //87

actor TwisterCringeChance : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jump(170,"Cringe")
stop
Cringe:
TNT1 A 0 A_GiveInventory("CringeStatusProc")
stop

Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor TwisterRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("TwisterGuideDot", TwisterRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("TwisterGuideDot", -TwisterRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor TwisterRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 450
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_SpawnItemEx("TwisterRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}

/*
actor TwisterGraphicSpawner : TwisterRingSpawner
{
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_SpawnItemEx("Twister_Graphic1",0,0,-(z-floorz)+60,0,0,0,0,0)
stop

Terminate:
TNT1 A 0 
stop
}
}
*/
actor TwisterGuideDot : BasicGuideDot {}

actor TwisterSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 450
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("TwisterSource",0,0,-(z-floorz),0,0,0,0,1)
TNT1 A 1
stop

Terminate:
TNT1 A 0 
stop
}
}


actor TwisterSource
{
+DONTBLAST
+NOEXPLODEFLOOR
+FIXMAPTHINGPOS
+RIPPER
+DONTREFLECT
Scale 2.5
Height 48
Radius 20
Speed 0
Damage (0)
reactiontime 40
States
{
Spawn:
TNT1 A 1 
TNT1 B 0 A_CountDown
TNT1 A 0 A_ChangeFlag("MISSILE",1)
TNT1 A 0 A_ChangeFlag("FLOORHUGGER",1)
TNT1 A 0 A_ChangeFlag("NOCLIP",1)
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 AAAAA 4 A_SpawnItemEx("TwisterFXSpawn",0,0,0,0,0,0,0,1)
TNT1 A 0 A_PlaySoundEx("weapon/tornadohold","Weapon")
Holding:
TNT1 A 0 A_SpawnItemEx("TwisterFXSpawn",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("TwisterTornado",0,0,15,0,0,0,0,SXF_NOCHECKPOSITION)
TNT1 AA 2
TORH D 0 A_GiveInventory("CutterFlag", 1)
TORH D 0 A_JumpIfInventory("CutterFlag", 5, "End")
loop
End:
TNT1 A 0
stop
Death:
FLMB A 0 
stop
}
}

actor TwisterTornado
{
PROJECTILE
damage(0) 
+DONTREFLECT
damagetype "Twister"
Obituary "$OB_TWISTER"
height 6
radius 16
Scale 4.5
Speed 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TORH FF 0 ThrustThingZ(0,40,0,0)
Loop1:
TORH F 0 A_JumpIf(z-floorz>=288,"Death")
TNT1 A 0 A_Explode(MBP_TWISTER,TwisterRad,0,0,TwisterRad/2)
TORH FFF 1 
TNT1 A 0 A_CheckSightOrRange(TwisterRad,"Loop2")
goto Lift1
Loop2:
TORH G 0 A_JumpIf(z-floorz>=288,"Death")
TNT1 A 0 A_Explode(MBP_TWISTER,TwisterRad,0,0,TwisterRad/2)
TORH GGG 1 //A_JumpIfCloser(40,"Lift2")
TNT1 A 0 A_CheckSightOrRange(TwisterRad,"Loop3")
goto Lift2
Loop3:
TORH H 0 A_JumpIf(z-floorz>=288,"Death")
TNT1 A 0 A_Explode(MBP_TWISTER,TwisterRad,0,0,TwisterRad/2)
TORH HHH 1 //A_JumpIfCloser(40,"Lift3")
TNT1 A 0 A_CheckSightOrRange(TwisterRad,"Loop3")
goto Loop1
Lift1:
TORH F 1 A_SpawnItemEx("TwisterThruster")
TORH FF 1
goto Loop2
Lift2:
TORH G 1 A_SpawnItemEx("TwisterThruster")
TORH GG 1
goto Loop3
Lift3:
TORH H 1 A_SpawnItemEx("TwisterThruster")
TORH HH 1
goto Loop1
Death:
TORH G 1 A_SpawnItemEx("TwisterEnd")
stop
XDeath:
TORH H 0 
TORH H 0 A_SpawnItemEx("TwisterLifter",0,0,0,0,0,momz)
stop
}
}

actor TwisterThruster
{
+MISSILE
+NOINTERACTION
radius 16
height 4
-SOLID
states
{
Spawn:
TNT1 A 0
TNT1 A 2 A_CheckSightOrRange(TwisterRad,"Nope")
TNT1 A 1 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,Twister_Item,TwisterRad+25,RG_TEAM|RG_SELF)
TNT1 A 0
stop
Nope:
TNT1 A 0 
stop
}
}


actor TwisterFXSpawn : BasicHelper
{
	reactiontime 4
	+CLIENTSIDEONLY
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
		TNT1 A 1
		SpawnLoop:
        TNT1 A 0 A_SpawnItemEx("MudBombFx2", TwisterRad, 0, 24, random(5,8), 0, 0, random(0,360), 1)
        TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0 
		stop
	}
}


actor TwisterLifter : TwisterTornado
{
+RIPPER
DAMAGE (0)
States
{
Death:
TORH H 0 
stop
XDeath:
TORH H 0 
stop
}
}

actor TwisterEnd
{
+NOINTERACTION
Scale 4.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TORH HGFHGF  3
stop
}
}