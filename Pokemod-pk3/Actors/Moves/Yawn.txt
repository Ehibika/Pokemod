actor YawnMove : WepMasterBase
{
Tag "Yawn"
obituary "$OB_YAWN"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "YawnPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
+WEAPON.EXPLOSIVE
States
{
Spawn:
WEAP X 1
stop
Fire:
"####" B 0 //A_Jumpif(CallACS("CheckWeaponFire",RC_C)==0,"NoAmmo")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==0,"NoAmmo")
"####" B 0 A_JumpIf(IsPointerEqual(AAPTR_TRACER, AAPTR_NULL) == TRUE, "NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/noisecrush2","Weapon")
"####" BBBBB 2 A_GiveInventory("TargHolder")
"####" B 5 A_FireCustomMissile("YawnCloud",0,1,0,0)
"####" B 0 A_TakeInventory("TargHolder")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("SemiRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor YawnMove1 : YawnMove {Weapon.SlotNumber 1}
actor YawnMove2 : YawnMove {Weapon.SlotNumber 2}
actor YawnMove3 : YawnMove {Weapon.SlotNumber 3}
actor YawnMove4 : YawnMove {Weapon.SlotNumber 4}

actor YawnMove5 : YawnMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("YawnMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor YawnPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor STF_Yawn : Powerup {inventory.amount 1 Powerup.Duration -5}

actor YawnEffectInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("STF_Yawn",1,"Sustain")
TNT1 A 0 A_GiveInventory("STF_Yawn",1)
TNT1 A 0 A_SpawnItemEX("YawnEffectWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_Yawn",1)
stop
Nope:
TNT1 A 0
stop
}
}

Const int Yawn_Item = 1;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor YawnCloud : BasicProjectile
{
+SeekerMissile
+HitTracer
Translation "250:250=202:202", "192:192=69:69"
Speed 15
Damage (0)
DamageType "Yawn"
obituary "$OB_Yawn"
Renderstyle "Translucent"
Alpha 0.9
radius 16
Reactiontime 35
height 16
scale 1.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
Spawn2:
TNT1 A 0 A_SeekerMissile(2,5,SMF_PRECISE)
TNT1 A 0 A_Spawnitemex("YawnCloudTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
YAWN D 1 A_Countdown
loop

Crash:
YAWN E 0 A_JumpIfTracerCloser(100,"YawnEfect")
Goto Death

YawnEfect:
YAWN E 0 A_GiveInventory("YawnEffectInitiate",1,AAPTR_TRACER)
Goto Death

Death:
YAWN EFGHI 3 
PSMS IIII 0 A_Spawnitemex("YawnCloudFX",10,0,random2(3),1,0,random2(3),random(1,360),SXF_TRANSFERTRANSLATION)
YAWN I 1 
stop
}
}

actor YawnCloudTrail : NoiseCrushTrail1
{
ReactionTime 1
Renderstyle "Translucent"
Alpha 0.9
scale 1.5
States
{
Spawn:
YAWN CBA 3
NCRU E 0 A_CountDown
loop
Death:
TNT1 A 0
stop
}
}

actor YawnCloudFX : NoiseCrushTrail1
{
ReactionTime 1
Renderstyle "Translucent"
Alpha 0.9
scale 1.5
States
{
Spawn:
YAWN JJJKL 2
NCRU E 0 A_CountDown
loop
Death:
TNT1 A 0
stop
}
}


actor YawnEffectWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_GiveToTarget("SpdDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_Yawn",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 0 A_GiveToTarget("SleepStatusProc")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}

