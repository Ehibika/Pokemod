actor WishMove : WepMasterBase
{
Tag "Wish"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "WishPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 1
"####" C 0 A_REfire("Fire.Begin")
"####" A 0 A_GiveInventory("DashSet_A")
"####" A 0 A_GiveInventory("WishRCFlag")
"####" A 0 A_Stop
"####" A 0 A_SpawnItemEX("Wish_Remote")
"####" B 0 A_Jump(256,"Fire.Remote")
Goto Fire.Remote


Fire.Remote:
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After")
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_RELOAD)==1,"Fire.RemoteReload")
"####" B 0 A_JumpifInventory("WishRCFlag",1,"Fire.Remote")
Goto Fire.After
Fire.RemoteReload:
TNT1 A 0 A_GiveInventory("PPRecoveryItem") 
Goto Fire.Remote

Fire.After:
"####" B 1 A_GiveInventory("fullRecovery_A")
"####" A 0 A_GiveInventory("DashSetOff")
Fire.After2:
"####" B 1
"####" B 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After2")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor WishMove1 : WishMove {Weapon.SlotNumber 1}
actor WishMove2 : WishMove {Weapon.SlotNumber 2}
actor WishMove3 : WishMove {Weapon.SlotNumber 3}
actor WishMove4 : WishMove {Weapon.SlotNumber 4}

actor WishMove5 : WishMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("WishMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor WishPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor WishRCFlag : Powerup {inventory.amount 1 powerup.duration 35 }

Const int WishRad = 125;
Const int Wish_Forward = 150;
Const int Wish_Range = 1000;

actor WishPPTaker : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_Wish,1)
stop
}
}


//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor Wish_Remote 
{
Speed 5
+MISSILE
+THRUACTORS
+NOCLIP
+FLOORHUGGER
+NOEXPLODEFLOOR
//+SLIDESONWALLS
//+BOUNCEONWALLS 
+NOGRAVITY
Health 5
Damage (0)
DamageType "Wish"
obituary "$OB_Wish"
radius 5
Reactiontime 50
height 16
scale 3.5
Args 0, 60
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 ACS_NamedExecuteAlways("CamGrabObject",0)
TNT1 A 0 ACS_NamedExecuteAlways("RemoteControlActor",0,15.0,Wish_Range)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_DEFAULT)
Spawn2:
TNT1 A 1 A_JumpIfCloser(Wish_Range-1,"Spawn3")
TNT1 A 0 A_SpawnItemEx("WishRingGuide2",Wish_Forward,0,0,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("WishRCFlag",1)
TBLA A 0
loop
Spawn3:
TNT1 A 1 A_SpawnItemEx("WishRingGuide",Wish_Forward,0,0,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("WishRCFlag",1)
TBLA A 0
goto Spawn2

Trigger: //Check PP
TNT1 A 0 A_JumpifInTargetInventory("WishPP",1,"Trigger2")
goto Decline
Trigger2: // Check Recovery Frame
TNT1 A 0 A_JumpifInTargetInventory("Recovery_A",1,"Decline")
goto Detonate

Detonate: // Cast the attack!
TNT1 A 0 A_GiveToTarget("WishPPTaker",1)
TNT1 A 0 A_SpawnItemEx("WishStarDrop",Wish_Forward,0,-(z-floorz)+400,0,0,-8,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("WishAreaWarn",Wish_Forward,0,-(z-floorz)+16,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("SwiftSounder")
TNT1 A 0 A_GiveToTarget("fullRecovery_A",1)
goto PostTrigger

Decline: //Cannot cast the attack
TNT1 A 0 A_GiveToTarget("DeclineSoundCue",1)
goto PostTrigger

PostTrigger:
TNT1 AAAAA 1 A_SpawnItemEx("WishRingGuide",Wish_Forward,0,0,0,0,0,0,1)
Goto Spawn2

Death:
BSTG AAAAAA 0 A_SpawnItemEx("RazorLeafParticle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
SPKH ABCD 2
stop
}
}


actor WishStarDrop : BasicProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS

+RIPPER
+NOINTERACTION
-NOGRAVITY
reactiontime 60
damage (0)
radius 3
height 3
scale 2.0
Gravity 1.5
speed 0
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
//==
AQBA B 0 
AQBA AABBCC 1 A_JumpIf(ceilingz-z>8, "Spawn2")
AQBA B 0 A_JumpIf(momz==0,"Death")
AQBA B 0 A_CountDown
Goto Spawn+2
Spawn2:
TNT1 A 0 A_ChangeFlag("RIPPER", 0)
TNT1 A 0 A_ChangeFlag("NOINTERACTION",0)
TNT1 A 0
STRP AABBCCDD 1 A_SpawnItemEx("WishStarParticle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
Goto Spawn2+2
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("WishStarSplash",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE)
TNT1 A 0 A_SpawnItemEx("WishField",0,0,0,0,0,0,0,1)
stop
}
}

actor WishAreaWarn : BasicExplosion
{
ReactionTime 8
DamageType "Wish"
obituary "$OB_Wish"
//+FORCEPAIN
states
{
Spawn:
TNT1 A 0
TNT1 A 1
Spawn2:
TNT1 AAAA 0 A_SpawnItemEx("WishStarParticle", WishRad, 0, -(z-floorz)+24, 0, 0, random(1,4), Random(0,360), 1061)
TNT1 A 5 A_CountDown
loop 

Death:
TNT1 A 1 
stop
}
}

actor WishField : BasicExplosion
{
ReactionTime 5
DamageType "Wish"
obituary "$OB_Wish"
//+FORCEPAIN
states
{
Spawn:
TNT1 A 0
TNT1 A 1
TNT1 A 0 A_PlaySoundEx("support/LifeDew","SoundSlot7")
TNT1 A 0 ACS_NamedExecuteAlways("MoveAreaHeal",0,CallACS("StatValueRamp2",STAT_SATK,25,550),WishRad,Move_Wish)
Spawn2:
TNT1 AAAA 0 A_SpawnItemEx("AquaRingSparkle", WishRad, 0, -(z-floorz)+24, 0, 0, 0, Random(0,360), 1061)
TNT1 A 5 A_CountDown
loop 

Death:
TNT1 A 1 
stop
}
}


actor WishStarParticle : BasicClientside
{
scale 1.5
States
{
Spawn:
STAP A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
STAP A 2 A_SetScale(Frandom(2.0,2.5))
STAP ABCD 2 A_SetScale(Scalex-0.1)
STAP ABCD 2 A_SetScale(Scalex-0.1)
stop
}
}

actor WishStarSplash : BasicClientside
{
+MISSILE
translation "192:192=218:218", "198:198=221:221"
scale 2.0
Alpha 1.0
States
{
Spawn:
AQBA A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
STAP EEEEEE 0 A_SpawnItemEx("WishStarParticle",0,0,0,random(2,5),0,random(2,8),random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
STAP EEFFGG 2 A_SpawnItemEx("WishStarParticle",0,0,0,random(2,5),0,random(2,8),random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
stop
}
}

actor WishHealFX : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpIf(CallACS("PKMNACS_HealthPercent",0)>=100,"Nope")
LASH A 0 A_JumpIf(ACS_NamedExecuteWithResult("PKMNACS_ActorClassifier",0,2) == TRUE, "HealMe")
stop

HealMe:
TNT1 A 0 A_Giveinventory("HealEffectSpawner",1)
TNT1 A 0
stop

Nope:
TNT1 A 0
stop
}
}

actor WishRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("WishGuideDot", WishRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("WishGuideDot", -WishRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor WishRingGuide2 : BasicHelper
{
	+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("WishGuideDot2", WishRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("WishGuideDot2", -WishRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor WishRingGuideA : BasicHelper
{
+clientsideonly
	reactiontime 6
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		TNT1 A 0 A_SetArg(0,reactiontime)
		TNT1 A 0 A_SetArg(1,WishRad*2)
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("WishGuideDot", WishRad, (-WishRad)+((Args[1]/Args[0])*(Args[0]-reactiontime)), -(z-floorz)+24, 0, 0, 0, 0, 1061)
		TNT1 A 0 A_SpawnItemEx("WishGuideDot", WishRad, (-WishRad)+((Args[1]/Args[0])*(Args[0]-reactiontime)), -(z-floorz)+24, 0, 0, 0, 90, 1061)
		TNT1 A 0 A_SpawnItemEx("WishGuideDot", WishRad, (-WishRad)+((Args[1]/Args[0])*(Args[0]-reactiontime)), -(z-floorz)+24, 0, 0, 0, 180, 1061)
		TNT1 A 0 A_SpawnItemEx("WishGuideDot", WishRad, (-WishRad)+((Args[1]/Args[0])*(Args[0]-reactiontime)), -(z-floorz)+24, 0, 0, 0, 270, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor WishRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 150
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 175,"Terminate")
TNT1 A 0 A_SpawnItemEx("WishRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}


actor WishGuideDot : BasicGuideDot {}

actor WishGuideDot2 : BasicGuideDot {StencilColor "#F42A0C"}