actor WildChargeMove : WepMasterBase
{
Tag "WildCharge"
obituary "$OB_WILDCHARGE"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "WildChargePP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.DashWarmup")
Goto Fire.DashWarmup

Fire.DashWarmup:
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_WildCharge,1)
"####" A 0 A_GiveInventory("WildChargeWarmupTimer")
"####" A 0 A_GiveInventory("DashSet_A")
"####" A 0 A_ChangeFlag("NOGRAVITY",1)
"####" A 0 A_GiveInventory("PKMN_WhiteFlagDropper",1) //For objective combo
"####" A 0 A_Stop
Fire.DashWarmup1:
"####" A 3
"####" A 0 A_GiveInventory("WildChargeFX_Spawn")
"####" A 0 A_PlaySoundEx("weapon/Heat2","Weapon")
"####" A 0 A_Jumpifinventory("WildChargeWarmupTimer",1,"Fire.DashWarmup1")
"####" B 0 A_Jump(256,"Fire.DashStart")
Goto Fire.DashWarmup

Fire.DashStart:
"####" A 0 A_GiveInventory("DashSet_B")
"####" A 0 A_ChangeFlag("NOGRAVITY",0)
"####" A 0 A_GiveInventory("WildChargeTimer")
"####" A 0 A_GiveInventory("WildChargeInitiate")
Fire.Dash:
"####" A 1
"####" A 0 A_SpawnItemEX("GroundDustFX",-25,0,-(z-floorz),0)
"####" A 0 A_Jumpifinventory("DashBreaker",1,"Fire.DashEnd")
"####" A 0 A_Jumpifinventory("WildChargeTimer",1,"Fire.Dash")
goto Fire.DashEnd

Fire.DashEnd:
"####" B 0 A_ScaleVelocity(0.2)
"####" B 0 A_GiveInventory("DashSetOff")
"####" A 0 A_GiveInventory("DashSetOff")
"####" B 1 A_GiveInventory("HalfRecovery_B")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor WildChargeMove1 : WildChargeMove {Weapon.SlotNumber 1}
actor WildChargeMove2 : WildChargeMove {Weapon.SlotNumber 2}
actor WildChargeMove3 : WildChargeMove {Weapon.SlotNumber 3}
actor WildChargeMove4 : WildChargeMove {Weapon.SlotNumber 4}

actor WildChargeMove5 : WildChargeMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("WildChargeMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor WildChargePP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor WildChargeWarmupTimer : Powerup {inventory.amount 1 powerup.duration 10 }
actor WildChargeTimer : Powerup {inventory.amount 1 powerup.duration 35 }


Actor WildChargeDelay : powerprotection
{
Powerup.duration 5
Damagefactor "WildCharge", 0
}

const int WLDCHR_Thrust = 30;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor WildChargeBumper : BasicProjectile
{
Speed 30 
Damage (0)
radius 10
height 20
DamageType "WildCharge"
Obituary "$OB_WILDCHARGE"
scale 1.5
//+NOINTERACTION
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Terminate")
TNT1 A 1 
TNT1 A 1 A_Explode(MBP_WildCharge,80,0,0,80)
FWVE W 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
Death:
TNT1 A 0 A_Explode(MBP_WildCharge,80,0,0,80)
TNT1 A 1 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_GiveToTarget("WildChargeCollision2",1)
TNT1 A 0 A_SpawnItemEx("BasicMeleeFX", 0, 0, 0, 0, 0, 0, 0,1)
Crash:
TNT1 A 0 A_Explode(MBP_WildCharge,80,0,0,80)
Terminate:
TNT1 A 1
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

actor WildChargeWave
{
PROJECTILE
+FLOORCLIP
+THRUGHOST
+MTHRUSPECIES
+NOEXPLODEFLOOR
Radius 5
Height 5
scale 2.0
damage (0)
reactiontime 3
DamageType "WildCharge"
obituary "$OB_WildCharge"
speed 0
States
{
Spawn:
BEWA B 0

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(ScaleX+0.3)
BEWA B 0 A_CountDown
loop
Death:
BEWA A 0
stop
}
}


actor WildChargeCollision : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*-35,0,sin(pitch)*35,CVF_RELATIVE)
TNT1 A 0 ThrustThingZ(0,30,1,1)
stop
Nope:
TNT1 A 0
stop
}
}


actor WildChargeCollision2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_PlaySoundEx("weapon/Rebound","SoundSlot5")
TNT1 A 0 ACS_NamedExecuteAlways("PKMN_CollisionDamager",0,MBP_WildCharge)
//TNT1 A 0 DamageThing(MBP_WildCharge*(CallACS("StatValueRamp",STAT_PATK,1,400)*0.01))
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
stop
Nope:
TNT1 A 0
stop
}
}


actor WildChargeThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_PlaySoundEx("weapon/Trample","SoundSlot5")
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*WLDCHR_Thrust,0,sin(-pitch)*WLDCHR_Thrust,CVF_RELATIVE|CVF_REPLACE)//cos(-pitch)* , sin(-pitch)*WLDCHR_Thrust
TNT1 A 1 A_FireCustomMissile("WildChargeBumper",0,0,0,15,0,0)
BASB A 1 A_SpawnitemEx("WildChargeWave",cos(-pitch)*30,0,(25),0,0,0,0,SXF_TRANSFERTRANSLATION) //+(sin(pitch)*30)
BASB AAA 0 A_SpawnItemEx("MoveThunderBoltFX", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359))
BASB AAA 0 A_SpawnItemEx("DragonBreathParticle2", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359))
stop
Nope:
TNT1 A 0
stop
}
}

actor WildChargeFX_Spawn : CustomInventory
{
states
{
Pickup:
BASB AAA 0 A_SpawnItemEx("MoveThunderBoltFX", random2(32), random2(32), random2(32), 0, 0, 0, random(0, 359))
BASB AAA 0 A_SpawnItemEx("DragonBreathParticle2", random2(32), random2(32), random2(32), 0, 0, 0, random(0, 359))
stop
Nope:
TNT1 A 0
stop
}
}

actor WildChargeInitiate : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Spawnitemex("WildChargeWatcher")
stop
}
}

actor WildChargeWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Death")
TNT1 A 0 A_SetUserVar("user_DashTime",user_DashTime+1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 3 A_GiveToTarget("WildChargeThrust")
TNT1 A 0 A_JumpIfInTargetInventory("WildChargeTimer",1,"Spawn")
//TNT1 A 0 A_Jumpif(user_DashTime <= 6,"Spawn")
Goto Death
Check2:
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Spawn")
Goto Death
Death:
DeathTake:
TNT1 A 0 //A_TakeFromTarget("WildChargeActive",1)
stop
}
}