actor MagicalLeafMove : WepMasterBase
{
Tag "MagicalLeaf"
obituary "$OB_MagicalLeaf"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "MagicalLeafPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("weapon/tengushot","Weapon")
"####" B 0 A_FireCustomMissile("MagicalLeafShot",8,1,0,0)
"####" B 0 A_FireCustomMissile("MagicalLeafShot",-8,0,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("SemiRecovery_B")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor MagicalLeafMove1 : MagicalLeafMove {Weapon.SlotNumber 1}
actor MagicalLeafMove2 : MagicalLeafMove {Weapon.SlotNumber 2}
actor MagicalLeafMove3 : MagicalLeafMove {Weapon.SlotNumber 3}
actor MagicalLeafMove4 : MagicalLeafMove {Weapon.SlotNumber 4}

actor MagicalLeafMove5 : MagicalLeafMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("MagicalLeafMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor MagicalLeafPP : Ammo {inventory.amount 1 inventory.Maxamount 20}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor MagicalLeafShot : BasicProjectile
{
Speed 55
Damage (MBP_MagicalLeaf)
DamageType "MagicalLeaf"
obituary "$OB_MagicalLeaf"
+SEEKERMISSILE
+SCREENSEEKER
+FLOAT
radius 5
Reactiontime 8
height 16
scale 3.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("MagicalLeafFX",0,0,0,0,0,0,0,1)

Spawn1: // -=- Pre Seeking -=-
MLEF B 2 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF A 0 A_Countdown
MLEF C 2 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF A 0 A_Countdown
MLEF D 2 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF A 0 A_Countdown


Spawn2: // -=- Seeking Target -=-
MLEF B 1 A_SeekerMissile (2, 10, SMF_LOOK|SMF_PRECISE, 256, 3)
MLEF B 1 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF A 0 A_Countdown
MLEF C 1 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF C 1 A_SeekerMissile (2, 10, SMF_LOOK|SMF_PRECISE, 256, 3)
MLEF A 0 A_Countdown
MLEF D 1 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF D 1 A_SeekerMissile (2, 10, SMF_LOOK|SMF_PRECISE, 256, 3)
MLEF A 0 A_Countdown

TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TRACER, AAPTR_NULL) == TRUE, "Spawn2")
Goto Spawn3

Spawn3: // -=- ExamineTarget -=-
TNT1 A 0 A_JumpifInventory("SafeguardPower",1,"TargClear",AAPTR_TRACER)
TNT1 A 0 A_JumpifInventory("CraftyShieldPower",1,"TargClear",AAPTR_TRACER)
Goto Spawn4

TargClear:
MLEF A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_NULL)
Goto Spawn4

Spawn4: // -=- target acquired -=-
MLEF BB 1 A_SeekerMissile (2, 10, SMF_PRECISE)
MLEF B 0 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF CC 1 A_SeekerMissile (2, 10, SMF_PRECISE)
MLEF C 0 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF DD 1 A_SeekerMissile (2, 10, SMF_PRECISE)
MLEF D 0 A_SpawnItemEx("MagicalLeafSparkle",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,1)
MLEF A 0 A_Countdown
loop


Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
BSTG AAAAAA 0 A_SpawnItemEx("MagicalLeafParticle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
BSTG AA 0 A_SpawnItemEx("MagicalLeafSparkle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
SPKH ABCD 2
stop
}
}


actor MagicalLeafFX : BasicHelper
{
reactiontime 6
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,28,0,WARPF_COPYINTERPOLATION)
TNT1 A 1 A_SpawnItemEx("MagicalLeafParticle",random(-40,40),random(-40,40),random(10,40),0,0,0,0,1)
TNT1 A 0 A_CountDown
loop
}
}

actor MagicalLeafParticle : BasicClientside
{
-NOINTERACTION
-NOGRAVITY
scale 2
Gravity 0.5
States
{
Spawn:
WOOB UVWXYZ 2 A_ScaleVelocity (0.6)
WOOB ZYXWVU 2 A_ScaleVelocity (0.8)
WOOB U 2 A_ScaleVelocity (0.8)
TNT1 A 2 A_ScaleVelocity (0.8)
WOOB W 2 A_ScaleVelocity (0.8)
TNT1 A 2 A_ScaleVelocity (0.8)
WOOB Y 2 A_ScaleVelocity (0.8)
TNT1 A 2 A_ScaleVelocity (0.8)
WOOB Y 2 A_ScaleVelocity (0.8)
TNT1 A 2 A_ScaleVelocity (0.8)
WOOB W 2 A_ScaleVelocity (0.8)
TNT1 A 2 A_ScaleVelocity (0.8)
stop
}
}

actor MagicalLeafSparkle : BasicClientside
{
scale 1.2
ReactionTime 2
Renderstyle "ADD"
Alpha 0.9
States
{
Spawn:
TNT1 A 0
BMBU D 0 Thrustthingz(0,random(2,6),0,0)
Spawn2:
FX01 ABCDEFGHI 2 
stop
}
}