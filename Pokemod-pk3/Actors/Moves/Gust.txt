actor GustMove : WepMasterBase
{
Tag "Gust"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "GustPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("GustRingSpawner",0,0,0,Aim_OverHead)
//"####" B 0 A_FireCustomMissile("GustGraphicSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_JumpIfInventory("IsBot",1,"Fire.Bullet")
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/Usemove","Weapon")
"####" B 0 A_FireCustomMissile("GustSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("FullRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor GustMove1 : GustMove {Weapon.SlotNumber 1}
actor GustMove2 : GustMove {Weapon.SlotNumber 2}
actor GustMove3 : GustMove {Weapon.SlotNumber 3}
actor GustMove4 : GustMove {Weapon.SlotNumber 4}

actor GustMove5 : GustMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("GustMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor GustPP : Ammo {inventory.amount 1 inventory.Maxamount 10}


Const int GustRad = 100;
Const int Gust_Item = 27;

Actor GustProtect : powerprotection
{
Powerup.duration 5
Damagefactor "Gust", 0
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor GustRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("GustGuideDot", GustRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("GustGuideDot", -GustRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor GustRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 450
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_SpawnItemEx("GustRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}


actor GustGuideDot : BasicGuideDot {}

actor GustSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 450
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("GustBurst",0,0,-(z-floorz),0,0,0,0,1)
TNT1 A 1
stop

Terminate:
TNT1 A 0 
stop
}
}


actor GustBurst : BasicExplosion
{
translation "202:202=210:210"//90
Damagetype "Gust"
Obituary "$OB_Gust"
scale 2.5
Var int user_angle;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_PlaySoundEx("weapon/AirShooter", "Weapon")
TNT1 A 0 A_SpawnItemEx("GustPart",0,0,0,0,0,32,0,1)
TNT1 A 1 A_Explode(MBP_Gust,GustRad,0,0,GustRad/2)
TNT1 A 6 
stop
}
}


actor GustPart : BasicFastProjectile
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    RenderStyle None
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > 35,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("GustPart",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("GustTornado",0,0,0,0,0,0,0,1)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 
        stop
    Death2:
        TNT1 A 0 A_Stop
        stop
    }
}


actor GustTornado
{
PROJECTILE
damage(0) 
+DONTREFLECT
ReactionTime 8
damagetype "Gust"
Obituary "$OB_GUST"
height 6
radius 16
Scale 5.0
Speed 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TORH F 1 A_SpawnItemEx("GustThruster")
Loop1:
TORH F 0 A_CountDown
TNT1 A 0 A_Explode(MBP_Gust,GustRad,0,0,GustRad/2)
TORH F 1 
Loop2:
TORH F 0 A_CountDown
TNT1 A 0 A_Explode(MBP_Gust,GustRad,0,0,GustRad/2)
TORH G 1 
Loop3:
TORH F 0 A_CountDown
TNT1 A 0 A_Explode(MBP_Gust,GustRad,0,0,GustRad/2)
TORH H 1
goto Loop1
Death:
TORH G 1 A_SpawnItemEx("GustEnd")
stop
XDeath:
TORH H 0 
stop
}
}

actor GustThruster
{
+MISSILE
+NOINTERACTION
radius 16
height 4
-SOLID
states
{
Spawn:
TNT1 A 0
TNT1 A 2 A_CheckSightOrRange(GustRad,"Nope")
TNT1 A 1 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,Gust_Item,GustRad+25,RG_TEAM|RG_SELF|RG_ENEMY)
TNT1 A 0
stop
Nope:
TNT1 A 0 
stop
}
}

actor GustEnd
{
+NOINTERACTION
Scale 5.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TORH HGFHGF 1 A_Fadeout
stop
}
}