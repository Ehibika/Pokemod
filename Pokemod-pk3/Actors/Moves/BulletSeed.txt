const Int BSSPRD = 2.6;

actor BulletSeedMove : WepMasterBase
{
Tag "BulletSeed"
obituary "$OB_BulletSeed"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "BulletSeedPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("Weapon/CrashLand","Soundslot6")
TNT1 A 0 A_Jump(256,"Fire.Charge0")
Goto Fire.Charge0

Fire.Charge0: //Initiation
TNT1 A 0 A_JumpIfInventory("WeaponCharge",5,"Fire.Charge1")
TNT1 A 0 A_GiveInventory("WeaponCharge",1)
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
TNT1 A 1
TNT1 A 0 A_REfire("Fire.Charge0")
TNT1 A 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Charge1: //Preparing a stack
TNT1 A 0 A_Jumpif(CallACS("CheckWeaponFire",RC_A)!=1,"Fire.Charge3")
TNT1 A 0 A_JumpIfInventory("MultiShotCounter",5,"Fire.Charge3")
TNT1 A 0 A_JumpIfInventory("WeaponCharge",5,"Fire.Charge2")
TNT1 A 0 A_GiveInventory("WeaponCharge",1)
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
TNT1 A 0 //A_PlaySoundEx("Weapon/Heat1","Soundslot6")
TNT1 A 1
TNT1 A 0 A_REfire("Fire.Charge1")
TNT1 A 0 A_Jump(256,"Fire.Launch")
Goto Fire.Launch

Fire.Charge2: //Make a stack
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
TNT1 A 0 A_GiveInventory("Give_MultiShotCounter",1)
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_BulletSeed,1)
TNT1 A 0 A_TakeInventory("WeaponCharge",99)
TNT1 A 0 A_PlaySoundEx("Weapon/rminestick","Soundslot6")

TNT1 A 1
TNT1 A 0 A_JumpifNoAmmo("Fire.Charge3")
TNT1 A 0 A_REfire("Fire.Charge1")
TNT1 A 0 A_Jump(256,"Fire.Launch")
Goto Fire.Launch

Fire.Charge3: //Maximum stacks
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
TNT1 A 1
TNT1 A 0 A_REfire("Fire.Charge3")
TNT1 A 0 A_Jump(256,"Fire.Launch")
Goto Fire.Launch

Fire.Launch:
TNT1 A 0
TNT1 A 0 A_GiveInventory("MultiShotTimer")
TNT1 A 0 A_Jumpifinventory("MultiShotCounter",1,"Fire.Bullet")
TNT1 A 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Bullet:
TNT1 A 0 A_PlaySoundEx("move/BulletSeed","Weapon")
TNT1 A 2 A_FireCustomMissile("BulletSeedShot",BSSPRD,0,0,0,0,0)
TNT1 A 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After2")
TNT1 A 0 A_PlaySoundEx("move/BulletSeed","Weapon")
TNT1 A 2 A_FireCustomMissile("BulletSeedShot",0,0,0,0,0,0)
TNT1 A 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After2")
TNT1 A 0 A_PlaySoundEx("move/BulletSeed","Weapon")
TNT1 A 2 A_FireCustomMissile("BulletSeedShot",-BSSPRD,0,0,0,0,0)
TNT1 A 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After2")
TNT1 A 0 A_GiveInventory("ChannelingSpeed")
TNT1 A 0 A_REfire("Fire.Charge1")
TNT1 A 0 A_Jumpifinventory("MultiShotTimer",1,"Fire.Bullet")
TNT1 A 0 A_TakeInventory("MultiShotCounter",1)
TNT1 A 0 A_Jump(256,"Fire.Launch")
Goto Fire.Launch

Fire.After2:
TNT1 A 5 A_PlaySoundEx("Weapon/CrashLand","Soundslot6")
Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_A")
"####" B 0 A_TakeInventory("MultiShotTimer",99)
"####" B 0 A_TakeInventory("MultiShotCounter",99)
"####" B 0 A_TakeInventory("WeaponCharge",99)
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor BulletSeedMove1 : BulletSeedMove {Weapon.SlotNumber 1}
actor BulletSeedMove2 : BulletSeedMove {Weapon.SlotNumber 2}
actor BulletSeedMove3 : BulletSeedMove {Weapon.SlotNumber 3}
actor BulletSeedMove4 : BulletSeedMove {Weapon.SlotNumber 4}

actor BulletSeedMove5 : BulletSeedMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("BulletSeedMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor BulletSeedPP : Ammo {inventory.amount 1 inventory.Maxamount 20}

actor BulletSeedCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 10
powerup.type "ConShotTimer"
}

actor Give_MultiShotCounter : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("MultiShotCounter",1,"Pickup2")
TNT1 A 0 A_GiveInventory("MultiShotCounter")
TNT1 A 0 A_SpawnItemEx("MultiShotCountWarper")
stop

Pickup2:
TNT1 A 0
TNT1 A 1 A_GiveInventory("MultiShotCounter")
stop

Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor PKMN_MultiHitShot : BasicProjectile
{
Speed 64
Reactiontime 25
radius 4
height 4
Scale 2.5
}

actor BulletSeedShot : PKMN_MultiHitShot
{
Damage (MBP_BulletSeed)
DamageType "BulletSeed"
obituary "$OB_BulletSeed"
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BASB T 1 A_Spawnitemex("BulletSeedTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Countdown
loop

Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A  0 A_PlaySoundEx("weapon/OrbBounce","Weapon")
BSTG A 1 A_Spawnitemex("BulletSeedPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor BulletSeedTrail : BasicClientside
{
scale 2
Renderstyle "Translucent"
Alpha 0.6
States
{
Spawn:
TNT1 A 0 
BASB U 2 
BASB UUU 2 A_Fadeout(0.2)
stop
}
}

actor BulletSeedPuff1 : BasicClientside
{
scale 2
States
{
Spawn:
TNT1 A 0 
NDFX A 1
NDFX AAAA 1 A_SetScale(ScaleX-0.5)
stop
}
}

actor MultiShotCountWarper : BasicWarper
{
+FORCEXYBILLBOARD
scale 1.0
states
{
Spawn:
TNT1 A 0
TNT1 A 3
TNT1 A 0 ACS_NamedExecuteAlways("MultishotCountWarper",0)
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Goto Count0
Count0:
TNT1 A 0
MHCT A 1 A_Warp(AAPTR_TARGET,-1,-10,0,0,72)
loop

Count1:
TNT1 A 0
MHCT B 1 A_Warp(AAPTR_TARGET,-1,-10,0,0,72)
loop

Count2:
TNT1 A 0
MHCT C 1 A_Warp(AAPTR_TARGET,-1,-10,0,0,72)
loop

Count3:
TNT1 A 0
MHCT D 1 A_Warp(AAPTR_TARGET,-1,-10,0,0,72)
loop

Count4:
TNT1 A 0
MHCT E 1 A_Warp(AAPTR_TARGET,-1,-10,0,0,72)
loop

Count5:
TNT1 A 0
MHCT F 1 A_Warp(AAPTR_TARGET,-1,-10,0,0,72)
loop

Death:
TNT1 A 0
stop
}
}