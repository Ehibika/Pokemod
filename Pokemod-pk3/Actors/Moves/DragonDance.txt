actor DragonDanceMove : WepMasterBase
{
Tag "DragonDance"
obituary "$OB_DRAGONDANCE"
Weapon.AmmoUse 1
Weapon.AmmoGive 25
weapon.ammotype "DragonDancePP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_JumpIfInventory("STF_DragonDance",1,"Fire.Off")
"####" B 0 A_Jump(256,"Fire.On")
Goto Fire.On

Fire.On:
"####" B 0 A_PlaySoundEx("Move/DragonDance","Soundslot6")
"####" BBBBBB 3 A_GiveInventory("DragonDanceParticleSpawn")
"####" B 0 A_PlaySoundEx("Support/Statup","Soundslot7")
"####" B 0 A_GiveInventory("DragonDanceBuffInitiate")
"####" B 0 A_TakeInventory("DragonDancePP",1)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Off:
"####" B 0 A_TakeInventory("DragonDance_Active",99)
"####" B 0 A_GiveInventory("PartRecovery_C")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor DragonDanceMove1 : DragonDanceMove {Weapon.SlotNumber 1}
actor DragonDanceMove2 : DragonDanceMove {Weapon.SlotNumber 2}
actor DragonDanceMove3 : DragonDanceMove {Weapon.SlotNumber 3}
actor DragonDanceMove4 : DragonDanceMove {Weapon.SlotNumber 4}

actor DragonDanceMove5 : DragonDanceMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("DragonDanceMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor DragonDancePP : Ammo {inventory.amount 1 inventory.Maxamount 25}
actor STF_DragonDance : Powerup {inventory.amount 1 Powerup.Duration 40}
actor DragonDance_Active : Inventory {inventory.amount 1 inventory.Maxamount 1}

actor DragonDanceBuffInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("DragonDance_Active",1,"Sustain")
TNT1 A 0 A_GiveInventory("DragonDance_Active",1)
TNT1 A 0 A_SpawnitemEX("DragonDanceBuffWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_DragonDance",1)
stop
Nope:
TNT1 A 0
stop
}
}


actor DragonDanceBuffWatcher : BasicHelper 
{
Var int user_TickTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 0 A_JumpIfInTargetInventory("HazeFlag",1,"Death")
TNT1 A 5 A_GiveToTarget("STF_DragonDance")
TNT1 A 0 A_GiveToTarget("PAtkUpFXSpawn")
TNT1 A 0 A_GiveToTarget("SpdUpFXSpawn")
TNT1 A 0 A_SetUserVar("user_TickTime",user_TickTime+5)
TNT1 A 0 A_JumpIf(user_TickTime>=35,"SpawnTick")
TNT1 A 0 A_JumpIfInTargetInventory("DragonDance_Active",1,"Spawn")
Goto Death

SpawnTick:
TNT1 A 0 A_SetUserVar("user_TickTime",0)
TNT1 A 0 A_TakeFromTarget("DragonDancePP",1)
TNT1 A 0 A_JumpIfInTargetInventory("DragonDancePP",1,"Spawn")
Goto Death

Death:
TNT1 A 0 A_GiveToTarget("StatUpdator")
TNT1 A 0 A_GiveToTarget("BuffLossSoundCue")
TNT1 A 0 A_TakeFromTarget("STF_DragonDance",9)
TNT1 A 0 A_TakeFromTarget("DragonDance_Active",99)
TNT1 A 2 
stop
}
}


actor DragonDanceParticle : BasicClientside
{
scale 1.2
Reactiontime 35
speed 7
var int user_speed;
var int user_maxturn;
var int user_turndivisor;
var int user_turndivisor_add;
States
{
    Spawn:
    POWS A 0
    TNT1 A 0 A_SetUserVar("user_speed", 30)
    TNT1 A 0 A_SetUserVar("user_maxturn", 30)
    TNT1 A 0 A_SetUserVar("user_turndivisor", 50)
    TNT1 A 0 A_SetUserVar("user_turndivisor_add", 12)
    Spawn2:
    TNT1 A 0 A_Setangle(angle-user_maxturn)
    HARD B 1 A_ChangeVelocity(user_speed,0,5,CVF_RELATIVE|CVF_REPLACE)
    TNT1 A 0 A_CountDown
    loop
}
}

actor DragonDanceParticleSpawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 AA 0 A_SpawnItemEx ("DragonDanceParticle", 0, -60, 0, 0, 0, 5, random(0,360),SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
stop
Nope:
TNT1 A 0
stop
}
}