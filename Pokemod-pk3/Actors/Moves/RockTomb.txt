Const int ROCKTOMB_AREA1 = 128;
Const int ROCKTOMB_AREA2 = 64;

actor RockTombMove : WepMasterBase
{
Tag "RockTomb"
Weapon.AmmoUse 1
Weapon.AmmoGive 15
weapon.ammotype "RockTombPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("RockTombAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("weapon/GrenadeLauncher","Weapon")
"####" B 0 A_FireCustomMissile("RockTombShot",0,1,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor RockTombMove1 : RockTombMove {Weapon.SlotNumber 1}
actor RockTombMove2 : RockTombMove {Weapon.SlotNumber 2}
actor RockTombMove3 : RockTombMove {Weapon.SlotNumber 3}
actor RockTombMove4 : RockTombMove {Weapon.SlotNumber 4}

actor RockTombMove5 : RockTombMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("RockTombMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor RockTombPP : Ammo {inventory.amount 1 inventory.Maxamount 15}


actor STF_RockTomb : Powerup {inventory.amount 1 Powerup.Duration -6}

actor RockTombNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",0,STATUS_STATS),"Nope") //Blocked by Protect|Detect|Substitute|Mist
TNT1 A 0 A_Jumpifinventory("STF_RockTomb",1,"Sustain")
TNT1 A 0 A_GiveInventory("STF_RockTomb",1)
TNT1 A 0 A_SpawnItemEX("RockTombNerfWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_RockTomb",1)
stop
Nope:
TNT1 A 0
stop
}
}


Const int RockTombEntiTID = 9;

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor RockTombShot : BasicProjectile
{
DamageType "RockTomb"
obituary "$OB_RockTomb"
+BOUNCEONCEILINGS
-NoGravity
Gravity 1.5
damage (MBP_RockTomb1)
Radius 8
Height 8
speed 35
Scale 3.0
ReactionTime 4
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0
BROK A 1 A_SpawnItemEx("RockTombShotTrail",0,0,8,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
loop
Crash:
XDeath:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
AQBA C 0 A_Stop
TNT1 A 0 A_Explode(MBP_RockTomb2,ROCKTOMB_AREA2,0,0,ROCKTOMB_AREA2)
TNT1 A 5 A_SpawnItemEx("RockTombImpactFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
stop

Deploy.Death:
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
AQBA C 0 A_Stop
TNT1 A 1 A_CheckFloor("Death2")
TNT1 A 0 A_Explode(MBP_RockTomb2,ROCKTOMB_AREA2,0,0,ROCKTOMB_AREA2)
TNT1 A 5 A_SpawnItemEx("RockTombImpactFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
stop

Death2:
TNT1 A 0 A_SpawnItemEx("RockTombBlast",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 5 A_GiveInventory("SpawnRTRockItem",1)
stop

}
}


actor RockTombAimPath : GolfBallAimInstantStarter2 { Speed 35 Gravity 1.5}


actor RockTombBlast : BasicExplosion
{
DamageType "RockTomb"
obituary "$OB_RockTomb"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_Explode(MBP_RockTomb2,ROCKTOMB_AREA1,0,0,ROCKTOMB_AREA2)
TNT1 A 0 A_PlaySoundEx("misc/FATLAND","Weapon")
TNT1 A 0 A_Quake(8,17,0,400)
TNT1 A 0 A_SpawnItemEx("RockTombPoundFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 17
stop
}
}



actor RockTombImpactFX : BasicClientside
{
scale 3.5
States
{
Spawn:
TNT1 A 0 
BSTG AAA 0 A_SpawnItemEx("RockTombParticle", 0, 0, 0, random2(256/24), random2(256/24), random(0,5), random(0, 359),1)
BSTG AAA 0 A_SpawnItemEx("RockBlastParticle", 0, 0, 0, random2(256/24), random2(256/24), random(0,5), random(0, 359),1)
TNT1 A 0 A_PlaySoundEx("weapon/deepdiggerbreak", "Voice")
HITG FGH 1
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
TNT1 A 6
stop
}
}

actor RockTombParticle : BasicClientside
{
scale 1.5
-NOINTERACTION
-NOGRAVITY
RenderStyle "Translucent"
Alpha 0.6
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SetScale(Frandom(1.5,3.5))
STRK A 7 
STRK AZAZAZA 2
stop
}
}

actor RockTombShotTrail : BasicClientside
{
translation "192:192=218:218", "198:198=221:221"
scale 2.5
Alpha 0.6
States
{
Spawn:
AQBA A 0
BASB UUUU 2 A_Fadeout(0.1)
stop
}
}

actor RockTombPoundFX : BasicHelper
{
+CLIENTSIDEONLY
reactiontime 20
var int user_angle;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 //A_GiveInventory("ProjectileCodeSyncTarget",1)
//TNT1 A 1
SpawnLoop:
TNT1 A 0 A_SpawnItemEx("RockTomb_WaveFX",ROCKTOMB_AREA1,0,-(z-floorz),7,0,0,user_angle,1061)
TNT1 A 0 A_SpawnItemEx("RockTomb_WaveFX",ROCKTOMB_AREA1,0,-(z-floorz),5,0,0,user_angle+16,1061)
TNT1 A 0 A_SetUserVar("user_angle", user_angle + 36)
TNT1 A 0 A_Countdown
loop
Death:
TNT1 A 0
stop
}
}


actor RockTomb_WaveFX : BasicClientside
{
Scale 2.5
States
{
Spawn:
TNT1 A 0
//TNT1 A 0 A_GiveInventory("ProjectileCode",1)
EQWV BCD 2
stop
}
}


Actor RockTombRock : BasicDeployable 
{
-SHOOTABLE
-SOLID
+DONTRIP
+SLIDESONWALLS
+MISSILE
//+THRUSPECIES
//+MTHRUSPECIES
+NOTARGETSWITCH
PainChance "Decay", 0
PainChance "RockTomb", 0
Mass 0
Health 150
Scale 3.0
Radius 25
Height 55
States
{
Spawn:
BROK A 0
BROK A 0 A_GiveInventory("ProjectileCode",1)
BROK A 0 
Spawn.Setup1:
BROK A 1 A_CheckFloor("Spawn.Setup2")
Wait
Spawn.Setup2:
BROK A 0 ACS_NamedExecuteAlways("DeployableDefenseSet",0,150)
BROK A 0 ACS_NamedExecuteAlways("SetObjectType",0,Type_Rock,Type_Null)
SEDG D 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,RockTombEntiTID,3)
BROK A 1 A_CheckRange(70,"Spawn.Setup3")
Wait
Spawn.Setup3:
BROK A 3 
BROK A 0 A_ChangeFlag("SHOOTABLE",1)
BROK A 0 A_ChangeFlag("SOLID",1)
goto Spawn.Basic

Spawn.Basic:
BROK A 0 A_JumpIfHealthLower(2,"Death")
BROK A 7 ACS_NamedExecuteAlways("ActorDecay",0,7)
loop

Deploy.Pain:
TNT1 A 0 A_PlaySoundEX("Weapon/ObjectHit","Voice")
BROK A 3 A_SpawnItemEx("PainFX",-5,0,3,0,0,0)
goto Spawn.Basic

Death:
BROK A 5
BROK A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_PlaySoundEx("weapon/deepdiggerbreak","Weapon")
TNT1 A 0 A_SpawnItemEx("RockTombImpactFX",0,0,0,0,0,0,0,1)
TNT1 A 3
stop
}
}

Actor RockTombRock_B : RockTombRock{/*Designatedteam 0 /*Species "LightMember"*/ }
Actor RockTombRock_R : RockTombRock{/*Designatedteam 1 /*Species "WilyMember"*/ }
Actor RockTombRock_G : RockTombRock{/*Designatedteam 2 /*Species "CossackMember"*/ }
Actor RockTombRock_O : RockTombRock{/*Designatedteam 3 /*Species "KingMember"*/ }

actor SpawnRTRockItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"BFire")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"RFire")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"GFire")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"YFire")

TNT1 A 0 A_SpawnItemEx("RockTombRock",0,0,-(z-floorz)+1,0,0,0,0,0)//SXF_TRANSFERPOINTERS
stop
BFire:
TNT1 A 0 A_SpawnItemEx("RockTombRock_B",0,0,-(z-floorz)+1,0,0,0,0,0)
stop
RFire:
TNT1 A 0 A_SpawnItemEx("RockTombRock_R",0,0,-(z-floorz)+1,0,0,0,0,0)
stop
GFire:
TNT1 A 0 A_SpawnItemEx("RockTombRock_G",0,0,-(z-floorz)+1,0,0,0,0,0)
stop
YFire:
TNT1 A 0 A_SpawnItemEx("RockTombRock_O",0,0,-(z-floorz)+1,0,0,0,0,0)
stop
}
}


actor RockTombNerfWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_GiveToTarget("SpdDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_RockTomb",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}

