actor MuddyWaterMove : WepMasterBase
{
Tag "MuddyWater"
obituary "$OB_MUDDYWATER"
Weapon.AmmoUse 1
Weapon.AmmoGive 25
weapon.ammotype "MuddyWaterPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 //A_Jumpif(CallACS("CheckWeaponFire",RC_A)==0,"NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/watershieldburst","Weapon")
"####" B 1 A_FireCustomMissile("MuddyWaterWaveSpawner",0,1,0,0)
"####" B 1 A_FireCustomMissile("MuddyWaterWaveSpawner",0,0,30,0)
"####" B 0 A_FireCustomMissile("MuddyWaterWaveSpawner",0,0,60,0)
"####" B 1 A_FireCustomMissile("MuddyWaterWaveSpawner",0,0,-30,0)
"####" B 0 A_FireCustomMissile("MuddyWaterWaveSpawner",0,0,-60,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End

}
}

actor MuddyWaterMove1 : MuddyWaterMove {Weapon.SlotNumber 1}
actor MuddyWaterMove2 : MuddyWaterMove {Weapon.SlotNumber 2}
actor MuddyWaterMove3 : MuddyWaterMove {Weapon.SlotNumber 3}
actor MuddyWaterMove4 : MuddyWaterMove {Weapon.SlotNumber 4}

actor MuddyWaterMove5 : MuddyWaterMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("MuddyWaterMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor MuddyWaterPP : Ammo {inventory.amount 1 inventory.Maxamount 25}


actor STF_MuddyWater : BaseSTatusFlag {}

actor MuddyWaterNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",0,STATUS_STATS),"Nope") //Blocked by Protect|Detect|Substitute|Mist
TNT1 A 0 A_Jumpifinventory("STF_MuddyWater",1,"Sustain")
TNT1 A 0 A_PlaySoundEx("support/StatDown","Weapon")
TNT1 A 0 A_GiveInventory("STF_MuddyWater",1)
TNT1 A 0 A_SpawnItemEX("MuddyWaterNerfWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_MuddyWater",1)
stop
Nope:
TNT1 A 0
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]



actor MuddyWaterWaveSpawner : WaterWaveSpawner
{
States
{
Spawn:
TNT1 A 0
TNT1 A 1
TNT1 A 1 A_JumpIf(momz*momz<1,"Death")
Spawn2:
MWAT ABCDCB 4
TNT1 A 0 A_JumpIf(momz*momz<1,"Death")
loop
Death:
TNT1 A 0 A_CustomMissile("MuddyWaterWave1",0,0,0,2,0)
TNT1 A 0 A_CustomMissile("MuddyWaterWave2",0,0,0,2,0)
stop
}
}

actor MuddyWaterWave : WaterWave
{
DamageType "MuddyWater"
obituary "$OB_MuddyWater"
Damage (MBP_MuddyWater)
+MTHRUSPECIES
+STEPMISSILE
//+SPECTRAL 
maxstepheight 16
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_GiveInventory("MuddyWaterProximityCheck",1)
MWAT A 0 A_GiveInventory("Clip",1)
Spawn2:
MWAT AAAABBBBCCCCDDDDCCCCBBBB 1 A_GiveInventory("MuddyWaterFunc",1)
loop

Crash:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Spawnitemex("MuddyWaterPuff",0,0,22,0,0,0,0,1)
stop
}
}

actor MuddyWaterFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("MuddyWaterProximityCheck",1)
TNT1 A 0 A_GiveInventory("MuddyWaterFallCheck",1)
stop
}
}

actor MuddyWaterProximityCheck : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfCloser(600,"PickupNo")
TNT1 A 0 Thing_Remove(0)
stop
PickupNo:
TNT1 A 0
stop
}
}

//wait, this doesn't do anything...
actor MuddyWaterFallCheck : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIf(z-floorz>8, "Fall")
stop
Fall:
MWAT A 0 A_ChangeFlag(NOGRAVITY, false)
stop
}
}

actor MuddyWaterWave1 : MuddyWaterWave
{
speed 10
States
{
Continue:
TNT1 A 0 A_CustomMissile("MuddyWaterWave1",0,0,0,2,0)
TNT1 A 1
stop
}
}

actor MuddyWaterWave2 : MuddyWaterWave
{
speed 15
States
{
Continue:
TNT1 A 0 A_CustomMissile("MuddyWaterWave2",0,0,0,2,0)
TNT1 A 1
stop
}
}

actor MuddyWaterPuff : BasicClientside
{
scale 1.5
States
{
Spawn:
MMFX A 0
MMFX B 0 A_PlaySoundEx("misc/WaterSlide","Weapon")
WHIT ABC 2 A_Spawnitemex("MudslapParticle",0,0,0,random2(15),random2(15),random2(15),0,SXF_TRANSFERTRANSLATION)
HITG HHHH 1 A_Fadeout(0.3)
stop
}
}


actor MuddyWaterNerfWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_GiveToTarget("SpawnStatusParticles")
TNT1 A 0 A_JumpIfInTargetInventory("STF_MuddyWater",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}

