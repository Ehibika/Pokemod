actor EarthPowerMove : WepMasterBase
{
Tag "EarthPower"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "EarthPowerPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("EarthPowerRingSpawner",0,0,0,Aim_OverHead)
//"####" B 0 A_FireCustomMissile("EarthPowerGraphicSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/Usemove","Weapon")
"####" B 0 A_FireCustomMissile("EarthPowerSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("FullRecovery_C")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor EarthPowerMove1 : EarthPowerMove {Weapon.SlotNumber 1}
actor EarthPowerMove2 : EarthPowerMove {Weapon.SlotNumber 2}
actor EarthPowerMove3 : EarthPowerMove {Weapon.SlotNumber 3}
actor EarthPowerMove4 : EarthPowerMove {Weapon.SlotNumber 4}

actor EarthPowerMove5 : EarthPowerMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("EarthPowerMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor EarthPowerPP : Ammo {inventory.amount 1 inventory.Maxamount 10}


Const int EarthPowerRad = 100;

actor EarthPowerCheck : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",AAPTR_TRACER,STATUS_MAJOR),"Nope") //Blocked by protect|Detect|Substitute|Safeguard
TNT1 A 0 A_JumpifInventory("PoisonMark",1,"Crit")
stop

Crit:
TNT1 A 0 A_SpawnItemEX("PowerStrikeFX2",0,0,28)
stop

Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor EarthPowerRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("EarthPowerGuideDot", EarthPowerRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("EarthPowerGuideDot", -EarthPowerRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor EarthPowerRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 450
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_SpawnItemEx("EarthPowerRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}

/*
actor EarthPowerGraphicSpawner : EarthPowerRingSpawner
{
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_SpawnItemEx("EarthPower_Graphic1",0,0,-(z-floorz)+60,0,0,0,0,0)
stop

Terminate:
TNT1 A 0 
stop
}
}
*/
actor EarthPowerGuideDot : BasicGuideDot {}

actor EarthPowerSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 450
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 475,"Terminate")
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("EarthPowerBurst",0,0,-(z-floorz),0,0,0,0,1)
TNT1 A 1
stop

Terminate:
TNT1 A 0 
stop
}
}


actor EarthPowerBurst : BasicExplosion
{
translation "202:202=210:210"//90
Damagetype "EarthPower"
Obituary "$OB_EarthPower"
scale 2.5
Var int user_angle;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_PlaySoundEx("Misc/Quake", "Weapon")
TNT1 A 1 A_RadiusGive("EarthPowerCheck",EarthPowerRad,RGF_PLAYERS|RGF_OBJECTS)
TNT1 A 0 A_SpawnItemEx("EarthPowerBurstFX",0,0,0,0,0,15,0,1)
TNT1 A 1 A_Explode(MBP_EarthPower,EarthPowerRad,0,0,EarthPowerRad/2)
TNT1 A 6 
stop
}
}


actor EarthPowerBurstFX : BasicHelper
{
+CLIENTSIDEONLY
reactiontime 4
var int user_angle;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 //A_GiveInventory("ProjectileCodeSyncTarget",1)
//TNT1 A 1
SpawnLoop:
TNT1 A 0 A_SpawnItemEx("EarthPowerFX",EarthPowerRad,0,-(z-floorz),0,0,0,user_angle,1061) //1061
TNT1 A 0 A_SpawnItemEx("EarthPowerFX2",EarthPowerRad/2,0,-(z-floorz),0,0,0,user_angle+22,1061) //1061
TNT1 A 0 A_SpawnItemEx("EarthPowerFX",-EarthPowerRad,0,-(z-floorz),0,0,0,user_angle,1061) //1061
TNT1 A 0 A_SpawnItemEx("EarthPowerFX2",-EarthPowerRad/2,0,-(z-floorz),0,0,0,user_angle+22,1061) //1061
TNT1 A 0 A_SetUserVar("user_angle", user_angle + 45)
TNT1 A 1 A_Countdown
loop
Death:
TNT1 A 0
stop
}
}

actor EarthPowerFX : BasicClientside
{
Scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
EPOW ABCDEFGHIJKLMNOPQRSTUVWXY 1 
stop
}
}

actor EarthPowerFX2 : BasicClientside
{
Scale 3.5
Renderstyle "add"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
EPOW ABCDEFGHIJKLMNOPQRSTUVWXY 1 
stop
}
}


