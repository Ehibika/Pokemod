actor SleepPowderMove : WepMasterBase
{
Tag "Sleep Powder"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "SleepPowderPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_C)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("SleepPowderAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 1 A_FireCustomMissile("SleepPowder_FlareShot",0,1,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))


//"####" B 0 A_FireCustomMissile("SleepPowderSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("FullRecovery_C")
"####" A 0 A_GiveInventory("MoveCooldown_1Half")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor SleepPowderMove1 : SleepPowderMove {Weapon.SlotNumber 1}
actor SleepPowderMove2 : SleepPowderMove {Weapon.SlotNumber 2}
actor SleepPowderMove3 : SleepPowderMove {Weapon.SlotNumber 3}
actor SleepPowderMove4 : SleepPowderMove {Weapon.SlotNumber 4}

actor SleepPowderMove5 : SleepPowderMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("SleepPowderMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor SleepPowderPP : Ammo {inventory.amount 1 inventory.Maxamount 10}


Const int StnSprRad = 100;
Const int StnSpr_Item = 5;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor SleepPowder_FlareShot : BasicProjectile
{
DamageType "SleepPowder"
obituary "$OB_SleepPowder"
+BOUNCEONWALLS
+BOUNCEONCEILINGS
+HITTRACER
-NOGRAVITY
Speed 40
Damage (0)
Height 10
Radius 8
ReactionTime 80
Gravity 1.5
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2: 
BASB T 2 A_Spawnitemex("SleepPowderFx2",random2(15),random2(15),-5,momX*0.8,momy*0.8,momz*0.8,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM)
TNT1 AA 0 A_CountDown
//OILL DD 2 A_Spawnitemex("SleepPowderFx2",0,0,0,random2(15),random2(15),-5,0,SXF_TRANSFERTRANSLATION)
loop

Death:
TNT1 A 1 A_CheckFloor("FlareExplode")
TNT1 A 2 A_Spawnitemex("SleepPowderPuff",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
FlareExplode:
TNT1 A 0 A_PlaySoundEx("weapon/solarshoot","Weapon")
TNT1 A 5 A_Spawnitemex("SleepPowderField",0,0,-(z-floorz)+0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop

Crash:
TNT1 A 1 
TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TRACER,AAPTR_NULL) == False, "SPCheck")
Goto Death

SPCheck:
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",AAPTR_TRACER,STATUS_MAJOR),"Death") //Blocked by protect|Detect|Substitute|Safeguard and pre-existing conditions
TNT1 A 0 A_JumpIfInventory("Grassguard",1,"Death",AAPTR_TRACER)
TNT1 A 0 A_GiveInventory("SleepStatusProc",1,AAPTR_TRACER)
TNT1 A 0 A_PlaySoundEx("weapon/solarshoot","Weapon")
TNT1 AAA 0 A_Spawnitemex("SleepPowderPuff",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
Goto Death
}
}

actor SleepPowderAimPath : GolfBallAimInstantStarter2 { Speed 40 Gravity 1.5}


actor SleepPowderPuff : BasicClientside
{
scale 1.5
States
{
Spawn:
MMFX A 0
MMFX AAAA 1 A_Spawnitemex("SleepPowderFx2",0,0,0,random2(8),random2(8),random2(8),0,SXF_TRANSFERTRANSLATION)
stop
}
}


actor SleepPowderRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("SleepPowderGuideDot", StnSprRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("SleepPowderGuideDot", -StnSprRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor SleepPowderRingSpawner : BasicFastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 300
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("SleepPowderRingGuide",0,0,0,0,0,0,0,1)
stop
}
}

actor SleepPowderGuideDot : BasicGuideDot {}

actor SleepPowderSpawner : BasicFastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 300
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("SleepPowderField",0,0,-(z-floorz)+24,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
TNT1 A 1
stop
}
}

actor SleepPowderField : BasicExplosion
{
ReactionTime 25
Damage (0)
DamageType "SleepPowder"
obituary "$OB_SleepPowder"
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("SleepPowderRing1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 AA 3 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,StnSpr_Item,StnSprRad+25,RG_ENEMY)
TNT1 A 0 
TNT1 A 0 A_SpawnItemEx("SleepPowderRing2",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 5 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,StnSpr_Item,StnSprRad+25,RG_ENEMY)
TNT1 A 5
stop 

Death:
TNT1 A 1 
stop
}
}

actor SleepPowderRing1 : GravityHoldFX
{
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
		TNT1 A 1
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("SleepPowderFx1", StnSprRad-random(0,40), 0, random(130,160), 0, 0, random(-15,-8), user_angle, SXF_TRANSFERTRANSLATION) //1061
		TNT1 A 0 A_SpawnItemEx("SleepPowderFx1", -StnSprRad-random(0,40), 0, random(130,160), 0, 0, random(-15,-8), user_angle, SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor SleepPowderRing2 : GravityHoldFX
{
	reactiontime 15
	var int user_angle;
	RenderStyle "Translucent"
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
		TNT1 A 1
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("SleepPowderFx2", StnSprRad, 0, -(z-floorz)+24, 5, 0, 0, user_angle, SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SpawnItemEx("SleepPowderFx2", -StnSprRad, 0, -(z-floorz)+24, -5, 0, 0, user_angle, SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0 
		stop
	}
}

actor SleepPowderFx1 : BasicClientside
{
+NOCLIP
scale 1.5
RenderStyle "Translucent"
Alpha 0.1
States
{
Spawn:
TNT1 A 0 
PSPD B 2 A_Fadein(0.2)
PSPD B 0 A_ScaleVelocity(0.8)
PSPD B 2 A_Fadein(0.2)
PSPD B 0 A_ScaleVelocity(0.8)
PSPD B 2 A_Fadein(0.2)
PSPD B 0 A_ScaleVelocity(0.8)
PSPD B 2 A_Fadein(0.2)
PSPD B 0 A_ScaleVelocity(0.8)
PSPD B 2 A_Fadein(0.2)
PSPD B 0 A_ScaleVelocity(0.8)
PSPD B 10 
Goto Death

Death:
PSPD BBBBB 4 A_Fadeout
stop
}
}

actor SleepPowderFx2 : BasicClientside
{
scale 1.5
RenderStyle "Translucent"
Alpha 0.6
States
{
Spawn:
TNT1 A 0 
TNT1 A random(1,8)
PSMS ABC 3 A_ScaleVelocity(0.6)
PSMS DEF 3
stop
}
}