actor ThunderMove : WepMasterBase
{
Tag "Thunder"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "ThunderPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_D)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim

Fire.Aim:
"####" B 1 A_FireCustomMissile("ThunderRingSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_JumpIfInventory("IsBot",1,"Fire.Bullet")
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/Usemove","Weapon")
"####" B 0 A_FireCustomMissile("ThunderSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("Thunder_Recovery_D")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End


AltFire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_D)==1,"AltFire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

AltFire.Begin:
"####" B 1
"####" C 0 A_REfire("AltFire.Begin")
"####" A 0 A_GiveInventory("DashSet_A")
"####" A 0 A_GiveInventory("ThunderRCFlag")
"####" A 0 A_Stop
"####" A 0 A_SpawnItemEX("Thunder_Remote")
"####" B 0 A_Jump(256,"AltFire.Remote")
Goto AltFire.Remote


AltFire.Remote:
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"AltFire.After")
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_RELOAD)==1,"AltFire.RemoteReload")
"####" B 0 A_JumpifInventory("ThunderRCFlag",1,"AltFire.Remote")
Goto AltFire.After
AltFire.RemoteReload:
TNT1 A 0 A_GiveInventory("PPRecoveryItem") 
Goto AltFire.Remote

AltFire.After:
"####" B 1 A_GiveInventory("FullRecovery_D")
"####" A 0 A_GiveInventory("DashSetOff")
AltFire.After2:
"####" B 1
"####" B 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"AltFire.After2")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor ThunderMove1 : ThunderMove {Weapon.SlotNumber 1}
actor ThunderMove2 : ThunderMove {Weapon.SlotNumber 2}
actor ThunderMove3 : ThunderMove {Weapon.SlotNumber 3}
actor ThunderMove4 : ThunderMove {Weapon.SlotNumber 4}

actor ThunderMove5 : ThunderMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("ThunderMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor ThunderPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor ThunderRCFlag : Powerup {inventory.amount 1 powerup.duration 35 }

Const int ThunderRad = 150;
Const int ThunderRad2 = 70;
Const int Thunder_Forward = 150;
Const int Thunder_Range = 1500;

actor ThunderPPTaker : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_Thunder,1)
stop
}
}

actor Thunder_Recovery_D : FullRecovery_D{Powerup.Duration 52} //87

actor Thunder_SweetSpot : Powerup {inventory.amount 1 powerup.duration 17}

actor ThunderParaChance : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpifInventory("Thunder_SweetSpot",1,"Pickup2")
stop
Pickup2:
TNT1 A 0 A_Jump(203,"Paralyse")
stop
Paralyse:
TNT1 A 0 A_GiveInventory("Zap_ParalysisStatusProc2")
stop

Nope:
TNT1 A 0
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor Thunder_Remote 
{
Speed 5
+MISSILE
+THRUACTORS
+NOCLIP
+FLOORHUGGER
+NOEXPLODEFLOOR
//+SLIDESONWALLS
//+BOUNCEONWALLS 
+NOGRAVITY
Health 5
Damage (0)
DamageType "Thunder"
obituary "$OB_Thunder"
radius 5
Reactiontime 50
height 16
scale 3.5
Args 0, 60
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 ACS_NamedExecuteAlways("CamGrabObject",0)
TNT1 A 0 ACS_NamedExecuteAlways("RemoteControlActor",0,15.0,Thunder_Range)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_DEFAULT)
Spawn2:
TNT1 A 1 A_JumpIfCloser(Thunder_Range-1,"Spawn3")
TNT1 A 0 A_SpawnItemEx("ThunderRingGuide2",Thunder_Forward,0,0,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("ThunderRCFlag",1)
TBLA A 0
loop
Spawn3:
TNT1 A 1 A_SpawnItemEx("ThunderRingGuide",Thunder_Forward,0,0,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("ThunderRCFlag",1)
TBLA A 0
goto Spawn2

Trigger: //Check PP
TNT1 A 0 A_JumpifInTargetInventory("ThunderPP",1,"Trigger2")
goto Decline
Trigger2: // Check Recovery Frame
TNT1 A 0 A_JumpifInTargetInventory("Recovery_D",1,"Decline")
goto Detonate

Detonate: // Cast the attack!
TNT1 A 0 A_GiveToTarget("ThunderPPTaker",1)
TNT1 A 0 A_SpawnItemEx("ThunderHitbox",Thunder_Forward,0,-(z-floorz)+60,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("Thunder_Recovery_D",1)
goto PostTrigger

Decline: //Cannot cast the attack
TNT1 A 0 A_GiveToTarget("DeclineSoundCue",1)
goto PostTrigger

PostTrigger:
TNT1 AAAAA 1 A_SpawnItemEx("ThunderRingGuide",Thunder_Forward,0,0,0,0,0,0,1)
Goto Spawn2

Death:
BSTG AAAAAA 0 A_SpawnItemEx("RazorLeafParticle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
SPKH ABCD 2
stop
}
}

actor ThunderHitbox : LightningBolt
{
ReactionTime 1
DamageType "Thunder"
obituary "$OB_Thunder"
+OLDRADIUSDMG
YScale 4.0
XScale 8.0
states
{
Spawn:
Y_BC N 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 AAAAAAA 3 A_SpawnItemEx("Thunder_SparksFXSpawn",0,0,16,0,0,0,0,1)
TNT1 A 2 A_RadiusGive("Thunder_SweetSpot",ThunderRad2,RGF_PLAYERS)
Y_BC A 1 A_SpawnItemEx("TThunder_Extend",0,0,256,0,0,0,0,1)
Y_BC A 0 A_SpawnItemEx("Thunder_GroundWaveRing",0,0,16,0,0,0,0,1)
Y_BC N 0 A_PlaySoundEx("weapon/lightningbolt","Weapon")
Y_BC N 0 A_Explode(MBP_Thunder,ThunderRad,0,0,ThunderRad2)
Y_BC A 1
Y_BC P 0 A_SpawnItemEx("Thunder_BoltFX2",Random(32,96),Random(32,96),32,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("Thunder_BoltFX2",Random(-32,-96),Random(-32,-96),64,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("Thunder_BoltFX2",Random(32,96),Random(32,96),96,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("Thunder_BoltFX2",Random(-32,-96),Random(-32,-96),128,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("Thunder_BoltFX2",Random(32,96),Random(32,96),160,0,0,0,0,1)
Y_BC P 0 A_SpawnItemEx("Thunder_BoltFX2",Random(-32,-96),Random(-32,-96),192,0,0,0,0,1)
Y_BC ABC 3
Stop
}
}



Actor TThunder_Extend : LightningExtend
{
YScale 4.0
XScale 8.0
States
{
Spawn:
Y_BC S 0 
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
Y_BC S 0 A_JumpIf(ceilingz-z <= 32,"Death")
Y_BC S 0 A_SpawnItemEx("TThunder_Extend",0,0,256,0,0,0,0,1)
Y_BC AA 1
Y_BC ABC 3
stop
}
}

actor Thunder_BoltFX2 : BasicClientSide
{
States
{
Spawn:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
Y_BC DEFGHIJ 2
stop
}
}


actor Thunder_GroundWaveFX : BasicClientside
{
Scale 1.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
DUOR T 1 A_FadeOut(0.05)
loop
}
}

actor Thunder_GroundWaveRing : BasicHelper
{
	+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("Thunder_GroundWaveFX", ThunderRad, 0, -(z-floorz)+24, 5, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("Thunder_GroundWaveFX", -ThunderRad, 0, -(z-floorz)+24, -5, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor Thunder_SparksFX : BasicClientside
{

 Scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_Jump(256,"S_K","S_L","S_M")
wait
S_K:
SHOC EFGH 3 A_Fadeout
stop
S_L:
SHOC GHIE 3 A_Fadeout
stop
S_M:
SHOC FGHI 3 A_Fadeout
stop
}
}

actor Thunder_SparksFXSpawn : BasicHelper
{
+CLIENTSIDEONLY
States
{
Spawn:
TNT1 A 0
TNT1 AA 0 A_SpawnItemEx("Thunder_SparksFX", ThunderRad, 0, -(z-floorz)+16, 0, 0, 2, random(0,360), 1061)
TNT1 AAAA 0 A_SpawnItemEx("Thunder_SparksFX", ThunderRad2, 0, -(z-floorz)+16, 0, 0, 2, random(0,360), 1061)
TNT1 A 2 
stop
}
}

actor ThunderRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("ThunderGuideDotA", ThunderRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
        TNT1 A 0 A_SpawnItemEx("ThunderGuideDotA", -ThunderRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
        
        TNT1 A 0 A_SpawnItemEx("ThunderGuideDotA2", ThunderRad2, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
        TNT1 A 0 A_SpawnItemEx("ThunderGuideDotA2", -ThunderRad2, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
        
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor ThunderRingGuide2 : BasicHelper
{
	+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("ThunderGuideDotB", ThunderRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
        TNT1 A 0 A_SpawnItemEx("ThunderGuideDotB", -ThunderRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)		
        
        TNT1 A 0 A_SpawnItemEx("ThunderGuideDotB2", ThunderRad2, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("ThunderGuideDotB2", -ThunderRad2, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor ThunderRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 600
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 175,"Terminate")
TNT1 A 0 A_SpawnItemEx("ThunderRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}


actor ThunderSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 600
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 625,"Terminate")
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("ThunderHitbox",0,0,-(z-floorz),0,0,0,0,1)
TNT1 A 1
stop

Terminate:
TNT1 A 0 
stop
}
}


actor ThunderGuideDotA : BasicGuideDot {}
actor ThunderGuideDotA2 : BasicGuideDot {scale 0.5}

actor ThunderGuideDotB : BasicGuideDot {StencilColor "#F42A0C"}
actor ThunderGuideDotB2 : BasicGuideDot {scale 0.5}
