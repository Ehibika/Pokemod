actor DracoMeteorMove : WepMasterBase
{
Tag "DracoMeteor"
Weapon.AmmoUse 1
Weapon.AmmoGive 5
weapon.ammotype "DracoMeteorPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_D)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 1
"####" C 0 A_REfire("Fire.Begin")
"####" A 0 A_GiveInventory("DashSet_A")
"####" A 0 A_GiveInventory("DracoMeteorRCFlag")
"####" A 0 A_Stop
"####" A 0 A_SpawnItemEX("DracoMeteor_Remote")
"####" B 0 A_Jump(256,"Fire.Remote")
Goto Fire.Remote


Fire.Remote:
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After")
"####" B 1 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_RELOAD)==1,"Fire.RemoteReload")
"####" B 0 A_JumpifInventory("DracoMeteorRCFlag",1,"Fire.Remote")
Goto Fire.After
Fire.RemoteReload:
TNT1 A 0 A_GiveInventory("PPRecoveryItem") 
Goto Fire.Remote

Fire.After:
"####" B 1 //A_GiveInventory("DracoMeteor_Recovery_D")
"####" A 0 A_GiveInventory("DashSetOff")
Fire.After2:
"####" B 1
"####" B 0 A_Jumpif(CallACS("PKMNACS_ButtonCheck",BT_ALTATTACK)==1,"Fire.After2")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}


actor DracoMeteorMove1 : DracoMeteorMove {Weapon.SlotNumber 1}
actor DracoMeteorMove2 : DracoMeteorMove {Weapon.SlotNumber 2}
actor DracoMeteorMove3 : DracoMeteorMove {Weapon.SlotNumber 3}
actor DracoMeteorMove4 : DracoMeteorMove {Weapon.SlotNumber 4}

actor DracoMeteorMove5 : DracoMeteorMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("DracoMeteorMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor DracoMeteorPP : Ammo {inventory.amount 1 inventory.Maxamount 5}

actor DracoMeteorRCFlag : Powerup {inventory.amount 1 powerup.duration 35 }

Const int DracoMeteorRad = 200;
Const int DracoMeteor_Forward = 150;
Const int DracoMeteor_Range = 1000;

Actor DracoMeteorProtect : powerprotection
{
Powerup.duration 17
Damagefactor "DracoMeteor", 0
}

actor DracoMeteorPPTaker : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_DracoMeteor,1)
stop
}
}

actor DracoMeteor_Recovery_D : FullRecovery_D{Powerup.Duration -4} 

actor STF_DracoMeteor : Powerup {inventory.amount 1 Powerup.Duration -7}

actor DracoMeteorBuffPurge : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_GiveInventory("Overheated")
TNT1 A 0 ACS_NamedExecuteAlways("StatItemNuke",0,SB_GOOD)
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop
}
}

actor DracoMeteorNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
//Self inflicted, not blocked by mist
TNT1 A 0 A_Jumpifinventory("STF_DracoMeteor",1,"Sustain")
TNT1 A 0 A_GiveInventory("STF_DracoMeteor",1)
TNT1 A 0 A_SpawnItemEX("DracoMeteorNerfWatcher")
TNT1 A 0 A_GiveInventory("Overheated")
TNT1 A 0 ACS_NamedExecuteAlways("StatItemNuke",0,SB_GOOD)
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_DracoMeteor",1)
stop
Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor DracoMeteor_Remote 
{
Speed 5
+MISSILE
+THRUACTORS
+NOCLIP
+FLOORHUGGER
+NOEXPLODEFLOOR
//+SLIDESONWALLS
//+BOUNCEONWALLS 
+NOGRAVITY
Health 5
Damage (0)
DamageType "DracoMeteor"
obituary "$OB_DracoMeteor"
radius 5
Reactiontime 50
height 16
scale 3.5
Args 0, 60
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 ACS_NamedExecuteAlways("CamGrabObject",0)
TNT1 A 0 ACS_NamedExecuteAlways("RemoteControlActor",0,15.0,DracoMeteor_Range)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_DEFAULT)
Spawn2:
TNT1 A 1 A_JumpIfCloser(DracoMeteor_Range-1,"Spawn3")
TNT1 A 0 A_SpawnItemEx("DracoMeteorRingGuide2",DracoMeteor_Forward,0,0,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("DracoMeteorRCFlag",1)
TBLA A 0
loop
Spawn3:
TNT1 A 1 A_SpawnItemEx("DracoMeteorRingGuide",DracoMeteor_Forward,0,0,0,0,0,0,1)

TNT1 A 0 A_GiveToTarget("DracoMeteorRCFlag",1)
TBLA A 0
goto Spawn2

Trigger: //Check PP
TNT1 A 0 A_JumpifInTargetInventory("DracoMeteorPP",1,"Trigger2")
goto Decline
Trigger2: // Check Recovery Frame
TNT1 A 0 A_JumpifInTargetInventory("Recovery_D",1,"Decline")
goto Detonate

Detonate: // Cast the attack!
TNT1 A 0 A_GiveToTarget("DracoMeteorPPTaker",1)
TNT1 A 0 A_GiveToTarget("DracoMeteorBuffPurge",1)
TNT1 A 0 A_SpawnItemEx("DracoMeteorHitbox",DracoMeteor_Forward,0,-(z-floorz)+24,0,0,0,0,1)
TNT1 A 0 A_GiveToTarget("DracoMeteor_Recovery_D",1)
goto PostTrigger

Decline: //Cannot cast the attack
TNT1 A 0 A_GiveToTarget("DeclineSoundCue",1)
goto PostTrigger

PostTrigger:
TNT1 AAAAA 1 A_SpawnItemEx("DracoMeteorRingGuide",DracoMeteor_Forward,0,0,0,0,0,0,1)
Goto Spawn2

Death:
TNT1 A 0
stop
}
}

actor DracoMeteorHitbox : BasicExplosion
{
Damage(0)
DamageType "DracoMeteor"
obituary "$OB_DracoMeteor"
Reactiontime 35
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_playSoundEx("misc/alarmlong","Weapon")
TNT1 AAAAAA 4 A_SpawnItemEx("DracoMeteorWarnWave",0,0,0,0,0,0,0,1)
TNT1 A 0 A_playSoundEx("weapon/astrocrush","SoundSlot6")
Spawn2:
TNT1 A 0 A_SpawnItemEx("DracoMeteorWarnWave",0,0,0,0,0,0,0,1)
TNT1 A 2 A_SpawnItemEx("DracoMeteorMeteorSpawner",random2(DracoMeteorRad),random2(DracoMeteorRad),0,0,0,320,0,1)
TNT1 A 0 A_Countdown
TNT1 A 2 A_SpawnItemEx("DracoMeteorMeteorSpawner",random2(DracoMeteorRad),random2(DracoMeteorRad),0,0,0,320,0,1)
TNT1 A 0 A_Countdown
loop

Death:
TNT1 A 0 A_GiveToTarget("DracoMeteorNerfInitiate",1)
TNT1 A 0 
stop
}
}

actor DracoMeteorWarnWave : BasicHelper
{
translation "198:198=45:45", "192:192=45:45"
+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("DracoMeteorWarnFX", DracoMeteorRad*0.75, 0, -(z-floorz)+24, 5, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("DracoMeteorWarnFX", -DracoMeteorRad*0.75, 0, -(z-floorz)+24, -5, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor DracoMeteorWarnFX : BasicClientside
{
Scale 2.5
RenderStyle "Translucent"
Alpha 0.6
States
{
Spawn:
TNT1 A 0
TNT1 A 1
FIRE FEDEF 3 
stop
}
}

actor DracoMeteorMeteorSpawner : BasicFastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 300
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1
TNT1 A 0 A_SpawnItemEx("DracoMeteor",0,0,0,0,0,-35,0,1)
TNT1 A 1
stop
}
}

actor DracoMeteor
{
PROJECTILE
+RIPPER
+NOINTERACTION
+DONTBLAST
Damagetype "DracoMeteor"
Obituary "$OB_DRACOMETEOR"
damage (0)
height 8
radius 16
Scale 2.5
Reactiontime 10
States
{
Spawn:
DMTO B 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
DMTO BBBCCCDDD 1 A_JumpIf(ceilingz-z>8, "Spawn2")
DMTO B 0 A_JumpIf(momz==0,"Death")
DMTO B 0 A_CountDown
Goto Spawn+2
Spawn2:
DMTO B 0 A_ChangeFlag("RIPPER", 0)
DMTO B 0 A_ChangeFlag("NOINTERACTION",0)
DMTO BCD 3 
DMTO B 0 A_JumpIf(momz==0,"Death")
DMTO B 0 A_CountDown
Goto Spawn2+2
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
ASEX A 0 A_playSoundEx("weapon/astrocrush2","Weapon")
ASEX A 0 A_ChangeFlag(NOGRAVITY,1)
ASEX A 0 A_Explode(MBP_Dracometeor,128,0,0,128)
ASEX ABCDEF 2
stop
}
}


actor DracoMeteorRingGuide : BasicHelper
{
	+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("DracoMeteorGuideDot", DracoMeteorRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("DracoMeteorGuideDot", -DracoMeteorRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor DracoMeteorRingGuide2 : BasicHelper
{
	+CLIENTSIDEONLY
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("DracoMeteorGuideDot2", DracoMeteorRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("DracoMeteorGuideDot2", -DracoMeteorRad, 0, -(z-floorz)+24, 0, 0, 0, angle+user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}

actor DracoMeteorRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 150
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_Jumpif(z-floorz > 175,"Terminate")
TNT1 A 0 A_SpawnItemEx("DracoMeteorRingGuide",0,0,0,0,0,0,0,1)
stop

Terminate:
TNT1 A 0 
stop
}
}


actor DracoMeteorGuideDot : BasicGuideDot {}

actor DracoMeteorGuideDot2 : BasicGuideDot {StencilColor "#F42A0C"}



actor DracoMeteorNerfWatcher : BasicHelper 
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveToTarget("Overheated")
TNT1 A 5 A_GiveToTarget("SatkDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_DracoMeteor",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}
