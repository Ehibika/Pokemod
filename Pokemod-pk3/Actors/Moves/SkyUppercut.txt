actor SkyUppercutMove : WepMasterBase
{
Tag "SkyUppercut"
obituary "$OB_SkyUppercut"
Weapon.AmmoUse 1
Weapon.AmmoGive 35
weapon.ammotype "SkyUppercutPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop

Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_D)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 1 A_Jump(256,"Fire.Melee")//A_CheckFloor("Fire.Melee")
Goto Fire.Melee

Fire.Melee:
TNT1 A 0 ACS_NamedExecuteAlways("TakePP",0,Move_Skyuppercut,1)
TNT1 A 0 A_PlaySoundEx("weapon/ChargeKick","Weapon")
TNT1 B 5 ThrustThing(angle * 256 / 360, 25, 0, 0)
TNT1 B 0 A_GiveInventory("SkyUppercutBuffer")
TNT1 A 0 A_PlaySoundEx("weapon/SlashClaw","SoundSlot6")
Fire.Melee2:
TNT1 A 1 A_FireCustomMissile("SkyUppercutMelee",0,0,0,0,pitch)
TNT1 A 0 ThrustThingZ(0,25,0,1)
TNT1 A 0 A_JumpifInventory("SkyUppercutBuffer",1,"Fire.Melee2")
//===
TNT1 B 0 A_GiveInventory("SkyUppercutBuffer2")
Fire.Melee3:
TNT1 A 0 ThrustThingZ(0,-25,0,1)
TNT1 A 1 A_JumpIf(MomZ <=0 || Z-FloorZ == 0, "Fire.After")
TNT1 A 0 A_JumpifInventory("SkyUppercutBuffer2",1,"Fire.Melee3")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_D") //Sky Uppercut uses D recovery so it can combo with other moves
"####" A 0 A_GiveInventory("MoveCooldown_3")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor SkyUppercutMove1 : SkyUppercutMove {Weapon.SlotNumber 1}
actor SkyUppercutMove2 : SkyUppercutMove {Weapon.SlotNumber 2}
actor SkyUppercutMove3 : SkyUppercutMove {Weapon.SlotNumber 3}
actor SkyUppercutMove4 : SkyUppercutMove {Weapon.SlotNumber 4}

actor SkyUppercutMove5 : SkyUppercutMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("SkyUppercutMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}


//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor SkyUppercutPP : Ammo {inventory.amount 1 inventory.Maxamount 25}

Actor SkyUppercutBuffer : Powerup {inventory.amount 1 Powerup.duration 10}
Actor SkyUppercutBuffer2 : Powerup {inventory.amount 1 Powerup.duration 35}

Actor SkyUppercutProtect : powerprotection
{
Powerup.duration 8
Damagefactor "SkyUppercut", 0.1
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor SkyUppercutMelee : BasicMeleeAttack
{
Speed 80
Scale 2.0
DamageType "SkyUppercut"
Obituary "$OB_SkyUppercut"
Damage(0)
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 
TNT1 A 0 A_Explode(MBP_SkyUppercut,RAMHITBOX,0,0,RAMHITBOX)
TNT1 A 1 A_SpawnItemEx("SkyUppercutFX1", 0, 0, 0, 0, 0, 0, 0,1)
stop

Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Explode(MBP_SkyUppercut,RAMHITBOX,0,0,RAMHITBOX)
TNT1 A 0 A_SpawnItemEx("SkyUppercutFX1", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 5
stop
}
}


actor SkyUppercutFXTrail : BasicClientside
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
HARD CDE 1
HARD EEE 1 A_Fadeout(0.3)
stop
}
}


actor SkyUppercutFX1 : BasicClientside
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
HITG E 2
HITG EEEE 1 A_Fadeout(0.3)
stop
}
}

actor SkyUppercutCeilingBonk : BasicClientside
{
scale 4.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEX("Misc/CeilingBonk","SoundSlot8")
TNT1 A 0 A_Quake(4,12,0,250)
HITG FGH 1
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
TNT1 A 6
stop
}
}