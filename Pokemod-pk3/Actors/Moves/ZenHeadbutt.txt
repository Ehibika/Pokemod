actor ZenheadbuttMove : WepMasterBase
{
Tag "Zenheadbutt"
obituary "$OB_ZENHEADBUTT"
Weapon.AmmoUse 1
Weapon.AmmoGive 30
weapon.ammotype "ZenheadbuttPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.DashStart")
Goto Fire.DashStart

Fire.DashStart:
"####" A 0 A_GiveInventory("DashSet_B")
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_Zenheadbutt,1)
"####" A 0 A_GiveInventory("ZenheadbuttTimer")
"####" A 0 A_GiveInventory("ZenheadbuttInitiate")
"####" B 0 A_PlaySoundEx("misc/kingswing","SoundSlot6")
"####" B 0 A_PlaySoundEx("Weapon/TenguShot","SoundSlot7")
Fire.Dash:
"####" A 1
"####" A 0 A_SpawnItemEX("GroundDustFX",-25,0,-(z-floorz),0)
"####" A 0 A_Jumpifinventory("DashBreaker",1,"Fire.DashEnd")
"####" A 0 A_Jumpifinventory("ZenheadbuttTimer",1,"Fire.Dash")
goto Fire.DashEnd

Fire.DashEnd:
"####" B 0 A_ScaleVelocity(0.2)
"####" B 0 A_GiveInventory("DashSetOff")
"####" B 1 A_GiveInventory("FullRecovery_B")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End




}
}

actor ZenheadbuttMove1 : ZenheadbuttMove {Weapon.SlotNumber 1}
actor ZenheadbuttMove2 : ZenheadbuttMove {Weapon.SlotNumber 2}
actor ZenheadbuttMove3 : ZenheadbuttMove {Weapon.SlotNumber 3}
actor ZenheadbuttMove4 : ZenheadbuttMove {Weapon.SlotNumber 4}

actor ZenheadbuttMove5 : ZenheadbuttMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("ZenheadbuttMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor ZenheadbuttPP : Ammo {inventory.amount 1 inventory.Maxamount 30}

actor ZenheadbuttTimer : Powerup {inventory.amount 1 powerup.duration 10 }

Actor ZenheadbuttDelay : powerprotection
{
Powerup.duration 5
Damagefactor "Zenheadbutt", 0
}

const int ZHDBT_Thrust = 32;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor ZenheadbuttBumper : BasicProjectile
{
Speed 32 //Same as ZHDBT_Thrust
Damage (0)
radius 12
height 12
DamageType "Zenheadbutt"
Obituary "$OB_ZENHEADBUTT"
scale 1.5
//+NOINTERACTION
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Terminate")
TNT1 A 1
TNT1 A 1 A_Explode(MBP_Zenheadbutt,32,0,0,32)
stop
Death:
TNT1 A 0 A_Explode(MBP_Zenheadbutt,32,0,0,32)
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_GiveToTarget("ZenheadbuttCollision",1)
TNT1 A 0 A_SpawnItemEx("BasicMeleeFX", 0, 0, 0, 0, 0, 0, 0,1)
Crash:
TNT1 A 1 A_Explode(MBP_Zenheadbutt,32,0,0,32)
Terminate:
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

actor ZenheadbuttWave
{
PROJECTILE
+FLOORCLIP
+THRUGHOST
+MTHRUSPECIES
+NOEXPLODEFLOOR
Radius 5
Height 5
scale 2.0
damage (0)
reactiontime 3
DamageType "Zenheadbutt"
obituary "$OB_Zenheadbutt"
speed 0
States
{
Spawn:
BEWA B 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(ScaleX+0.3)
BEWA B 0 A_CountDown
BEWA BBB 0 A_SpawnItemEx("PsybeamParticle", random2(64), random2(64), random2(64), 0, 0, 0, 0,SXF_TRANSFERTRANSLATION)
Spawn2:
BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(ScaleX+0.3)
BEWA B 0 A_CountDown
loop
Death:
BEWA A 0
stop
}
}


actor ZenheadbuttCollision : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_PlaySoundEx("weapon/Rebound","SoundSlot5")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*-35,0,sin(pitch)*35,CVF_RELATIVE)
TNT1 A 0 ThrustThingZ(0,30,1,1)
stop
Nope:
TNT1 A 0
stop
}
}

/*
actor ZenheadbuttCollision2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_PlaySoundEx("weapon/Rebound","SoundSlot5")
TNT1 A 0 DamageThing(100*(CallACS("StatValueRamp",STAT_PATK,1,400)*0.01))
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
stop
Nope:
TNT1 A 0
stop
}
}
*/

actor ZenheadbuttThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_ChangeVelocity(ZHDBT_Thrust,0,Momz,CVF_RELATIVE|CVF_REPLACE)//cos(-pitch)* , sin(-pitch)*ZHDBT_Thrust
TNT1 A 1 A_FireCustomMissile("ZenheadbuttBumper",0,0,0,15,0,Pitch)
BASB A 1 A_SpawnitemEx("ZenheadbuttWave",cos(-pitch)*ZHDBT_Thrust,0,(25),0,0,0,0,SXF_TRANSFERTRANSLATION) //+(sin(pitch)*30)
stop
Nope:
TNT1 A 0
stop
}
}

actor ZenheadbuttInitiate : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Spawnitemex("ZenheadbuttWatcher")
stop
}
}

actor ZenheadbuttWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Death")
TNT1 A 0 A_SetUserVar("user_DashTime",user_DashTime+1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1 A_GiveToTarget("ZenheadbuttThrust")
TNT1 A 0 A_JumpIfInTargetInventory("ZenheadbuttTimer",1,"Spawn")
//TNT1 A 0 A_Jumpif(user_DashTime <= 6,"Spawn")
Goto Death
Check2:
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Spawn")
Goto Death
Death:
DeathTake:
TNT1 A 0 A_GiveToTarget("DashSetOff",1)
stop
}
}