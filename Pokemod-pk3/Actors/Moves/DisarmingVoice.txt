actor DisarmingVoiceMove : WepMasterBase
{
Tag "DisarmingVoice"
obituary "$OB_DisarmingVoice"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "DisarmingVoicePP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Move/DisarmingVoice","Weapon")
"####" B 0 A_FireCustomMissile("DisarmingVoiceShot",0,1,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor DisarmingVoiceMove1 : DisarmingVoiceMove {Weapon.SlotNumber 1}
actor DisarmingVoiceMove2 : DisarmingVoiceMove {Weapon.SlotNumber 2}
actor DisarmingVoiceMove3 : DisarmingVoiceMove {Weapon.SlotNumber 3}
actor DisarmingVoiceMove4 : DisarmingVoiceMove {Weapon.SlotNumber 4}

actor DisarmingVoiceMove5 : DisarmingVoiceMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("DisarmingVoiceMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor DisarmingVoicePP : Ammo {inventory.amount 1 inventory.Maxamount 20}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor DisarmingVoiceShot : BasicProjectile
{
Speed 32
Damage (MBP_DisarmingVoice)
DamageType "DisarmingVoice"
obituary "$OB_DisarmingVoice"
+SEEKERMISSILE
+SCREENSEEKER
+FLOAT
radius 5
Reactiontime 30
height 16
scale 3.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("DisarmingVoiceFX",0,0,0,0,0,0,0,1)
Spawn2: // -=- Seeking Target -=-
SPLQ R 0 A_SeekerMissile (2, 10, SMF_LOOK|SMF_PRECISE, 256, 2)
SPLQ R 0 A_SpawnItemEx("CaptivateParticle",random(-25,25),random(-25,25),random(-25,25),0,0,0,0,1)
SPLQ R 2 A_SpawnItemEx("GrassWhistleParticle",0,0,0,0,0,0,0,1)
SPLQ R 0 A_Countdown

TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TRACER, AAPTR_NULL) == TRUE, "Spawn2")
Goto Spawn3

Spawn3: // -=- ExamineTarget -=-
TNT1 A 0 A_JumpifInventory("SafeguardPower",1,"TargClear",AAPTR_TRACER)
TNT1 A 0 A_JumpifInventory("CraftyShieldPower",1,"TargClear",AAPTR_TRACER)
Goto Spawn4

TargClear:
SPLQ R 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_NULL)
Goto Spawn4

Spawn4: // -=- target acquired -=-
TNT1 AAA 1 A_SeekerMissile (2, 10, SMF_PRECISE)
SPLQ R 0 A_SpawnItemEx("GrassWhistleParticle",0,0,0,0,0,0,0,1)
SPLQ R 0 A_SpawnItemEx("CaptivateParticle",random(-25,25),random(-25,25),random(-25,25),0,0,0,0,1)
TNT1 AAA 1 A_SeekerMissile (2, 10, SMF_PRECISE)
SPLQ R 0 A_SpawnItemEx("GrassWhistleParticle",0,0,0,0,0,0,0,1)
SPLQ R 0 A_SpawnItemEx("CaptivateParticle",random(-25,25),random(-25,25),random(-25,25),0,0,0,0,1)
TNT1 AAA 1 A_SeekerMissile (2, 10, SMF_PRECISE)
SPLQ R 0 A_SpawnItemEx("GrassWhistleParticle",0,0,0,0,0,0,0,1)
SPLQ R 0 A_SpawnItemEx("CaptivateParticle",random(-25,25),random(-25,25),random(-25,25),0,0,0,0,1)
SPLQ R 0 A_Countdown
loop


Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_PlaySoundEx("weapon/noisecrush1","Weapon")
BSTG RRR 0 A_SpawnItemEx("GrassWhistleParticle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
BSTG AAA 0 A_SpawnItemEx("CaptivateParticle", 0, 0, 28, 256/24, 256/24, random2(256/24), random(0, 359),1)
SPKH ABCD 2
stop
}
}


actor DisarmingVoiceFX : BasicHelper
{
reactiontime 6
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,28,0,WARPF_COPYINTERPOLATION)
TNT1 A 1 A_SpawnItemEx("GrassWhistleParticle",random(-40,40),random(-40,40),random(10,40),0,0,0,0,1)
TNT1 A 0 A_CountDown
loop
}
}

