const int HJK_Thrust = 55;

actor HiJumpKickMove : WepMasterBase
{
Tag "HiJumpKick"
obituary "$OB_HiJumpKick"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "HiJumpKickPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.DashWarmup")
Goto Fire.DashWarmup

Fire.DashWarmup:
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_HiJumpKick,1)
"####" A 0 A_GiveInventory("HiJumpKickWarmupTimer")
"####" A 0 SetPlayerProperty(0,1,0)
"####" A 0 A_Stop
"####" A 0 A_PlaySoundEx("weapon/ChargeKick","Weapon")
"####" A 3 ThrustthingZ(0,120,0,0)
Fire.DashWarmup1:
"####" A 1 ThrustthingZ(0,-30,0,1)
"####" A 0 A_Jumpifinventory("HiJumpKickWarmupTimer",1,"Fire.DashWarmup1")
"####" B 0 A_Jump(256,"Fire.DashStart")
Goto Fire.DashWarmup

Fire.DashStart:
"####" A 0 SetPlayerProperty(0,1,4)
"####" A 0 A_ChangeFlag("NOGRAVITY",1)
"####" A 0 A_GiveInventory("HiJumpKickTimer")
"####" A 0 A_GiveInventory("HiJumpKickInitiate")
"####" A 0 A_GunFlash("Flash.DashSound")
Fire.Dash:
"####" A 1 A_GiveInventory("HiJumpKickThrust")
"####" A 0 A_Jumpifinventory("DashBreaker",1,"Fire.DashEnd")
"####" A 0 A_Jumpifinventory("HiJumpKickTimer",1,"Fire.Dash")
"####" A 0 A_GiveInventory("HiJumpKickDashFX")
goto Fire.DashEnd

Fire.DashEnd:
"####" B 0 A_ScaleVelocity(0.2)
"####" A 0 A_ChangeFlag("NOGRAVITY",0)
"####" B 0 SetPlayerProperty(0,0,4)
"####" A 0 SetPlayerProperty(0,0,0)
"####" B 0 A_TakeInventory("HiJumpKickTimer",99)
"####" B 1 A_GiveInventory("FullRecovery_B")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End


Flash.DashSound:
TNT1 A 5 A_PlaySoundEx("misc/Windloop","SoundSlot5")
"####" A 0 A_Jumpifinventory("HiJumpKickTimer",1,"Flash.DashSound")
stop

}
}

actor HiJumpKickMove1 : HiJumpKickMove {Weapon.SlotNumber 1}
actor HiJumpKickMove2 : HiJumpKickMove {Weapon.SlotNumber 2}
actor HiJumpKickMove3 : HiJumpKickMove {Weapon.SlotNumber 3}
actor HiJumpKickMove4 : HiJumpKickMove {Weapon.SlotNumber 4}

actor HiJumpKickMove5 : HiJumpKickMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("HiJumpKickMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor HiJumpKickPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor HiJumpKickTimer : Powerup {inventory.amount 1 powerup.duration -3 }
actor HiJumpKickWarmupTimer : Powerup {inventory.amount 1 powerup.duration 5 }

Actor HiJumpKickDelay : powerprotection
{
Powerup.duration 5
Damagefactor "HiJumpKick", 0
}


//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor HiJumpKickBumper : BasicProjectile
{
Speed 55
Damage (0)
radius 6
height 6
DamageType "HiJumpKick"
Obituary "$OB_HiJumpKick"
scale 1.5
//+NOINTERACTION
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Terminate")
BASB T 1 
TNT1 A 1 A_Explode(120,80,0,0,80)
stop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_GiveToTarget("HiJumpKickCollision2",1)
TNT1 A 0 A_SpawnItemEx("BasicMeleeFX", 0, 0, 0, 0, 0, 0, 0,1)
Crash:
Terminate:
TNT1 A 1
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

actor HiJumpKickWave
{
PROJECTILE
+FLOORCLIP
+THRUGHOST
+MTHRUSPECIES
+NOEXPLODEFLOOR
Radius 5
Height 5
scale 2.0
damage (0)
reactiontime 3
DamageType "HiJumpKick"
obituary "$OB_HiJumpKick"
speed 0
States
{
Spawn:
BEWA B 0

BEWA B 1 A_Fadeout
BEWA B 0 A_SetScale(ScaleX+0.3)
BEWA B 0 A_CountDown
loop
Death:
BEWA A 0
stop
}
}


actor HiJumpKickCollision : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*-35,0,sin(pitch)*35,CVF_RELATIVE)
TNT1 A 0 ThrustThingZ(0,30,1,1)
stop
Nope:
TNT1 A 0
stop
}
}

actor HiJumpKickCollision2 : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_PlaySoundEx("weapon/Rebound","SoundSlot5")
TNT1 A 0 DamageThing(100*(CallACS("StatValueRamp",STAT_PATK,1,400)*0.01))
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 1 A_GiveInventory("DashBreaker",1)
TNT1 A 0 A_Stop
stop
Nope:
TNT1 A 0
stop
}
}

actor HiJumpKickThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_JumpIfInventory("DashBreaker",1,"Nope")
TNT1 A 0 A_ChangeVelocity(cos(Angle)*HJK_Thrust,sin(Angle)*HJK_Thrust,sin(-(Pitch))*HJK_Thrust,CVF_REPLACE)//cos(-pitch)* , sin(-pitch)*HJK_Thrust
TNT1 A 1 A_FireCustomMissile("HiJumpKickBumper",0,0,0,15,0,0) //CallACS("GRAB_MEMANGLE",0)
BASB A 1 A_SpawnitemEx("HiJumpKickWave",cos(Angle)*30,0,(25),0,0,0,0,SXF_TRANSFERTRANSLATION) //+(sin(pitch)*30)
stop
Nope:
TNT1 A 0
stop
}
}

actor HiJumpKickDashFX : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIf(Z-FloorZ <= 200,"Nope")
TNT1 A 0 A_SpawnItemEX("GroundDustFX",-25,0,-(z-floorz),0)
stop
Nope:
TNT1 A 0
stop
}
}

actor HiJumpKickInitiate : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_Spawnitemex("HiJumpKickWatcher")
stop
}
}

actor HiJumpKickWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Death")
TNT1 A 0 A_SetUserVar("user_DashTime",user_DashTime+1)
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 3 A_GiveToTarget("HiJumpKickThrust")
TNT1 A 0 A_JumpIfInTargetInventory("HiJumpKickTimer",1,"Spawn")
//TNT1 A 0 A_Jumpif(user_DashTime <= 6,"Spawn")
Goto Death
Check2:
TNT1 A 0 A_JumpIfInTargetInventory("DashBreaker",1,"Spawn")
Goto Death
Death:
DeathTake:
TNT1 A 0 //A_TakeFromTarget("HiJumpKickActive",1)
stop
}
}


actor BodyBumper : BasicProjectile
{
-NOGRAVITY
+NOEXPLODEFLOOR
+STEPMISSILE
//+RIPPER
+SKYEXPLODE
maxstepheight 35
BounceFactor 0.9
Gravity 1.55
damage (0)
Radius 26
Height 55
speed 20
Scale 2.5
Args 20
ReactionTime 70
States
{
Spawn:
TNT1 A 0
TNT1 AAAAAAAAAAAAAA 1 A_Warp(AAPTR_TARGET,0,0,22,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
stop
Death:
TNT1 A 1 A_SpawnItemEx("SkyUppercutCeilingBonk",0,0,0,0,0,0,0,1)
AQBA C 0 A_GiveToTarget("BodyBumpCheck")
TNT1 A 7
stop
}
}

actor BodyBumpCheck : Inventory {inventory.amount 1 inventory.Maxamount 3 }
