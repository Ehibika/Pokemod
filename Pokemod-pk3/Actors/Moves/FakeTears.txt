actor FakeTearsMove : WepMasterBase
{
Tag "FakeTears"
obituary "$OB_FakeTears"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "FakeTearsPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("weapon/noisecrush2","Weapon")
"####" B 0 A_FireCustomMissile("FakeTearsShot",0,1,0,0)
"####" B 0 A_FireCustomMissile("FakeTearsShot",15,0,0,0)
"####" B 0 A_FireCustomMissile("FakeTearsShot",345,0,0,0)
"####" BBBBBBBBB 1 A_GiveInventory("FakeTearsFX_Spawn")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("FullRecovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor FakeTearsMove1 : FakeTearsMove {Weapon.SlotNumber 1}
actor FakeTearsMove2 : FakeTearsMove {Weapon.SlotNumber 2}
actor FakeTearsMove3 : FakeTearsMove {Weapon.SlotNumber 3}
actor FakeTearsMove4 : FakeTearsMove {Weapon.SlotNumber 4}

actor FakeTearsMove5 : FakeTearsMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("FakeTearsMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor FakeTearsPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

actor STF_FakeTears : Powerup {inventory.amount 1 Powerup.Duration -6}

actor FakeTearsNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("STF_FakeTears",1,"Sustain")
TNT1 A 0 A_GiveInventory("STF_FakeTears",1)
TNT1 A 0 A_SpawnItemEX("FakeTearsNerfWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_FakeTears",1)
stop
Nope:
TNT1 A 0
stop
}
}

Const int FakeTears_Item = 15;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor FakeTearsShot
{
PROJECTILE
+RIPPER
Radius 4
Height 4
scale 2.5
damage (0)
+FORCEXYBILLBOARD
speed 45
reactiontime 3
Args 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("FakeTearsWaveFX",0,0,0,0,0,0,0,16385)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SpawnItemEx("FakeTearsShot_A",0,0,0,MomX,Momy,Momz,0,17929)
Stop

Death:
TNT1 A 0 
stop
}
}

actor FakeTearsShot_A : FakeTearsShot
{
Args 0,0,0
Var Int User_Step;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("FakeTearsWaveFX",0,0,0,0,0,0,0,16385)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SetScale(ScaleX+0.5)
TNT1 A 0 A_SetUserVar("User_Step",Pitch % 3)
TNT1 A 0 A_Jumpif(User_Step == 0,"Spawn2")
TNT1 A 0 A_SpawnItemEx("FakeTearsShot_A",0,0,0,MomX,Momy,Momz,0,17929)
stop

Spawn2:
TNT1 A 0 A_SetArg(0,MomX)
TNT1 A 0 A_SetArg(1,MomY)
TNT1 A 0 A_SetArg(2,MomZ)
TNT1 A 0 A_Stop
TNT1 A 0 ACS_NamedExecuteAlways("PKMNACS_RadiusGive",0,FakeTears_Item,150,RG_ENEMY)
TNT1 A 3 
TNT1 A 0 A_SpawnItemEx("FakeTearsShot_B",0,0,0,Args[0],Args[1],Args[2],0,17929)
TNT1 A 2
stop
}
}

actor FakeTearsShot_B : FakeTearsShot
{
Args 0,0,0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("FakeTearsWaveFX",0,0,0,0,0,0,0,16385)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SetScale(ScaleX+0.5)
TNT1 A 0 A_Jumpif(Pitch >= reactiontime*3,"Death")
TNT1 A 0 A_SpawnItemEx("FakeTearsShot_A",0,0,0,MomX,Momy,Momz,0,17929)
stop

}
}

actor FakeTearsWaveFX : NoiseCrushTrail1
{
ReactionTime 5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TMPK ABCD 2 
stop
}
}


actor FakeTearsNerfWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_GiveToTarget("SdefDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_FakeTears",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}

actor FakeTearsFX_Spawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 AA 0 A_SpawnItemEX("FakeTearDrops",0,16,35,0,random(5,8),10)
TNT1 AA 0 A_SpawnItemEX("FakeTearDrops",0,-16,35,0,random(-8,-5),10)
Nope:
TNT1 A 0
stop
}
}

actor FakeTearDrops : BasicClientside
{
-NOGRAVITY
-NOINTERACTION
+THRUACTORS
-NOCLIP
+MISSILE
scale 1.5
Alpha 0.8
States
{
Spawn:
AQBA A 0
AQBA A 0 A_SetScale(Frandom(1.0,2.0))
Spawn2:
AQAQ QR 2
loop 
Death:
AQAQ STUV 1 
stop
}
}