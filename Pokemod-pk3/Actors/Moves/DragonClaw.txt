Const int DRAGONCLAW_SPACE = 35;

actor DragonClawMove : WepMasterBase
{
Tag "DragonClaw"
obituary "$OB_DRAGONCLAW"
Weapon.AmmoUse 1
Weapon.AmmoGive 35
weapon.ammotype "DragonClawPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop

Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_DragonClaw,1)
"####" B 0 A_CheckFloor("Fire.PitchCheck")
"####" B 0 A_Jump(256,"Fire.Claw")
Goto Fire.Claw

Fire.PitchCheck:
"####" B 0 A_Jumpif(Pitch < 55,"Fire.Claw2")
"####" B 0 A_Jump(256,"Fire.Claw")
Goto Fire.Claw


Fire.Claw:
TNT1 A 0 A_PlaySoundEx("weapon/FlameSword","Weapon")
TNT1 A 0 A_FireCustomMissile("DragonClawMelee",0,0,DRAGONCLAW_SPACE,0)
TNT1 A 0 A_FireCustomMissile("DragonClawMelee",0,0,0,0)
TNT1 A 5 A_FireCustomMissile("DragonClawMelee",0,0,-DRAGONCLAW_SPACE,0)

"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Claw2:
TNT1 A 0 A_PlaySoundEx("weapon/FlameSword","Weapon")
TNT1 A 0 A_FireCustomMissile("DragonClawMelee",0,0,DRAGONCLAW_SPACE,0,0,Pitch)
TNT1 A 0 A_FireCustomMissile("DragonClawMelee",0,0,0,0,0,Pitch)
TNT1 A 5 A_FireCustomMissile("DragonClawMelee",0,0,-DRAGONCLAW_SPACE,0,0,Pitch)

"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("PartRecovery_B")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor DragonClawMove1 : DragonClawMove {Weapon.SlotNumber 1}
actor DragonClawMove2 : DragonClawMove {Weapon.SlotNumber 2}
actor DragonClawMove3 : DragonClawMove {Weapon.SlotNumber 3}
actor DragonClawMove4 : DragonClawMove {Weapon.SlotNumber 4}

actor DragonClawMove5 : DragonClawMove 
{
Weapon.SlotNumber 5
States
{

Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("DragonClawMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor DragonClawPP : Ammo {inventory.amount 1 inventory.Maxamount 35}
Actor Imbue_DragonClaw : Powerup{inventory.amount 1 powerup.duration 0x7FFFFFFF}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor DragonClawMelee : BasicMeleeAttack
{
DamageType "DragonClaw"
Obituary "$OB_DRAGONCLAW"
scale 2.0
Speed 48
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA GGGG 1 A_SpawnItemEx("DragonClawTrail",-48,0,0,momx,momy,momz,0,9)
stop

Death:
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NDFX A 1 A_Explode(MBP_DragonClaw,80,0,0,80)
NDFX A 1 A_SpawnItemEx("DragonClawImpactFX",0,0,0,0,0,0,0,1)
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

actor DragonClawTrail : BasicExplosion
{
Obituary "$OB_DRAGONCLAW"
damagetype "DragonClaw"
Args 10
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("DragonClawParticleFX1",45+Args[0],random(-32,32),random(-32,32),0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("DragonClawParticleFX2",45+Args[0],random(-32,32),random(-32,32),0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("DragonClawTrailFX1",0,0,0,-momx*0.3,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("DragonClawTrailFX2",0,0,0,-momx*0.6,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("DragonClawTrailFX3",0,0,0,-momx*0.9,-momy*0.6,-momz*0.6,0,9)
stop
}
}

actor DragonClawTrailFX1 : BasicClientside
{
scale 2.0
alpha 0.8
renderstyle "Translucent"
speed 7
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA G 1 
stop
}
}

actor DragonClawTrailFX2 : DragonClawTrailFX1
{
alpha 0.6
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA F 1 
stop
}
}

actor DragonClawTrailFX3 : DragonClawTrailFX1
{
alpha 0.4
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA E 1 
stop
}
}


actor DragonClawParticleFX1 : BasicClientside
{
scale 2.5
ReactionTime 2
Renderstyle "Translucent"
Alpha 0.9
States
{
Spawn:
TNT1 A 0
TNT1 A 1
WABU DCBA 3 
stop
}
}

actor DragonClawParticleFX2 : BasicClientside
{
scale 3.5
ReactionTime 2
Renderstyle "Translucent"
Alpha 0.9
States
{
Spawn:
TNT1 A 0
TNT1 A 1
TBOL JL 3 
stop
}
}

actor DragonClawImpactFX : BasicClientside
{
scale 2.0
States
{
Spawn:
TNT1 A 0
//MCLA ABCDE 1 
MCLA E 5
MCLA EZEZEZ 2
stop
}
}

