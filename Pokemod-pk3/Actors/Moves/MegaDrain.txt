
actor MegaDrainMove : WepMasterBase
{
Tag "MegaDrain"
obituary "$OB_MegaDrain"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "MegaDrainPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
//TNT1 A 0 A_Jumpifinventory("MegaDrainStopper",1,"Fire.After2")
TNT1 A 0 A_Jumpifinventory("ConShotTimer",1,"Fire.Bullet2")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("support/Ubercharge2","Weapon")
"####" B 0 A_FireCustomMissile("MegaDrainHitbox",0,1,0,0,0,0)
"####" B 0 A_FireCustomMissile("MegaDrainSapFX",0,0,0,0,0,0)
"####" B 0 A_GiveInventory("MegaDrainCST",1)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.Bullet2:
"####" B 0 A_PlaySoundEx("support/Ubercharge2","Weapon")
"####" B 0 A_FireCustomMissile("MegaDrainHitbox",0,0,0,0,0,0)
"####" B 0 A_FireCustomMissile("MegaDrainSapFX",0,0,0,0,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("3Recovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_TakeInventory("ConShotTimer",99)
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
/*
Fire.After2:
"####" B 0 A_PlaySoundEx("support/Ubercharge1","SoundSlot6")
"####" B 1 A_GiveInventory("FullRecovery_A")
"####" B 0 A_TakeInventory("ConShotTimer",99)
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
*/
}
}

actor MegaDrainMove1 : MegaDrainMove {Weapon.SlotNumber 1}
actor MegaDrainMove2 : MegaDrainMove {Weapon.SlotNumber 2}
actor MegaDrainMove3 : MegaDrainMove {Weapon.SlotNumber 3}
actor MegaDrainMove4 : MegaDrainMove {Weapon.SlotNumber 4}

actor MegaDrainMove5 : MegaDrainMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("MegaDrainMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor MegaDrainPP : Ammo {inventory.amount 1 inventory.Maxamount 20}

Actor MegaDrainProtect : powerprotection
{
Powerup.duration 17
Damagefactor "MegaDrain", 0
}

actor MegaDrainStopper : Powerup {inventory.amount 1 Powerup.Duration 7}

actor MegaDrainCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 35
powerup.type "ConShotTimer"
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor MegaDrainSapFX : BasicProjectile
{
Speed 300
Damage (0)
DamageType "MegaDrain"
obituary "$OB_MegaDrain"
radius 16
Reactiontime 15
height 16
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1
TNT1 A 0 A_Stop
Goto Death

Death:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 AAA 0 A_SpawnItemEx ("MegaDrainParticleSpawner", random2(55), random2(55), random2(55), 0, 0, 0, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 4
stop
}
}

actor MegaDrainHitbox : BasicExplosion
{
Damage(0)
DamageType "MegaDrain"
obituary "$OB_MegaDrain"
Reactiontime 17
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
//==
TNT1 A 1 
TNT1 A 1 A_JumpIfInventory("MegaDrainProtect",1,"Death",AAPTR_TRACER)
TNT1 A 1 A_JumpIf(IsPointerEqual(AAPTR_TRACER,AAPTR_NULL) == False, "Drain")
Goto Death

Drain:
TNT1 A 1 A_Warp(AAPTR_TRACER,0,0,28,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
TNT1 A 0 A_Explode(MBP_MegaDrain,35,0,0,35)
NAPC A 0 A_PlaySoundEx("weapon/Noisecrush1", "Weapon")
//NAPC A 0 A_GiveToTarget("MegaDrainStopper", 1)
TNT1 A 3 A_SpawnItemEx("MegaDrainFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
Drain2:
TNT1 A 1 A_Warp(AAPTR_TRACER,0,0,28,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
TNT1 AA 0 A_SpawnItemEx ("MegaDrainParticleSpawner", random2(20), random2(20), random2(20), 0, 0, 0, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_CountDown
loop

Death:
TNT1 A 0 
stop
}
}


actor MegaDrainFX : BasicClientside
{
scale 4.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 //A_PlaySoundEX("Misc/CeilingBonk","SoundSlot8")
TNT1 A 0 //A_Quake(4,12,0,250)
HITG FGH 1
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
TNT1 A 6
stop
}
}


actor MegaDrainParticleSpawner : Basicprojectile
{
scale 1.2
+NOINTERACTION
+RIPPER
Damage(0)
speed 0
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CustomMissile("MegaDrainParticle",0,0,0,CMF_TRACKOWNER,0)
TNT1 A 0
stop
}
}

actor MegaDrainParticle : Basicprojectile
{
scale 1.2
+SeekerMissile
+NOINTERACTION
+RIPPER
Damage(0)
speed 45
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_TARGET)
Spawn1:
GRBU CDE 1 A_Seekermissile(90,90,SMF_PRECISE)
TNT1 A 0 A_JumpIFTracerCloser(250,"Spawn2")
loop
Spawn2:
GRBU MNO 1 A_Seekermissile(90,90,SMF_PRECISE)
TNT1 A 0 A_JumpIFTracerCloser(100,"Death")
loop
Death:
TNT1 A 0
stop
}
}