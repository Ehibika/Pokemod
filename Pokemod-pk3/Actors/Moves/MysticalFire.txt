actor MysticalFireMove : WepMasterBase
{
Tag "MysticalFire"
obituary "$OB_MYSTICALFIRE"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
weapon.ammotype "MysticalFirePP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("weapon/magmabazooka","Weapon")
"####" B 2 A_FireCustomMissile("MysticalFireShot",0,1,0,0)
"####" BBBBB 3 A_GiveInventory("ChannelingSpeed")
"####" B 0 A_Jump(256,"Fire.Holding")
Goto Fire.Holding

Fire.Holding:
//"####" B 0 A_Jumpif(CallACS("CheckWeaponFire",RC_A)!=1,"NoAmmo")
"####" C 0 A_JumpIfInventory("HoldBreak",1,"Fire.Release")
"####" B 1 A_GiveInventory("ChannelingSpeed")
"####" C 0 A_REfire("Fire.Holding")
"####" B 0 A_Jump(256,"Fire.Release")
Goto Fire.Release

Fire.Release:
"####" B 0 A_PlaySoundEx("weapon/FireStorm","Weapon")
"####" B 5 A_GiveInventory("MoveDetonator")
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_A")
"####" B 0 A_TakeInventory("HoldBreak",999)
"####" B 0 A_TakeInventory("ChannelingSpeed",9) 
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor MysticalFireMove1 : MysticalFireMove {Weapon.SlotNumber 1}
actor MysticalFireMove2 : MysticalFireMove {Weapon.SlotNumber 2}
actor MysticalFireMove3 : MysticalFireMove {Weapon.SlotNumber 3}
actor MysticalFireMove4 : MysticalFireMove {Weapon.SlotNumber 4}

actor MysticalFireMove5 : MysticalFireMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("MysticalFireMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor MysticalFirePP : Ammo {inventory.amount 1 inventory.Maxamount 20}

actor MoveDetonator : PowerUp {inventory.amount 1 Powerup.Duration 17}

actor HoldBreak : PowerUp {inventory.amount 1 Powerup.Duration 17}


Const int MysticalFireRad = 160;
Const int MysticalFireRad2 = MysticalFireRad/2;



actor STF_MysticalFire : BaseSTatusFlag {}

actor MysticalFireNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpif(CallACS("CheckStatusGuard",0,STATUS_STATS),"Nope") //Blocked by Protect|Detect|Substitute|Mist
TNT1 A 0 A_Jumpifinventory("STF_MysticalFire",1,"Sustain")
TNT1 A 0 A_PlaySoundEx("support/StatDown","Weapon")
TNT1 A 0 A_GiveInventory("STF_MysticalFire",1)
TNT1 A 0 A_SpawnItemEX("MysticalFireNerfWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_MysticalFire",1)
stop
Nope:
TNT1 A 0
stop
}
}

actor MysticalFireNerfProc : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jump(127,"Nerf")
stop
Nerf:
TNT1 A 0 A_GiveInventory("MysticalFireNerfInitiate")
stop

Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor MysticalFireShot : BasicProjectile
{
Speed 32
+SKYEXPLODE
Damage (MBP_MysticalFire1)
DamageType "MysticalFire"
obituary "$OB_MysticalFire"
radius 5
Reactiontime 10
height 16
scale 3.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 //A_SpawnItemEx("MysticalFireFX",0,0,0,0,0,0,0,1)
Spawn2:
TNT1 A 0 
MFIR AB 1 A_SpawnItemEx("ConfuseRayParticle2",0,0,0,0,0,0,0,1)
MFIR A 0 A_PlaySoundEx("weapon/WaveBurner", "Weapon")
MFIR A 0 ACS_ExecuteAlways(259, 0, 1)
MFIR A 0 A_ChangeVelocity(cos(-pitch)*32, 0, sin(-pitch)*32, CVF_RELATIVE|CVF_REPLACE)
MFIR A 0 A_JumpIfInTargetInventory("MoveDetonator",1,"Death")

MFIR CD 1 A_SpawnItemEx("ConfuseRayParticle2",0,0,0,0,0,0,0,1)
MFIR B 0 A_PlaySoundEx("weapon/WaveBurner", "Weapon")
MFIR B 0 ACS_ExecuteAlways(259, 0, 1)
MFIR B 0 A_ChangeVelocity(cos(-pitch)*32, 0, sin(-pitch)*32, CVF_RELATIVE|CVF_REPLACE)
MFIR B 0 A_JumpIfInTargetInventory("MoveDetonator",1,"Death")

MFIR EF 1 A_SpawnItemEx("ConfuseRayParticle2",0,0,0,0,0,0,0,1)
MFIR C 0 A_PlaySoundEx("weapon/WaveBurner", "Weapon")
MFIR C 0 ACS_ExecuteAlways(259, 0, 1)
MFIR C 0 A_ChangeVelocity(cos(-pitch)*32, 0, sin(-pitch)*32, CVF_RELATIVE|CVF_REPLACE)
MFIR C 0 A_JumpIfInTargetInventory("MoveDetonator",1,"Death")
loop

Death:
BSTG A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_GiveToTarget("HoldBreak",1)
BSTG A 0 A_SpawnItemEx("MysticalFireImpact", 0, 0, 0, 0, 0, 0, 0,1)
HARD BCDE 2
stop
}
}

actor MysticalFireImpact : BasicExplosion2
{
scale 5.5
DamageType "MysticalFire"
obituary "$OB_MysticalFire"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_Explode (MBP_MysticalFire2,MysticalFireRad,0,0,MysticalFireRad2)
FWVE W 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
TNT1 A 0 A_SpawnItemEx("MysticalFireImpactFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 2
stop
}
}

actor MysticalFireImpactFX : BasicClientside
{
translation "192:192=218:218", "198:198=221:221"
scale 3.0
Alpha 1.0
States
{
Spawn:
AQBA A 0
TNT1 A 0 A_PlaySoundEx("weapon/napalm","Weapon")
ASEX AABBCDEF 1 A_SpawnItemEx("MysticalFireParticle",random2(MysticalFireRad),random2(MysticalFireRad),random(-MysticalFireRad,MysticalFireRad),0,0,0,0,1)
stop
}
}

actor MysticalFireParticle : BasicClientside  
{
RenderStyle "Normal"
States
{
Spawn:
BRNF A 0 A_SetScale(Frandom(1.5,4.5))
MFIR GHIJKLM 1 
stop
}
}


actor MysticalFireNerfWatcher : BasicHelper 
{
Var int user_DashTime;
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_GiveToTarget("SAtkDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_MysticalFire",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}
