actor RockSlideMove : WepMasterBase
{
Tag "RockSlide"
Weapon.AmmoUse 1
Weapon.AmmoGive 5
weapon.ammotype "RockSlidePP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0 
"####" B 1 A_JumpifInventory("RockSlideBuffer",1,"NoAmmo2")
"####" B 0 A_Jump(256,"Fire.Aim")
Goto Fire.Aim


Fire.Aim:
"####" B 1 A_FireCustomMissile("RockSlideRingSpawner",0,0,0,Aim_OverHead)
"####" C 0 A_REfire("Fire.Aim")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_FireCustomMissile("RockSlideSpawner",0,1,0,Aim_OverHead)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("FullRecovery_A")
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End





}
}

actor RockSlideMove1 : RockSlideMove {Weapon.SlotNumber 1}
actor RockSlideMove2 : RockSlideMove {Weapon.SlotNumber 2}
actor RockSlideMove3 : RockSlideMove {Weapon.SlotNumber 3}
actor RockSlideMove4 : RockSlideMove {Weapon.SlotNumber 4}

actor RockSlideMove5 : RockSlideMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("RockSlideMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor RockSlidePP : Ammo {inventory.amount 1 inventory.Maxamount 5}

actor RockSlideProtect : PowerProtection
{
Powerup.Duration 5
damagefactor "RockSlide", 0.0
}

actor RockSlideProtect2 : PowerProtection
{
Powerup.Duration 17
damagefactor "RockSlide", 0.25
}

actor RockSlideBuffer : Powerup
{
Powerup.Duration 17
}

Const int RockSldRad = 200;
Const int RockSldRad2 = 32;
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor RockSlideRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("RockSlideGuideDot", RockSldRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("RockSlideGuideDot", -RockSldRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor RockSlideRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 300
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("RockSlideRingGuide",0,0,0,0,0,0,0,1)
stop
}
}

actor RockSlideGuideDot : BasicGuideDot {}


actor RockSlideRock : BasicProjectile
{
+RIPPER
+NOINTERACTION
+DONTBLAST
Damagetype "RockSlide"
Obituary "$OB_ROCKSLIDE"
damage (0)
height 8
radius 16
Scale 2.5
Reactiontime 10
States
{
Spawn:
BROK B 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
BROK B 0 A_SetScale(FRandom(1.5,3.0))
BROK B 0 
BROK A 1 A_JumpIf(ceilingz-z>8, "Spawn2")
BROK B 0 A_JumpIf(momz==0,"Death")
BROK B 0 A_CountDown
Goto Spawn+2
Spawn2:
BROK B 0 A_ChangeFlag("RIPPER", 0)
BROK B 0 A_ChangeFlag("NOINTERACTION",0)
BROK A 3 A_SpawnItemEx("RockSlideFx2", 0, 0, 0, 0, 0, 0, 0, 1)
BROK B 0 A_JumpIf(momz==0,"Death")
BROK B 0 A_CountDown
Goto Spawn2+2
Death:
ASEX A 0 A_playSoundEx("Misc/HardDrop","Weapon")
ASEX A 0 A_ChangeFlag(NOGRAVITY,1)
ASEX A 0 A_Explode(MBP_RockSlide,RockSldRad2,0,0,RockSldRad2)
ASEX A 0 A_SpawnItemEx("ThrashFX2", 0, 0, 25, 0, 0, 0, 0, 1)
TNT1 A 2
stop
}
}



actor RockSlideWarning : RockSlideRock
{
Scale 1.5
States
{
Spawn:
BROK B 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
POWS A 1 A_JumpIf(ceilingz-z>8, "Spawn2")
BROK B 0 A_JumpIf(momz==0,"Death")
BROK B 0 A_CountDown
Goto Spawn
Spawn2:
BROK B 0 A_ChangeFlag("RIPPER", 0)
BROK B 0 A_ChangeFlag("NOINTERACTION",0)
POWS A 3  
BROK B 0 A_JumpIf(momz==0,"Death")
BROK B 0 A_CountDown
Goto Spawn2+2
Death:
TNT1 A 0 A_SpawnItemEx("RockSlideWarningFX",0,0,0,2,0,10,random(1,360),1)
TNT1 A 5
stop
}
}


actor RockSlideWarningFX : BasicClientside
{
scale 1.5
-NOINTERACTION
-NOGRAVITY
RenderStyle "Translucent"
Alpha 0.6
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
POWS A 7 
POWS AZAZAZA 2
stop
}
}

actor RockSlideSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 300
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 2
TNT1 A 0 A_SpawnItemEx("RockSlideField",0,0,-(z-floorz)+5,0,0,0,0,1)
TNT1 A 1
stop
}
}


actor RockSlideField : BasicExplosion
{
ReactionTime 52
DamageType "RockSlide"
obituary "$OB_RockSlide"
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn.Warning:
TNT1 A 0 A_Quake(2,ReactionTime,0,RockSldRad+RockSldRad2)
TNT1 A 0
TNT1 A 1 A_GiveToTarget("RockSlideBuffer")
TNT1 AAAA 0 A_SpawnItemEx("RockSlideFx1", RockSldRad+random(-30,10), 0, -(z-floorz)+24, 5, 0, 0, random(1,360), 1)
TNT1 AA 0 A_SpawnItemEx("RockSlideWarning",random(RockSldRad2,RockSldRad-RockSldRad2),0,320,0,0,-35,random(1,360),1)
TNT1 A 0 A_CountDown 
TNT1 A 0 A_JumpIf(ReactionTime < 36,"Spawn.RockFall")
Goto Spawn.Warning+2

Spawn.RockFall:
TNT1 A 0 A_Quake(6,ReactionTime,0,RockSldRad+RockSldRad2)
TNT1 A 0
TNT1 A 1 A_GiveToTarget("RockSlideBuffer")
TNT1 AAAA 0 A_SpawnItemEx("RockSlideFx1", RockSldRad+random(-30,10), 0, -(z-floorz)+24, 5, 0, 0, random(1,360), 1)
TNT1 AAAA 0 A_SpawnItemEx("RockSlideRock",random(RockSldRad2,RockSldRad-RockSldRad2),0,320,0,0,-35,random(1,360),1)
TNT1 A 0 A_SpawnItemEx("RockSlideWarning",random(RockSldRad2,RockSldRad-RockSldRad2),0,320,0,0,-35,random(1,360),1)
TNT1 A 0 A_CountDown 
Goto Spawn.RockFall+2 

Death:
TNT1 A 1 
stop
}
}


actor RockSlideFx1 : BasicClientside
{
scale 3.5
RenderStyle "Translucent"
Alpha 0.6
States
{
Spawn:
TNT1 A 0 
MUDB MMNOP random(3,4) A_ScaleVelocity(0.6)
stop
}
}

actor RockSlideFx2 : BasicClientside
{
scale 1.5
RenderStyle "Translucent"
Alpha 0.3
States
{
Spawn:
TNT1 A 0 
MUDB MMNOP 2 
stop
}
}