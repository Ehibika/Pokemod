Const int CLOSECOMBAT_SPD = 55;
Const int CLOSECOMBAT_SPD2 = -20;
Const int CLOSECOMBAT_SPD3 = 55;

actor CloseCombatMove : WepMasterBase
{
Tag "CloseCombat"
obituary "$OB_CloseCombat"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "CloseCombatPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" A 0 ACS_NamedExecuteAlways("TakePP",0,Move_CloseCombat,1)
"####" B 5 A_PlaySoundEx("Misc/UseMove","Weapon")
"####" B 0 A_GiveInventory("CloseCombatTimer",1)
"####" B 0 A_GiveInventory("CloseCombatNerfInitiate")
"####" B 0 A_Jump(256,"Fire.Rampage")
Goto Fire.Rampage

Fire.Rampage:
"####" B 2 A_PlaySoundEx("misc/darkleap","Weapon")
"####" BBB 0 A_FireCustomMissile("CloseCombatMelee",0,0,random(-CLOSECOMBAT_SPD,CLOSECOMBAT_SPD),random(CLOSECOMBAT_SPD2,CLOSECOMBAT_SPD3),0,0)
"####" B 1 A_Jumpifinventory("CloseCombatTimer",1,"Fire.RampageCheck")
"####" B 1 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.RampageCheck:
"####" B 0 A_Jumpifinventory("SleepMark",1,"Fire.After")
"####" B 0 A_Jumpifinventory("FreezeMark",1,"Fire.After")
"####" B 0 A_Jumpifinventory("CringeMark",1,"Fire.After")

"####" B 1 A_Jump(256,"Fire.Rampage")
Goto Fire.Rampage

Fire.After:
"####" B 1 A_GiveInventory("SemiRecovery_B")
"####" B 0 A_TakeInventory("CloseCombatTimer",99)
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor CloseCombatMove1 : CloseCombatMove {Weapon.SlotNumber 1}
actor CloseCombatMove2 : CloseCombatMove {Weapon.SlotNumber 2}
actor CloseCombatMove3 : CloseCombatMove {Weapon.SlotNumber 3}
actor CloseCombatMove4 : CloseCombatMove {Weapon.SlotNumber 4}

actor CloseCombatMove5 : CloseCombatMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("CloseCombatMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor CloseCombatPP : Ammo {inventory.amount 1 inventory.Maxamount 10}
actor CloseCombatTimer : Powerup {inventory.amount 1 powerup.duration 25 }

Actor CloseCombatProtect : powerprotection
{
Powerup.duration 1
Damagefactor "CloseCombat", 0
}

actor STF_CloseCombat : Powerup {inventory.amount 1 Powerup.Duration -3}

actor CloseCombatNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
//Self inflicted, not blocked by mist
TNT1 A 0 A_Jumpifinventory("STF_CloseCombat",1,"Sustain")
TNT1 A 0 A_GiveInventory("STF_CloseCombat",1)
TNT1 A 0 A_SpawnItemEX("CloseCombatNerfWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_CloseCombat",1)
stop
Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor CloseCombatMelee : BasicMeleeAttack
{
Speed 80
Scale 5.0
Radius 8
Height 8
DamageType "CloseCombat"
Obituary "$OB_CloseCombat"
Damage(MBP_CloseCombat)
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 
TNT1 AAA 1 A_SpawnItemEx("CloseCombatFXTrail", 30, 0, 0, 0, 0, 0, 0,1)
Goto Death

Crash:
XDeath:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Stop
TNT1 A 0 A_SpawnItemEx("CloseCombatFX1", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_SpawnItemEx("CloseCombatFX2", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_SpawnItemEx("CloseCombatSFX_Sounder", 0, 0, 0, 0, 0, 0, 0,1)
Death:
TNT1 A 0 A_Stop
TNT1 A 0
stop
}
}


actor CloseCombatSFX_Sounder : BasicClientside
{
scale 1.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("misc/hitconfirm","soundslot5")
TNT1 A 17
stop
}
}

actor CloseCombatFXTrail : BasicClientside
{
scale 3.5
Renderstyle Translucent
alpha 1.0
reactiontime 5
States
{
Spawn:
TNT1 A 0 
HITG B 1
HITG B 0 A_Fadeout(0.3)
HITG B 0 A_SetScale(Scalex-0.2)
loop
}
}



actor CloseCombatFX1 : BasicClientside
{
scale 5.5
States
{
Spawn:
TNT1 A 0 
HITG A 2
HITG B 2
"####" "####" 1 A_Fadeout(0.3)
stop
}
}

actor CloseCombatFX2 : BasicClientside
{
scale 5.5
States
{
Spawn:
TNT1 A 0 
HITG FGH 1
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
HITG H 1 A_SetScale(ScaleX+0.5)
HITG H 0 A_Fadeout(0.3)
stop
}
}

actor CloseCombatNerfWatcher : BasicHelper 
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveToTarget("SDefDownFXSpawn")
TNT1 A 5 A_GiveToTarget("PDefDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_CloseCombat",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}