actor SuperPowerMove : WepMasterBase
{
Tag "SuperPower"
obituary "$OB_SUPERPOWER"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "SuperPowerPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop

Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.PreCharge")
Goto Fire.PreCharge


Fire.PreCharge:
"####" B 0 A_FireCustomMissile("SuperPowerChargeWatcher",0,0)
"####" B 0 A_PlaySoundEx("move/Curse1","Soundslot6")
"####" B 0 A_Jump(256,"Fire.Charge1")
Goto Fire.Charge1

Fire.Charge1:
"####" B 0 A_JumpIfInventory("WeaponCharge",35,"Fire.Charge2")
"####" B 0 A_GiveInventory("WeaponCharge",1)
"####" B 0 A_GiveInventory("ChannelingSpeed")
"####" B 1
"####" C 0 A_REfire("Fire.Charge1")
"####" B 0 A_StopSoundEX("Soundslot6")
"####" B 0 A_Jump(256,"Fire.After2")
Goto Fire.After2

Fire.Charge2:
"####" B 0 A_GiveInventory("ChannelingSpeed")
"####" B 1
"####" C 0 A_REfire("Fire.Charge1")
"####" B 0 A_Jump(256,"Fire.GrabStart")
Goto Fire.GrabStart

Fire.GrabStart:
JARM F 0 A_TakeInventory("SuperPowerPP",1)
JARM F 0 A_GiveInventory("SuperPowerGrabTimer",1)
TNT1 A 0 A_TakeInventory("SuplexID",999)
TNT1 A 0 A_TakeInventory("WeaponCharge",999)
//SAKH C 0 ThrustThingZ(0,45,0,1)
TNT1 A 0 A_PlaySound("weapon/BreakDash")
"####" A 0 A_GiveInventory("DashSet_B")
SuperPowerWalk:
JARM F 0 A_GiveInventory("SuperPowerWalkThrust",1)
JARM F 1 A_GiveInventory("SuperPowerGrabFunc",1)
JARM F 0 A_JumpIfInventory("SuplexID",1,"SuperPower")
JARM F 0 A_JumpIfInventory("SuperPowerGrabTimer",1,"SuperPowerWalk")
JARM F 0 A_Stop
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

SuperPower:
TNT1 A 0 A_PlaySound("weapon/crashland")
TNT1 A 0 A_GiveInventory("DashSetOff")
TNT1 A 0 A_GiveInventory("DashSet_A")
TNT1 A 0 A_TakeInventory("Fly_Active",255)
TNT1 A 0 A_TakeInventory("ZangiefAngle",255)
TNT1 A 0 A_GiveInventory("ZangiefAngle",CallACS("GetAngleToTid",CallACS("Get_SuplexID_TID")))
TNT1 A 0 A_SetAngle(CallACS("GetAngleToTid",CallACS("Get_SuplexID_TID"))*360/256)
TOPH A 0 A_ChangeFlag("SHOOTABLE",0)
TNT1 A 0 A_ChangeFlag("SOLID",0)
TNT1 A 0 A_Stop

TNT1 AAAAAAAAAAAAAAAAA 1 A_Jumpif(!CallACS("SuplexGrasp_TID",pitch,angle,CallACS("Get_SuplexID_TID")),"SuperPowerSlam")
goto SuperPowerSlamJump

SuperPowerSlamJump:
TNT1 A 0 
TNT1 A 1 A_Jump(256, "SuperPowerSlam")
wait

SuperPowerSlam:
TOPH A 0 A_ChangeFlag("SHOOTABLE",1)
TOPH A 0 A_ChangeFlag("INVULNERABLE",1)
TNT1 A 0 ACS_NamedExecuteWithResult("Give_AntiInvincible_ToTID",CallACS("Get_SuplexID_TID"))
TNT1 A 0 A_SpawnItemEX("SuperPower", 0,0,0, 0,0,0, 0, SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION, 0)
TNT1 A 1 A_Jumpif(!CallACS("SuplexGrasp_TID",pitch,angle,CallACS("Get_SuplexID_TID")),"SuperPowerSlam")
goto SuperPowerSlamEnd

SuperPowerSlamEnd:
TOPH A 0 A_ChangeFlag("INVULNERABLE",0)
TNT1 A 0 A_GiveInventory("DashSetOff")
TNT1 A 0 A_SpawnItemEx("ReSolidifier") // here it is
//TOPH A 0 A_ChangeFlag("SOLID",1) //More elegant solution later
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 0 A_GiveInventory("HalfRecovery_B")
"####" B 0 A_GiveInventory("SuperPowerNerfInitiate")
Fire.After2:
"####" B 1 A_GiveInventory("5Recovery_B")
"####" A 0 A_GiveInventory("DashSetOff")
"####" B 0 A_TakeInventory("WeaponCharge",999)
"####" C 0 A_REfire("Fire")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor SuperPowerMove1 : SuperPowerMove {Weapon.SlotNumber 1}
actor SuperPowerMove2 : SuperPowerMove {Weapon.SlotNumber 2}
actor SuperPowerMove3 : SuperPowerMove {Weapon.SlotNumber 3}
actor SuperPowerMove4 : SuperPowerMove {Weapon.SlotNumber 4}

actor SuperPowerMove5 : SuperPowerMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("SuperPowerMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor SuperPowerPP : Ammo {inventory.amount 1 inventory.Maxamount 10}
actor SuperPowerGrabTimer : Powerup {inventory.amount 1 powerup.duration 8}

Const int SuperPowerRad = 120;
Const int SuperPower_Item = 14;

actor SuperPowerWalkThrust : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("CutterFlag",999,"Nope")
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*64,0,sin(-pitch)*64,CVF_RELATIVE|CVF_REPLACE)
stop
Nope:
TNT1 A 0
stop
}
}


actor SuperPowerGrabFunc : CustomInventory
{
states
{
pickup:
TNT1 A 0 A_JumpIf(pitch<350 && pitch>=270, "UpLimit")
goto Pickup2
UpLimit:
TNT1 A 0 A_SetPitch(350)
Pickup2:
TNT1 A 0 A_FireCustomMissile("SuperPowerGrab",0,0,0,0)
stop
}
}

actor STF_SuperPower : Powerup {inventory.amount 1 Powerup.Duration -5}

actor SuperPowerNerfInitiate : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
//Self inflicted, not blocked by mist
TNT1 A 0 A_Jumpifinventory("STF_SuperPower",1,"Sustain")
TNT1 A 0 A_GiveInventory("STF_SuperPower",1)
TNT1 A 0 A_SpawnItemEX("SuperPowerNerfWatcher")
TNT1 A 0 A_GiveInventory("StatUpdator",1)
stop

Sustain:
TNT1 A 0 A_GiveInventory("STF_SuperPower",1)
stop
Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor SuperPowerGrab
{
PROJECTILE
Radius 20
Height 20
scale 2.5
Obituary "$OB_SUPERPOWER"
speed 55
damage (MBP_SuperPower1)
reactiontime 1
damagetype "SuperPower1"
States
{
Spawn:
MMFX C 0
MMFX C 1 A_CountDown
goto Spawn+1
Death:
MMFX C 0 A_Explode(MBP_SuperPower1,40,0)
goto XDeath
XDeath:
MMFX C 0 A_Stop
MMFX CBCD 2
stop
}
}


actor SuperPower
{
PROJECTILE
scale 5.0
Obituary "$OB_SUPERPOWER"
damagetype "SuperPower2"
Speed 0
+DONTBLAST
height 0
radius 0

+NOINTERACTION
+NOCLIP
+NOTARGETSWITCH
States
{
Spawn:
TNT1 A 0
TNT1 A 0 Thing_Move(0,CallACS("Get_Target_SuplexID_TID"),1)
TNT1 A 0 A_Explode(MBP_SuperPower2, 300, 0,0,300)
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
goto Spawn2
Spawn2:
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
TNT1 A 5
NAPA EFGHIJKLMNOPQRS 1
stop
}
}

actor SuperPowerSuplexStart : CustomInventory
{
states
{
pickup:
TNT1 A 0 A_SpawnItemEx("SuperPowerSuplexHit")
stop
}
}

actor SuperPowerHitBase
{
Height 0
Radius 0
+NOINTERACTION
-SOLID
+NOGRAVITY
+MISSILE
}

actor SuperPowerSuplexHit : SuperPowerHitBase
{
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TRACER) //target's target to self's tracer
TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_DEFAULT,AAPTR_TARGET) //target <-> tracer
TNT1 A 0 A_JumpIfInTargetInventory("SuplexID",1,"SpawnNo")
TNT1 A 0 A_GiveToTarget("SuplexID",CallACS("core_gettracer",0)-1000+1)
TNT1 A 1
stop
SpawnNo:
TNT1 A 1
stop
}
}

actor SuperPowerPushStart : CustomInventory
{
states
{
pickup:
TNT1 A 0 A_SpawnItemEx("SuperPowerPusher")
stop
}
}

actor SuperPowerPusher : SuperPowerHitBase
{
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TRACER) //target's target to self's tracer
TNT1 A 0 A_FaceTracer
TNT1 A 0 A_SetPitch(CallACS("PKMNACS_TracerPitchGrab"))
TNT1 AA 1 //A_GiveToTarget("TempPitProtect",1)
TNT1 A 0 ThrustThing(angle*256/360,-160,1,ACS_ExecuteWithResult(257,0))
TNT1 A 0 ThrustThingZ(ACS_ExecuteWithResult(257,0),160*Sin(-Pitch),0,1)
stop
}
}

actor SuperPowerChargeWatcher
{
    PROJECTILE
    +NOINTERACTION
    renderstyle "ADD"
    speed 32
    height 1
    radius 1
    Scale 2.5
    ReactionTime 17
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
    Spawn2:
        //TNT1 A 0 A_SpawnItemEx("GrassWhistleParticle", 0, 0, 0, random(5,10), 0, random2(4), random(0, 359),1)
        PHAS KKLL 1 A_Warp(AAPTR_TARGET, 16,0,32, 0, 0)
        TCLA A 0 A_JumpIfInTargetInventory("WeaponCharge",35,"Spawn3")
        TCLA A 0 A_JumpIfInTargetInventory("WeaponCharge",1,"Spawn2")
        stop
    Spawn3:
        //TNT1 A 0 A_SpawnItemEx("FireBlastSymbolParticle", 0, 0, 0, random(2,4), 0, random2(4), random(0, 359),1)
        FX10 BBCCDD 1 A_Warp(AAPTR_TARGET, 16,0,32, 0, 0)
        TCLA A 0 A_JumpIfInTargetInventory("WeaponCharge",1,"Spawn2")
        stop
    Death:
        TNT1 A 0 
        stop
    }
}


actor SuperPowerNerfWatcher : BasicHelper 
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveToTarget("PatkDownFXSpawn")
TNT1 A 5 A_GiveToTarget("PDefDownFXSpawn")
TNT1 A 0 A_JumpIfInTargetInventory("STF_SuperPower",1,"Spawn")
TNT1 A 1 A_GiveToTarget("StatUpdator")
TNT1 A 5
Goto Death

Death:
TNT1 A 0 
stop
}
}