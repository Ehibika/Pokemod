actor DynamicPunchMove : WepMasterBase
{
Tag "DynamicPunch"
obituary "$OB_DYNAMICPUNCH"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
weapon.ammotype "DynamicPunchPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop


Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_A)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.PreCharge")
Goto Fire.PreCharge

Fire.PreCharge:
"####" B 0 A_FireCustomMissile("DynamicPunchWatcher",0,0)
"####" B 0 A_Jump(256,"Fire.Charge1")
Goto Fire.Charge1

Fire.Charge1:
"####" B 0 A_Jumpif(CallACS("CheckWeaponFire",RC_A)!=1,"NoAmmo")
"####" B 0 A_JumpIfInventory("WeaponCharge",10,"Fire.Charge2")
"####" B 0 A_GiveInventory("WeaponCharge",1)
"####" B 0 A_GiveInventory("ChannelingSpeed")  //Firing Slow
"####" B 0 A_PlaySoundEx("Weapon/Heat1","Soundslot6")
"####" B 1
"####" C 0 A_REfire("Fire.Charge1")
"####" B 0 A_Jump(256,"Fire.After2")
Goto Fire.After2

Fire.Charge2:
"####" B 0 A_GiveInventory("ChannelingSpeed")  //Firing Slow
"####" B 1
"####" C 0 A_REfire("Fire.Charge1")
"####" B 0 A_Jump(256,"Fire.Bullet")
Goto Fire.Bullet

Fire.Bullet:
"####" B 0 A_PlaySoundEx("weapon/ChargeKick","Weapon")
"####" B 0 A_FireCustomMissile("DynamicPunchMelee",0,1,0,0)
"####" B 0 A_Jump(256,"Fire.After")
Goto Fire.After

Fire.After:
"####" B 0 A_GiveInventory("HalfRecovery_A")
Fire.After2:
"####" B 1 A_GiveInventory("5Recovery_A")
"####" B 0 A_TakeInventory("WeaponCharge",999)
"####" B 0 A_TakeInventory("ChannelingSpeed",9) 
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor DynamicPunchMove1 : DynamicPunchMove {Weapon.SlotNumber 1}
actor DynamicPunchMove2 : DynamicPunchMove {Weapon.SlotNumber 2}
actor DynamicPunchMove3 : DynamicPunchMove {Weapon.SlotNumber 3}
actor DynamicPunchMove4 : DynamicPunchMove {Weapon.SlotNumber 4}

actor DynamicPunchMove5 : DynamicPunchMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("DynamicPunchMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}

//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor DynamicPunchPP : Ammo {inventory.amount 1 inventory.Maxamount 10}

Const int DynamicPunchRad = 100;
Const int DynamicPunchRad2 = DynamicPunchRad/2;

actor DynamicPunchConfusionChance : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jump(255,"Cringe")
stop
Cringe:
TNT1 A 0 A_GiveInventory("ConfusionStatusProc")
stop

Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor DynamicPunchWatcher
{
    PROJECTILE
    +NOINTERACTION
    +CLIENTSIDEONLY
    renderstyle "ADD"
    speed 32
    height 1
    radius 1
    Scale 3.5
    ReactionTime 17
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
    Spawn2:
        //TNT1 A 0 A_SpawnItemEx("GrassWhistleParticle", 0, 0, 0, random(5,10), 0, random2(4), random(0, 359),1)
        TNT1 A 0 A_SetPitch(CallACS("PitchGrab"))
        HEAT CCDDBB 1 A_Warp(AAPTR_TARGET, cos(-pitch)*16,0,32+(sin(-pitch)*16), 0, 0)
        TCLA A 0 A_JumpIfInTargetInventory("WeaponCharge",10,"Spawn3")
        TCLA A 0 A_JumpIfInTargetInventory("WeaponCharge",1,"Spawn2")
        stop
    Spawn3:
        TNT1 A 0 A_SetPitch(CallACS("PitchGrab"))
        TNT1 AAA 0 A_SpawnItemEx("BlazeKickFXTrail", 0, 0, 0, random(6,8), 0, random2(4), random(0, 359),1)
        HEAT EEFF 1 A_Warp(AAPTR_TARGET, cos(-pitch)*16,0,32+(sin(-pitch)*16), 0, 0)
        TCLA A 0 A_JumpIfInTargetInventory("WeaponCharge",1,"Spawn2")
        stop
    Death:
        TNT1 A 0 
        stop
    }
}


actor DynamicPunchMelee : BasicMeleeAttack
{
Speed 80
Scale 4.0
DamageType "DynamicPunch"
Obituary "$OB_DynamicPunch"
Damage(MBP_DynamicPunch1)
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 
HITG KKKKK 1 A_SpawnItemEx("DynamicPunchFXTrail", 0, 0, 0, 0, 0, 0, 0,1)
stop

Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Explode(MBP_DynamicPunch2,DynamicPunchRad,0,0,DynamicPunchRad2)
TNT1 A 0 A_SpawnItemEx("DynamicPunchFX1", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_SpawnItemEx("DynamicPunchFX2", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_PlaySoundEx("Misc/HardDrop", "SoundSlot6",0)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
TNT1 AAAAAA 2 A_SpawnItemEx("EggBombExplosionFX",random2(DynamicPunchRad),random2(DynamicPunchRad),random(0,DynamicPunchRad2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 5
stop
}
}


actor DynamicPunchFXTrail : BasicClientside
{
Scale 4.0
States
{
Spawn:
TNT1 A 0 
HITG K 1
HITG KKK 1 A_Fadeout(0.3)
stop
}
}


actor DynamicPunchFX1 : BasicClientside
{
scale 4.0
States
{
Spawn:
TNT1 A 0 
HITG A 5
HITG AAAA 1 A_Fadeout(0.3)
stop
}
}

actor DynamicPunchFX2 : BasicClientside
{
scale 4.0
States
{
Spawn:
TNT1 A 0 
BGHF ABC 1
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
stop
}
}