actor BrickBreakMove : WepMasterBase
{
Tag "BrickBreak"
obituary "$OB_BrickBreak"
Weapon.AmmoUse 1
Weapon.AmmoGive 35
weapon.ammotype "BrickBreakPP"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
States
{
Spawn:
WEAP X 1
stop

Fire:
"####" BBB 1 A_Jumpif(CallACS("CheckWeaponFire",RC_B)==1,"Fire.Begin")
"####" B 0 A_Jump(256,"NoAmmo")
Goto NoAmmo

Fire.Begin:
"####" B 0
"####" B 0 A_Jump(256,"Fire.Melee")
Goto Fire.Melee

Fire.Melee:
TNT1 A 0 A_PlaySoundEx("weapon/ChargeKick","Weapon")
TNT1 A 0 A_FireCustomMissile("BrickBreakMelee",0,1,0,0)
Goto Fire.After

Fire.After:
"####" B 1 A_GiveInventory("HalfRecovery_B")
"####" B 0 A_Jump(256,"Fire.End")
Goto Fire.End
}
}

actor BrickBreakMove1 : BrickBreakMove {Weapon.SlotNumber 1}
actor BrickBreakMove2 : BrickBreakMove {Weapon.SlotNumber 2}
actor BrickBreakMove3 : BrickBreakMove {Weapon.SlotNumber 3}
actor BrickBreakMove4 : BrickBreakMove {Weapon.SlotNumber 4}

actor BrickBreakMove5 : BrickBreakMove 
{
Weapon.SlotNumber 5
States
{
Fire.End:
TNT1 A 0
TNT1 A 0 A_TakeInventory("BrickBreakMove5",9)
TNT1 A 0 A_Jump(256,"Ready")
Goto Ready
}
}


//[+]========================================================================[+]
//-----------------------------|Ammo & Inventory|------------------------------
//[+]========================================================================[+]
actor BrickBreakPP : Ammo {inventory.amount 1 inventory.Maxamount 25}

actor ScreenBreaker : CustomInventory
{ // - Removes net Positive effects on Enemies
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Jumpifinventory("LightScreenPower",1,"Pickup2")
TNT1 A 0 A_Jumpifinventory("ReflectPower",1,"Pickup2")
TNT1 A 0 A_Jumpifinventory("CraftyShieldPower",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 A_SpawnItemEX("BarrierBreakFX",0,0,16,0,0,0,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_TakeInventory("LightScreenPower",99)
TNT1 A 0 A_TakeInventory("ReflectPower",99)
TNT1 A 0 A_TakeInventory("CraftyShieldPower",99)
stop

Nope:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor BrickBreakMelee : BasicMeleeAttack
{
Speed 80
Scale 3.0
DamageType "BrickBreak"
Obituary "$OB_BrickBreak"
Damage(MBP_BrickBreak1)
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 
HITG KKKKK 1 A_SpawnItemEx("BrickBreakFXTrail", 0, 0, 0, 0, 0, 0, 0,1)
stop

Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_Explode(MBP_BrickBreak2,64,0,0,64)
TNT1 A 0 A_SpawnItemEx("BrickBreakFX1", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_SpawnItemEx("BrickBreakFX2", 0, 0, 0, 0, 0, 0, 0,1)
TNT1 A 0 A_PlaySoundEx("Misc/HardDrop", "SoundSlot6",0)
TNT1 A 5
stop
}
}


actor BrickBreakFXTrail : BasicClientside
{
Scale 3.0
States
{
Spawn:
TNT1 A 0 
HITG K 1
HITG KKK 1 A_Fadeout(0.3)
stop
}
}


actor BrickBreakFX1 : BasicClientside
{
scale 3.0
States
{
Spawn:
TNT1 A 0 
HITG A 5
HITG AAAA 1 A_Fadeout(0.3)
stop
}
}

actor BrickBreakFX2 : BasicClientside
{
scale 3.0
States
{
Spawn:
TNT1 A 0 
BGHF ABC 1
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
BGHF C 1 A_SetScale(ScaleX+0.5)
BGHF C 0 A_Fadeout(0.3)
stop
}
}