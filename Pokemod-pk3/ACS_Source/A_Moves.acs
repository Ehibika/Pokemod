/* -=- Move Scripts -=-
 * Specialized scripts made for specific moves within the game.
 *

*/

#library "A_Moves"
#include "zcommon.acs"
#include "8bdmlib.acs"
#import "DB_Moves.acs"
#import "DB_Player.acs"
#import "DB_Data.acs"
#import "Dex_Pokemon.acs"
#import "Dex_Types.acs"
#import "Dex_Moves.acs"
#import "WEPACS.acs"
#import "DB_Stats.acs"
#import "DB_Leveling.acs"
#import "Dex_Leveling.acs"
#import "Dex_BaseStats.acs"
Script "SketchGrab" (Void)
{

// -=- meant to work with the targeter, grabs the move that your tracer currently has and replaces sketch with it -=-


// -=- first, grab what slot this sketch is on -=-
int ThisWeapon = WhichWeapon();
int MyTID = ActivatorTID();
Int TracerTID = ACS_NamedExecuteWithResult("Tracer_GetTID",0);
Int SketchSlot;

for(int i=1;i<=4;i++)
    {
    if(CheckInventory(StrParam(s:weapons_ammo[ThisWeapon][0],i:i))){SketchSlot = i; break;}
    }

SetActivator(TracerTID,AAPTR_DEFAULT);
int SketchWeapon = WhichWeapon();
SetActivator(MyTID,AAPTR_DEFAULT);

// -=- check if you have this weapon already -=-

for(i=1;i<=4;i++)
    {
    if(CheckInventory(StrParam(s:weapons_ammo[SketchWeapon][0],i:i)))
        {
        print(s:"You already know ",s:weapons_ammo[SketchWeapon][2],s:"!!"); 
        Terminate;
        }
    }

GiveInventory(StrParam(s:weapons_ammo[SketchWeapon][0],i:SketchSlot),1);
TakeInventory(StrParam(s:weapons_ammo[ThisWeapon][0],i:SketchSlot),99);

//now if the player has less than 4 weapons, let's relearn sketch on the
//next empty slot
if(SketchSlot<4)
    {
        GiveInventory(StrParam(s:weapons_ammo[SKETCH][0],i:SketchSlot+1),1);

        /* Now this is assuming that since most pokemon always have 4 moves, and there is no
         * way to delete moves at all, since smeargle starts with only sketch, the next slot
         * will always be empty since smeargle is also barred from learning TMs
        */
    }
    SetWeapon(StrParam(s:weapons_ammo[SketchWeapon][0],i:SketchSlot));
    SaveLoadout();
}

Script "ProtectBarrierWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, "ProtectGuard") > 0
	){
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Script "AlertFXWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, "CringeGuard") > 0
	){
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Script "FreezeFXWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, "FreezeMark") > 0
	){
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Script "FalseSwipeDamage" (int Damage)
{
int TargTID = ACS_NamedExecuteWithResult("core_gettarget",0);
int TracTID = ACS_NamedExecuteWithResult("core_gettracer",0);
if(GetActorProperty(TracTID,APROP_HEALTH) < 2){Terminate;}

ACS_NamedExecuteAlways("FalseBuddha",0,TracTID,1);
Delay(1);
Thing_Damage2(TracTID,Damage,"FalseSwipe");
ACS_NamedExecuteAlways("FalseBuddha",0,TracTID,0);
}

Script "FalseBuddha" (int TID, int State)
{
SetActivator(TID,AAPTR_DEFAULT);
SetPlayerProperty(0, State, PROP_BUDDHA);
}

Script "FalseEXPAward" (void)
{
if(EXPTime(0) == 0){Terminate;}
int TargTID = ACS_NamedExecuteWithResult("core_gettarget",0);
int VictimPN = Playernumber();
    int PlayerLVL = GetCvar(StrParam(s:"PlayerLevel_",i:VictimPN));
    int PlayerDexNum = GetCvar(StrParam(s:"PlayerDEXNum_",i:VictimPN));
    int VictimFrags = ACS_NamedExecuteWithResult("GetPlayerFrags",VictimPN);
    int VictimBounty = StatDEX[PlayerDexNum][6];
int EXPAward = VictimBounty*PlayerLVL;

//SetActivatorToTarget(0);
//int KillerTID = ActivatorTID();
// --=- no exp if you kill yourself
//if(VictimTID == KillerTID){Terminate;}

ACS_NamedExecuteAlways("GivePlayerEXP",0,TargTID-1000,EXPAward);
//log(s:GetTeamProperty(PlayerTeam(),TPROP_Name), s:" Team EXP Award ", i:TeamEXPAward[PlayerTeam()]);
}


// Make's Smoke less Opaque for teams
script "TeamSmoke"  (VOID)
{

    int cln = consolePlayerNumber();
    int FiringPlayer= ACS_NamedExecuteWithResult("core_gettarget",0) - 1000;
    ACS_NamedExecuteAlways("TeamSmoke2",0,FiringPlayer);

}

script "TeamSmoke2"  (int Player) CLIENTSIDE
{
    int cln = consolePlayerNumber();
    if( GetPlayerInfo(cln, PLAYERINFO_TEAM) == GetPlayerInfo(Player, PLAYERINFO_TEAM))//(PlayerClass((consolePlayerNumber() >= SUPPORTSTART) && PlayerClass((consolePlayerNumber()) <= SUPPORTEND)))
    {
        SetActorProperty(0,APROP_ALPHA,0.3);
    }

}


Script "AbsorbEffect" (int Per)
{
int Amount = getLastHit(0);
switch(Amount)
    {
        case -3: terminate; break;
        case -2: Delay(1); Restart; break;
        case -1: terminate; break;
    }

int DrainerTID = ACS_NamedExecuteWithResult("core_gettarget",0);

if(!CheckActorInventory(DrainerTID,"Absorb_Active")){Terminate;}

Amount = (Amount*Per)/100;
if(Amount<1) Amount = 1;

GiveInventory("AbsorbDrainFXSpawner",1);
SetActivator(DrainerTID,AAPTR_DEFAULT);
ACS_NamedExecuteAlways("PKMNACS_Basic(Over)Heal",0,0,Amount);
}

Script "LifeDrainEffect" (int Per)
{

    int Amount = getLastHit(0);
    switch(Amount)
        {
            case -3: terminate; break;
            case -2: Delay(1); Restart; break;
            case -1: terminate; break;
        }

int DrainerTID = ACS_NamedExecuteWithResult("core_gettarget",0);

Amount = (Amount*Per)/100;
if(Amount<1) Amount = 1;

SetActivator(DrainerTID,AAPTR_DEFAULT);
ACS_NamedExecuteAlways("PKMNACS_Basic(Over)Heal",0,0,Amount);
}

Script "RemoteControlActor" (int ActorSpeed, int MaxRange)
{

    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    if (MaxRange <= 0){MaxRange = 500;}

    int InputButtons;
    int InputOldButtons; 
    int UpInput;
    int DownInput;
    int LeftInput;
    int RightInput;
    Int ConfirmInput;
    Int CancelInput;


    int Velx;
    int Vely;
    int Velx2;
    int Vely2;
    int Angle;
    int HostAngle;
    int MoveAngle;

    GiveActorInventory(OwnerTID,"InRemoteMode",1);
    
    while(GetActorProperty(OwnerTID, APROP_HEALTH)>0 && !CheckActorInventory(OwnerTID,"RemoteKiller"))
    {	
        //log(s:"test");
        // - Grab Inputs -
        InputButtons = GetPlayerInput(OwnerTID-1000, INPUT_BUTTONS);
        UpInput = ButtonCheck(InputButtons, InputOldButtons, BT_FORWARD);
        DownInput = ButtonCheck(InputButtons, InputOldButtons, BT_BACK);
        LeftInput = ButtonCheck(InputButtons, InputOldButtons, BT_MOVELEFT);
        RightInput = ButtonCheck(InputButtons, InputOldButtons, BT_MOVERIGHT);
        ConfirmInput = ButtonCheck(InputButtons, InputOldButtons, BT_ATTACK);
        CancelInput = ButtonCheck(InputButtons, InputOldButtons, BT_AltATTACK);
        Angle = GetActorAngle(OwnerTID);
        HostAngle = VectorAngle (GetActorX (0) - GetActorX (OwnerTID), GetActorY (0) - GetActorY (OwnerTID));
        //MoveAngle = Angle;


        Velx = 0;
        vely = 0;
        VelX2 = 0;
        Vely2 = 0;
        if(UpInput != DownInput)
            {
                if(UpInput == 3){Velx = FixedMul(cos(Angle),  ActorSpeed*65536); vely = FixedMul(Sin(Angle),  ActorSpeed*65536);}
                if(DownInput == 3){Velx = FixedMul(cos(Angle), -ActorSpeed*65536); vely = FixedMul(Sin(Angle),  -ActorSpeed*65536);}
            }  

        if(LeftInput != RightInput)
            {
                if(LeftInput == 3){Velx2 = FixedMul(cos(-Angle),  ActorSpeed*65536); vely2 = FixedMul(Sin(-Angle),  ActorSpeed*65536);}
                if(RightInput == 3){Velx2 = FixedMul(cos(-Angle), -ActorSpeed*65536); vely2 = FixedMul(Sin(-Angle),  -ActorSpeed*65536);}
            }    
//log(f:HostAngle);
            if(CancelInput == 1){Thing_Remove(0); Terminate;}
            if(ConfirmInput == 1){SetActorState(0,"Trigger");}
    SetActorVelocity(0, velx+vely2, vely+velx2, 0, FALSE, FALSE);
    SetActorAngle (0,Angle );

    if(xyDistance(0, OwnerTID) > MaxRange)
        {
            Warp(OwnerTID,MaxRange*cos(HostAngle),MaxRange*Sin(-HostAngle),0,HostAngle,WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION|WARPF_TOFLOOR);
            SetActorAngle (0,Angle );
        //Velx = FixedMul(cos(HostAngle),  ActorSpeed*65536); vely = FixedMul(Sin(HostAngle),  ActorSpeed*65536);
        //SetActorVelocity(0, velx, vely, 0, FALSE, FALSE);    
        }
    Delay(1);
	InputOldButtons = InputButtons;
    }

    TakeActorInventory(OwnerTID,"InRemoteMode",99);
    Thing_Remove(0);
    if(CheckActorInventory(OwnerTID,"RemoteKiller")){GiveActorInventory(OwnerTID,"FrightFXSpawn",1);}
}


Script "GrabRemoteCoor" (int T)
{
int output;
switch(T)
    {
    case 0: output = GetUserVariable(0,"user_RemoteX"); break;
    case 1: output = GetUserVariable(0,"user_RemoteY"); break;
    case 2: output = GetUserVariable(0,"user_RemoteZ"); break;
    }
SetResultValue(output);
}

script "SnapToRemote" (int X_Placement)
{
    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int OwnerPnum = OwnerTID-1000;
    Warp(9100+OwnerPnum, X_Placement, 0, 0, 0, WARPF_ABSOLUTEPOSITION|WARPF_USECALLERANGLE|WARPF_INTERPOLATE);

}



Script "SubstituteSetup" (void)
{

    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int OwnerMaxHP = GetActorProperty(OwnerTID, APROP_SPAWNHEALTH);
    int OwnerHP = GetActorProperty(OwnerTID, APROP_HEALTH);
    int SubsHP = OwnerMaxHP/3;
    int SubsPDEF = CheckActorInventory(OwnerTID,"Active_PDefense");
    int SubsSDEF = CheckActorInventory(OwnerTID,"Active_SDefense");
    int StatValue;
    int ModStat;
    int DollTID = SUBTID+OwnerTID;
    Bool Replaced;

    if(IsTIDUsed(DollTID))
        {
            until(!IsTIDUsed(DollTID)){SpawnSpotForced("SubPoofFXSpawn",DollTID,0,0); Thing_remove(DollTID);}
            Replaced = True;
        }
    Thing_ChangeTID(0,DollTID);

    SetActorPRoperty(0,APROP_SPAWNHEALTH,SubsHP);
    SetActorPRoperty(0,APROP_HEALTH,SubsHP);
    SetActorProperty(OwnerTID, APROP_HEALTH,OwnerHP-SubsHP);
    //Log(i:SubsHP);

    ModStat = SubsPDEF;

    if(ModStat >= DEFSTATBASELINE2)
    {
    StatValue = Noneg(ValueMap(ModStat,STATMAX,DEFSTATBASELINE2,DEFVAL1,DEFVAL2));
    }
else if(ModStat >= DEFSTATBASELINE1)
    {
    StatValue = Noneg(ValueMap(ModStat,DEFSTATBASELINE2,DEFSTATBASELINE1,DEFVAL2,DEFVAL3));
    }
else
    {
    StatValue = Noneg(ValueMap(ModStat,DEFSTATBASELINE1,STATMIN,DEFVAL3,DEFVAL4));    
    }
if(StatValue < 10){StatValue = 10;}
GiveInventory(StrParam(s:"PDEF",i:StatValue),1);

ModStat = SubsSDEF;

if(ModStat >= DEFSTATBASELINE2)
    {
    StatValue = Noneg(ValueMap(ModStat,STATMAX,DEFSTATBASELINE2,DEFVAL1,DEFVAL2));
    }
else if(ModStat >= DEFSTATBASELINE1)
    {
    StatValue = Noneg(ValueMap(ModStat,DEFSTATBASELINE2,DEFSTATBASELINE1,DEFVAL2,DEFVAL3));
    }
else
    {
    StatValue = Noneg(ValueMap(ModStat,DEFSTATBASELINE1,STATMIN,DEFVAL3,DEFVAL4));    
    }
if(StatValue < 10){StatValue = 10;}
GiveInventory(StrParam(s:"SDEF",i:StatValue),1);

}



Script "SubstituteActive" (int Dist)
{
    int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
    Int DollTID = ActivatorTID();
    Int DollState = 0;
    int DollNewState = 0;
    
    SetPointer(AAPTR_TRACER, Player);

	// Loop if shield is active
	while(GetActorProperty(0, APROP_HEALTH)>0 && GetActorProperty(Player, APROP_HEALTH)>0){

        if(IsPointerEqual(AAPTR_TARGET, AAPTR_TRACER) == FALSE){Delay(1); SetPointer(AAPTR_TARGET, Player); continue;}

        //log(i:xyzDistance(0,Player),s:"   ",i:Dist);
        if(CheckSight(DollTID,Player,0) && xyzDistance(DollTID,Player) < Dist)
            {
               //GiveActorInventory(DollTID,"SubstituteFunction_A",1);
               //SpawnProjectile(1, "SubstituteLinkFX", 0, 20, 0, 0, 0);
            //if(CheckActorInventory(Player,"SubGuard"))GiveActorInventory(Player,"SubstituteGuardGiver_B",1);
           // else GiveActorInventory(Player,"SubstituteGuardGiver_A",1);
           DollNewState = 1;
            }
            else {DollNewState = 0;}

            if(DollState != DollNewState)
            {

                //Switch(DollNewState)
                //    {
                //        Case 0: SetActorState(DollTID,"Spawn.Basic",0); Break;
                //        Case 1: SetActorState(DollTID,"Spawn.Linked",0); Break;
                //    }
                SetUserVariable(DollTID,"User_DecoyState",DollNewState);
                 DollState = DollNewState;
                 
            }
            
        Delay(1);
        
    }
    
    if(GetActorProperty(Player, APROP_HEALTH)<=0 || !PlayerInGame(Player-1000)){SpawnSpotForced("SubPoofFXSpawn",DollTID,0,0); Thing_remove(DollTID);}
}


Script "SubstituteBarrierWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, "SubGuard") > 0
	){
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Script "MultishotCountWarper" (void) CLIENTSIDE
{
    int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
    int MHCount;
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& (CheckActorInventory(Player, "MultiShotCounter") > 0 || CheckActorInventory(Player, "MultiShotTimer") > 0)
	){

        MHCount = CheckActorInventory(Player, "MultiShotCounter");
        SetActorState(0,strParam(s:"Count",i:MHCount));
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Script "GeneralWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, "BasicWarperHold") > 0 
	){
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Script "GeneralWarper2" (void) //CLIENTSIDE  //for nonclientsided actors
{
    int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
    str FlagName = StrParam(s:GetActorClass(0), s:"Flag");

	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, FlagName) > 0 
	){
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}


Script "EscapeOrbHitCheck" (void) //CLIENTSIDE  //for nonclientsided actors
{
    int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
    str FlagName = "PainPower";

	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& !CheckActorInventory(Player, FlagName) 
	){
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Failure");
    Delay(17);
	Thing_Remove(0);
}

Script "CheckStatusGuard" (int Pointer, int Input)
{
Int Output;

/*
 * 0 = no status protection
 * 1 = Minor status protection
 * 2 = Major status protection
 *
 */ 


if(Pointer!=0){SetActivator(0,Pointer);}

if(!IsValidPKMNActor(activatorTID())){Output = Output | STATUS_MAJOR | STATUS_MINOR | STATUS_SPECIALTY | STATUS_STATS | STATUS_TAUNT;;}



if(CheckInventory("SleepMark")
||CheckInventory("PoisonMark")
||CheckInventory("BurnMark")
||CheckInventory("ParalysisMark")
||CheckInventory("semiFreezeMark")
||CheckInventory("FreezeMark")
||CheckInventory("RestMark")
||CheckInventory("CureAll")
||CheckInventory("FeelingFine")
||CheckInventory("CraftyShieldPower"))
{Output = Output | STATUS_MAJOR;}

if(CheckInventory("ProtectGuard")
||CheckInventory("DetectGuard")
||CheckInventory("SubGuard"))
{Output = Output | STATUS_MAJOR | STATUS_MINOR | STATUS_STATS;}

    if(CheckInventory("SafeguardPower"))
    {Output = Output | STATUS_MAJOR | STATUS_MINOR | STATUS_SPECIALTY | STATUS_TAUNT;}

    if(CheckInventory("MistFlag"))
    {Output = Output | STATUS_STATS  | STATUS_MINOR | STATUS_TAUNT;}

    if(CheckInventory("CleanseTag"))
    {Output = Output | STATUS_STATS;}
SetResultValue(Output & Input);
}

Script "InfernoPowerLevel" (int MaxDamage)
{
    int Player = ACS_NamedExecuteWithResult("core_gettarget",0);

    int CurrentPower = CheckActorInventory(Player,"InfernoPower");
    int Output = ((MaxDamage*CurrentPower)/100);
    SetResultValue(Output);
}

Script "DisableEffect" (Void)
{
int ThisWeapon = WhichWeapon();
TakeInventory("DisableFlag",999);
GiveInventory("DisableFlag",ThisWeapon);
}

Script "IsMoveDisabled" (Void)
{

//used so that you get an error sound when attempting to use a disabled move

int ThisWeapon = WhichWeapon();
int Output;

if(CheckInventory("DisableFlag") == ThisWeapon){Output = 1;}
SetResultValue(Output);
}



script "BarrierBlockTID" (void)
{
    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int WallTID = BARRIERTID+OwnerTID;

    if(IsTIDUsed(WallTID))
    {
        until(!IsTIDUsed(WallTID)){/*SpawnSpotForced("SubPoofFXSpawn",WallTID,0,0);*/ Thing_remove(WallTID);}
    }
    Thing_ChangeTID(0,WallTID);
}

Script "BarrierStatGrab" (int WallTID)
{

    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int OwnerMaxHP = GetActorProperty(OwnerTID, APROP_SPAWNHEALTH);
    int PlayerLVL = GetCvar(StrParam(s:"PlayerLevel_",i:OwnerTID-1000));
    int StatValue;
    int ModStat;

    //Log(i:SubsHP);



    GiveInventory(StrParam(s:"PDEF",i:100),1);
    GiveInventory(StrParam(s:"SDEF",i:100),1);
/*
    ModStat = CheckActorInventory(OwnerTID,"Active_SAttack");

if(ModStat >= STATBASELINE)
    {
    StatValue = Noneg(ValueMap(ModStat,STATMAX,STATBASELINE,50,200));
    }
else
    {
    StatValue = Noneg(ValueMap(ModStat,STATBASELINE,STATMIN,50,10));    
    }
if(StatValue < 10){StatValue = 10;}
*/

StatValue = 150+(50*PlayerLVL);
SetActorProperty(0,APROP_SPAWNHEALTH,StatValue);
SetActorProperty(0,APROP_HEALTH,StatValue);

SetUserVariable(0,"User_HPR1",(StatValue/4)*3);
SetUserVariable(0,"User_HPR2",StatValue/2);
//SetActorProperty(0,APROP_SPECIES,GetActorProperty(OwnerTID,APROP_SPECIES));
//ACS_NamedExecuteAlways("ConsoleSpeciesSynch",0);

}



script "SomarianBlockTID" (void)
{
    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int WallTID = SOMARIATID+OwnerTID;

    if(IsTIDUsed(WallTID))
    {
        until(!IsTIDUsed(WallTID)){/*SpawnSpotForced("SubPoofFXSpawn",WallTID,0,0);*/ Thing_remove(WallTID);}
    }
    Thing_ChangeTID(0,WallTID);
}
Script "SomarianBlockStatGrab" (int WallTID)
{

    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int OwnerMaxHP = GetActorProperty(OwnerTID, APROP_SPAWNHEALTH);
    int BlockHP = OwnerMaxHP/4;

    GiveInventory(StrParam(s:"PDEF",i:100),1);
    GiveInventory(StrParam(s:"SDEF",i:100),1);

SetActorProperty(0,APROP_SPAWNHEALTH,BlockHP);
SetActorProperty(0,APROP_HEALTH,BlockHP);

SetUserVariable(0,"User_HPR1",(BlockHP/4)*3);
SetUserVariable(0,"User_HPR2",BlockHP/2);

}

Script "YawnBreakCheck" (int Pointer)
{
    Int Output;
    
    if(Pointer!=0){SetActivator(0,Pointer);}

    if(CheckInventory("CloseCombatTimer")
    ||CheckInventory("OutrageTimer")
    ||CheckInventory("ThrashTimer")
    ||CheckInventory("RolloutTimer")
    ||CheckInventory("GyroballTimer")
    ||CheckInventory("FlameWheelTimer")
    ||CheckInventory("FlareBlitzTimer")
    ||CheckInventory("HiJumpKickTimer")
    ||CheckInventory("BraveBirdTimer")
    ||CheckInventory("DivePPTimer")
    ||CheckInventory("PhantomForcePPTimer")
    ||CheckInventory("DoubleEdgeTimer"))
    {Output = 1;}


    SetResultValue(Output);
}


Script "CheckDebuffAura" (void)
{
Int Output;

if(CheckInventory("CharmAuraFlag")
||CheckInventory("CaptivateAuraFlag")
||CheckInventory("ScreechAuraFlag")
||CheckInventory("FakeTearsAuraFlag")
||CheckInventory("ScaryFaceAuraFlag"))
{Output = 1;}

SetResultValue(Output);
}

Script "GenericAttackPower" (int Rate)
{
    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int PlayerLVL = GetCvar(StrParam(s:"PlayerLevel_",i:OwnerTID-1000));
    setResultValue(Rate*(PlayerLVL-1));

}

Script "DeployableDefenseSet" (int MinHP)
{

    int OwnerTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int OwnerMaxHP = GetActorProperty(OwnerTID, APROP_SPAWNHEALTH);
    int OwnerHP = GetActorProperty(OwnerTID, APROP_HEALTH);
    int SubsHP = OwnerMaxHP;
    int SubsPDEF = CheckActorInventory(OwnerTID,"Active_PDefense");
    int SubsSDEF = CheckActorInventory(OwnerTID,"Active_SDefense");
    int StatValue;
    int ModStat;
    int DollTID = SUBTID+OwnerTID;
    Bool Replaced;

    if(MinHP > 0 && SubsHP < MinHP){SubsHP = MinHP;}

    SetActorPRoperty(0,APROP_SPAWNHEALTH,SubsHP);
    SetActorPRoperty(0,APROP_HEALTH,SubsHP);
    //Log(i:SubsHP);

    ModStat = SubsPDEF;

if(ModStat >= STATBASELINE)
    {
    StatValue = Noneg(ValueMap(ModStat,STATMAX,STATBASELINE,DEFVAL1,DEFVAL2));
    }
else
    {
    StatValue = Noneg(ValueMap(ModStat,STATBASELINE,STATMIN,DEFVAL2,DEFVAL3));    
    }
if(StatValue < 10){StatValue = 10;}
GiveInventory(StrParam(s:"PDEF",i:StatValue),1);

ModStat = SubsSDEF;

if(ModStat >= STATBASELINE)
    {
    StatValue = Noneg(ValueMap(ModStat,STATMAX,STATBASELINE,DEFVAL1,DEFVAL2));
    }
    else
    {
    StatValue = Noneg(ValueMap(ModStat,STATBASELINE,STATMIN,DEFVAL2,DEFVAL3));    
    }
if(StatValue < 10){StatValue = 10;}
GiveInventory(StrParam(s:"SDEF",i:StatValue),1);

}

Script "ActorDecay" (int Damage)
{
    Thing_Damage2(0,Damage,"Decay");
}

Script "PitchClamp" (int PitchVal, int PitchClamp)
{
int Output;

if(PitchVal < PitchClamp && GetActorZ(0)-GetActorFloorZ(0) == 0){Output = PitchVal;}
SetResultValue(Output);

}

Script "GetHoneClawsLVL" (int Pointer)
{
    if(Pointer!=0){SetActivator(0,Pointer);}

    int output;
    if(CheckInventory("STF_HoneClaws1")) output++;
    if(CheckInventory("STF_HoneClaws2")) output++;
    if(CheckInventory("STF_HoneClaws3")) output++;
    SetResultValue(Output);
}

Script "GetRockPolishLVL" (int Pointer)
{
    if(Pointer!=0){SetActivator(0,Pointer);}

    int output;
    if(CheckInventory("STF_RockPolish1")) output++;
    if(CheckInventory("STF_RockPolish2")) output++;
    if(CheckInventory("STF_RockPolish3")) output++;
    SetResultValue(Output);
}

script "TrapSensorIdentify"  (VOID) 
{

	int SensorPlayer= ACS_NamedExecuteWithResult("Core_GetTarget", 0) - 1000;
    int FiringPlayer= ACS_NamedExecuteWithResult("Core_GetMaster", 0) - 1000;
    int Output;
    if( GetPlayerInfo(SensorPlayer, PLAYERINFO_TEAM) == GetPlayerInfo(FiringPlayer, PLAYERINFO_TEAM))
    {
        Output = 1;
    }

    setResultValue(Output);
}

Script "CurseHarm" (int Damage)
{
    Thing_Damage2(0,Damage,"Curse");
}

script "Check_ShowStatParticles" (void) CLIENTSIDE
{
SetResultValue(GetStatParticles());
}

function bool GetStatParticles(void)
{
	return (getUserCvar(ConsolePlayerNumber(),"PKMN_ShowStatParticles")>0);
}

Script "ElectroBallSpeedDamage" (int Base)
{
    int TargTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int TracTID = ACS_NamedExecuteWithResult("core_gettracer",0);

    int TargSPDT = PlayerSpeedTiers[TargTID-1000];
    int TracSPDT = PlayerSpeedTiers[TracTID-1000];

    int SPDTDiff = TargSPDT-TracSPDT;
    if(SPDTDiff<1){SPDTDiff = 1;}

    int Damage = Base*SPDTDiff;

    Thing_Damage2(TracTID,Damage,"ElectroBall");
}

Script "BerrySteal" (void)
{
    int TargTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    str StealBerry = BerryStealPriority();

    if(StealBerry != "none")
        {
            takeInventory(StealBerry,1);
            GiveActorInventory(TargTID,StrParam(s:"Steal",s:StealBerry),1);
            ACS_NamedExecuteAlways("Write_Inventory",0,2);
        }
}

function Str BerryStealPriority (void)
{

if(CheckInventory("BigHPBerry")){return "BigHPBerry";}
if(CheckInventory("HPBerry")){return "HPBerry";}
if(CheckInventory("BigPPBerry")){return "BigPPBerry";}
if(CheckInventory("PPBerry")){return "PPBerry";}

return "none";
}

Script "FlightWorthy" (int Pointer)
{
    if(Pointer!=0){SetActivator(0,Pointer);}

    Bool output = true;
    if(CheckInventory("ParalysisMark")) output = false;
    if(CheckInventory("SleepMark"))  output = false;
    if(CheckInventory("RestMark"))  output = false;
    if(CheckInventory("SemiFreezeMark"))  output = false;
    if(CheckInventory("FreezeMark"))  output = false;
    if(CheckInventory("CringeMark"))  output = false;
    SetResultValue(Output);
}

Script "LevelPower" (int Damage)
{
    int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
    int PlayerLVL = GetPlayerPokeValue(Player-1000,POKELEVEL);
    int Output = (Damage*PlayerLVL);
    SetResultValue(Output);
}

script "Give_SubmissinInvis_ToTID" (int tid)
{
	if (tid==999) { terminate; }
	giveActorInventory(tid,"SubmissionInvisFunc",1);
}

script "Give_SubmissinRevis_ToTID" (int tid)
{
	if (tid==999) { terminate; }
	giveActorInventory(tid,"SubmissionReVisibleFunc",1);
}

script "Give_MegahornToss_ToTID" (int tid)
{
	if (tid==999) { terminate; }
	giveActorInventory(tid,"MegahornPushStart",1);
}

Script "DetectFlicker" (void)
{

bool FlickerState;

SetActorProperty(0,Aprop_Renderstyle,STYLE_None);
FlickerState = false;

while(GetActorProperty(0, APROP_HEALTH)>0
&& CheckInventory("DetectGuard") > 0
    ){
        Delay(2);

        Switch(FlickerState)
            {
                case 0: SetActorProperty(0,Aprop_Renderstyle,STYLE_Translucent); FlickerState = true; break;
                case 1: SetActorProperty(0,Aprop_Renderstyle,STYLE_None); FlickerState = false; break;
            }
    }

    SetActorProperty(0,Aprop_Renderstyle,STYLE_Translucent);
}

Script "ToxicDamage" (int DamagePer)
{
    int TargTID = ACS_NamedExecuteWithResult("core_gettarget",0);
    int SelfHP = GetActorProperty(0, APROP_HEALTH);
    if(SelfHP<=1){Terminate;}
    int Damage = ACS_NamedExecuteWithResult("PKMNACS_HealthPercent",0,1,DamagePer);
    if(Damage > SelfHP){Damage = SelfHP-1;}

    Thing_Damage2(0,Damage,"PoisonDamage");
}


Script "CheckSilverWindBuff" (int Pointer)
{
    if(Pointer!=0){SetActivator(0,Pointer);}

    bool output;
    if(CheckInventory("STF_SilverwindA")) output = true;
    if(CheckInventory("STF_SilverwindB")) output = true;
    if(CheckInventory("STF_SilverwindC")) output = true;
    if(CheckInventory("STF_SilverwindD")) output = true;
    if(CheckInventory("STF_SilverwindE")) output = true;
    SetResultValue(Output);
}

Script "TeleportSpotMark" (void)
{
    int pln = PlayerNumber();
    int tid = pln+1000;
    int ang = GetActorAngle(tid);
    If(ThingCount(0, pln+PLN_EXITUNITIMAGE)>0)
    {
        Thing_Remove(pln+PLN_EXITUNITIMAGE);
    }
    SpawnSpotForced("ExitUnitGhost",ActivatorTID(),pln+PLN_EXITUNITIMAGE,ang>>8);

    If(ThingCount(0, pln+PLN_EXITUNITPOINT)>0)
    {
        Thing_Remove(pln+PLN_EXITUNITPOINT);
    }
    SpawnSpotForced("TeleportDest2",ActivatorTID(),pln+PLN_EXITUNITPOINT,ang>>8);
}

Script "SetFlyTakeoffZ" (void)
{
SetUserVariable(0,"user_FlyStartZ",GetActorZ(0)>>16);
}

Script "GetFlyTakeoffZ" (void)
{
    SetResultValue(GetUserVariable(0,"user_FlyStartZ"));
}

Script "GetMoveRecoveryLVL" (int Pointer)
{
    if(Pointer!=0){SetActivator(0,Pointer);}

    int output;
    if(CheckInventory("Recovery_A")) output++;
    if(CheckInventory("Recovery_B")) output++;
    if(CheckInventory("Recovery_C")) output++;
    if(CheckInventory("Recovery_D")) output++;
    if(CheckInventory("ChannelingSpeed")) output++;
    if(CheckInventory("ConShotTimer")) output++;
    if(CheckInventory("HeldItemCooldown")) output+=2;
    SetResultValue(Output);
}



Script "DragonRagePower" (int BaseDamage, int ModDamage)
{
    int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
    int PlayerLVL = GetPlayerPokeValue(Player-1000,POKELEVEL);
    int Mult = CheckActorInventory(Player,"DragonPowerFlag");
    int a = ModDamage*PlayerLVL;
    int b = a+BaseDamage;
    int Output = b*Mult;
    SetResultValue(Output);
}


//Demi Code by Jaxof7, as most of the code here is.

str exploder;
int exploderOwner;

script "SetExploder" (int Type)
{

    Switch(Type)
        {
            Case 1: exploder = "PoisonPowderSploder"; break;
            Case 2: exploder = "PoisonGasSploder"; break;
            default: exploder = "TheDemi"; break;
        }
        //print(s:exploder);
exploderOwner = ACS_NamedExecuteWithResult("core_gettarget",0);
}

script "ExploderScript" (void)
{
int oldTID = ActivatorTID();
if(oldTID!=exploderOwner) {
	int u = UniqueTID();
	Thing_ChangeTID(0,u);

	int u2 = UniqueTID();
	SpawnForced(exploder,0,0,0,u2,0);

	SetActivator(u2);

	SetPointer(AAPTR_TARGET,exploderOwner);
	SetPointer(AAPTR_TRACER,u);

	Thing_ChangeTID(0,0);
	Thing_ChangeTID(u,oldTID);
}
}