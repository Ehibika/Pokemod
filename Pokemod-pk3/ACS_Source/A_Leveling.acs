/* -=- Leveling -=-
 * Scripts that handle the function of EXP gaining, leveling up, and evolutions, everything to do with player
 * advancement is found here.
 * 

*/

#library "A_Leveling"
#import "DB_Leveling.acs"
#import "Dex_Pokemon.acs"
#import "Dex_Evo.acs"
#import "Dex_Types.acs"
#import "Dex_Leveling.acs"
#import "Dex_BaseStats.acs"
#import "Dex_Moves.acs"
#import "Dex_Movesets.acs"
#import "DB_Player.acs"
#import "G_Startup.acs"
#import "A_Shop.acs"
#import "WEPACS.acs"
#include "zcommon.acs"

//#region -=- Experience And Levels -=-
#region -=- Experience And Levels -=-

Script "PKMN_EXPevents" (int type, int arg1, int arg2) EVENT
{
    switch(type)
        {
        Default: break;
        Case 0: // -=- Player Elimination -=-
            //ACS_NamedExecuteAlways("FragEXPAward",0,arg1);
        break;
        Case 6: // -=- Round End -=-
        //ACS_NamedExecuteAlways("TeamAward",0);
        ACS_NamedExecuteAlways("PKMN_MassLevelup",0);
        ACS_NamedExecuteAlways("IncrementRoundLevel",0);
        break;
        }
}

script "TeamAward" (Void)
{
    if(GetCvar("teamlms")!=1){Terminate;}
    Log(s:"\cgBegin Team EXP");
    int Gamestate = GetGamemodeState();
    int PTeam;
    Until(Gamestate == 1) {Gamestate = GetGamemodeState(); Delay(1);}
    Log(s:"\cgAbout to distribute Team EXP");
    Delay(35);
    for(int i = 0; i < PlayerCount(); i++)
    {
        Pteam = GetPlayerInfo(i, PLAYERINFO_TEAM);
        ACS_NamedExecuteAlways("GivePlayerEXP",0,i,TeamEXPAward[Pteam],1);
        ACS_NamedExecuteAlways("GivePlayerBP",0,i,TeamBPAward[Pteam],1);
    }
    for(int j = 0; j < 4; j++)
    {TeamEXPAward[j] = 0; TeamBPAward[j] = 0;}
    Log(s:"\cgEnd Team EXP");
}

//#region -=- Depreciated -=-
/* 

{
if(GetCvar("teamlms")!=1){Terminate;}
Log(s:"\cgBegin Team EXP");
int Gamestate;
int PTeam;
While(True)
    {
    Gamestate = GetGamemodeState();
    switch(Gamestate)
        {
        Default: break;
        Case 3://in result sequence
        for(int i = 0; i < PlayerCount(); i++)
            {
                Pteam = GetPlayerInfo(i, PLAYERINFO_TEAM);
                ACS_NamedExecuteAlways("GivePlayerEXP",0,i,TeamEXPAward[Pteam]);
            }
        for(int j = 0; j < 4; j++)
            {TeamEXPAward[j] = 0;}
        //Log(s:"\cgEnd Team EXP");
        Terminate;
        Break;

        }
        Delay(1);
    }
}
*/
//#endregion

Script "AddToBPAward" (int ammount)
{
    TeamBPAward[PlayerTeam()] += ammount;
}

Script "PKMN_HitEXP" (Int Damage, int Divisor)
{
if(EXPTime() == 0){Terminate;}

Damage = getLastHit(0);

switch(Damage)
    {
        case -3: terminate; break;
        case -2: Delay(1); Restart; break;
        case -1: Delay(1); Restart; break;
    }
    //log(i:Damage);
int Output;
int EnemyPlayer = ACS_NamedExecuteWithResult("core_gettarget", 0);
int EnemyPlN = EnemyPlayer - 1000;

if(Divisor<1){Divisor = 1;}
//log(i:EnemyPlN);
int PlayerLVL = GetCvar(StrParam(s:"PlayerLevel_",i:PlayerNumber()));
int PlayerDexNum = GetCvar(StrParam(s:"PlayerDEXNum_",i:PlayerNumber()));
//int SelfDexBounty = StatDEX[PlayerDexNum][6];

int SelfEXPBounty = CheckInventory("EXPBountyitem");
Output = 1 + Damage/Divisor;
//Output = 1 + PlayerLVL + Damage/Divisor;
if(SelfEXPBounty)
    {
        if(SelfEXPBounty < Output){Output = SelfEXPBounty;}
        TakeInventory("EXPBountyitem",Output);
        ACS_NamedExecuteAlways("GivePlayerEXP",0,EnemyPlN,Output,1);
        if(CheckActorInventory(EnemyPlayer,"HelpingHandPower")){GiveActorInventory(EnemyPlayer,"HelperEXPItem",Output/2);}
    }

    //Log(i:Damage, s:" : ",i:Output);
}


Script "GiveBlockEXP" (int TID, int EXPAward, int Override)
{
    if(TID>999)
        {
            //Log(i:TID);
            SetActivator(TID);
            ACS_NamedExecuteAlways("GiveEXP",0,EXPAward,Override);
        }    
}

Script "GivePlayerEXP" (int Pnum, int EXPAward, int Override)
{

    SetActivator(Pnum+1000);
    ACS_NamedExecuteAlways("GiveEXP",0,EXPAward,Override);
}

Script "GrabHelperEXP" (int TargPointer)
{
int HelperTID = ActivatorTID();
if(TargPointer!=0){SetActivator(0,TargPointer);}
else{Terminate;}

if(CheckInventory("HelperEXPItem"))
    {
        ACS_NamedExecuteAlways("GivePlayerEXP",0,HelperTID-1000,CheckInventory("HelperEXPItem"));
        TakeInventory("HelperEXPItem",9999);
    }
}

Script "GiveEXP" (int EXPAward, Int Override)
{
if(!(ClassifyActor(ActivatorTID()) & ACTOR_PLAYER)){Terminate;}
int p = Playernumber();
int PlayerDexNum = GetCvar(StrParam(s:"PlayerDEXNum_",i:PlayerNumber()));
Int LvlRate = StatDEX[PlayerDexNum][9];
if(EXPTime() == 0 && Override == 0){Terminate;}

While(PlayerIsSpectator(p) == 2){Delay(1);}
if(PlayerIsSpectator(p) == 1){Terminate;}
Bool LevelLoopDone;
str PLAYER_EXPCV = StrParam(s:"PlayerEXP_",i:PlayerNumber());

int PlayerLVL = GetCvar(StrParam(s:"PlayerLevel_",i:PlayerNumber()));
int PlayerEXP = GetCvar(PLAYER_EXPCV);

int levelGoal = 1;

if(PlayerLVL >= PKMN_MaxLVL){SetCvar(PLAYER_EXPCV,LevelUpTable[LvlRate][PKMN_MaxLVL-1]); Terminate;}

PlayerEXP = PlayerEXP+EXPAward;
SetCvar(PLAYER_EXPCV,PlayerEXP);

for(int i = 1; i<PKMN_MaxLVL; i++)
    {
        if(PlayerEXP >= LevelUpTable[LvlRate][i]){levelGoal++;}
    }

    if(levelGoal > PlayerLVL)
        {
            if(!CheckInventory("LevelupBuffer"))
            {
            GiveInventory("LevelupCue",1);
            Log(n:0,s:"\cf Grew to LVL ",i:levelGoal);//,s:"  ",i:PKMN_MaxLVL
            ACS_NamedExecuteAlways("SetPKMNLevel",0,levelGoal);  
            ReportNewMoves(PlayerDexNum,PlayerLVL,levelGoal);
            }
        }
//#region OLD
/*
if(PlayerLVL >= PKMN_MaxLVL){LevelLoopDone = true; Terminate;}

//Just in case
if(GetCVar(PLAYER_EXPCV) >= LevelUpTable[LvlRate][9]){LevelLoopDone = true; Terminate;}


SetCvar(PLAYER_EXPCV,PlayerEXP+EXPAward);
// check if this passes the next level threshold
Until(LevelLoopDone)
    {
    if(GetCVar(PLAYER_EXPCV) >= LevelUpTable[LvlRate][PlayerLVL] && PlayerLVL < PKMN_MaxLVL)
        {
            PlayerLVL++;
            GiveInventory("LevelupCue",1);
            Log(n:0,s:"\cf Grew to LVL ",i:PlayerLVL);

        }
    else
        {
        ACS_NamedExecuteAlways("SetPKMNLevel",0,PlayerLVL);
        //if(PlayerIsBot(p)){ACS_NamedExecuteAlways("EvolveTrigger",0);}
        LevelLoopDone = true;
        }

    }
    */
//#endregion

}



Script "GummiEXP" (int EXPAward, Int TypeMatch)
{
// -=- Script called by Gummy Pickups, gives EXP based on type matchup
int PlayerDEXNum = GetPokeValue(POKEDEXNUM);
int TypeA = StatDEX[PlayerDEXNum][7];
int TypeB = StatDEX[PlayerDEXNum][8];
int MatchVal;
int EXPMult = 1.0;
int EXPOutput;
if(TypeA == TypeMatch || TypeB == TypeMatch){EXPMult = 2.0;}
else 
    {    
        switch(PKMNTypeList2[TypeA][TypeMatch])
        {
        Case T_NEU: break;
        Case T_WEA: MatchVal--; break;
        Case T_RES: MatchVal++; break;
        Case T_IMU: MatchVal++; break;
        }

        if(TypeB!=Type_NULL)
        {
        switch(PKMNTypeList2[TypeB][TypeMatch])
        {
            Case T_NEU: break;
            Case T_WEA: MatchVal--; break;
            Case T_RES: MatchVal++; break;
            Case T_IMU: MatchVal++; break;
            }  
        }
    }

    if(MatchVal > 0){EXPMult = 1.5;}
    if(MatchVal < 0){EXPMult = 0.5;}
    EXPOutput = EXPAward*EXPMult >> 16;
    ACS_NamedExecuteAlways("GiveEXP",0,EXPOutput,1);

}

Script "SetPlayerPKMNLevel" (int Pnum, int NewPlayerLevel, int SynchEXP)
{
    SetActivator(Pnum+1000);
    ACS_NamedExecuteAlways("SetPKMNLevel",0,NewPlayerLevel,SynchEXP);

}

Script "SetPKMNLevel" (int NewPlayerLevel, int SynchEXP)
{
        int CurLevel = GetCVar(StrParam(s:"PlayerLevel_",i:PlayerNumber()));
        if(NewPlayerLevel > PKMN_MaxLVL){NewPlayerLevel = PKMN_MaxLVL;}
        if(NewPlayerLevel < 1){NewPlayerLevel = 1;}
        if(NewPlayerLevel != CurLevel)
        {
        SetCVar(StrParam(s:"PlayerLevel_",i:PlayerNumber()),NewPlayerLevel);
        if(SynchEXP)
        {
            int PlayerDEXNum = GetCvar(StrParam(s:"PlayerDEXNum_",i:PlayerNumber()));
            int EXPRate = StatDEX[PlayerDEXNum][9];
            if(NewPlayerLevel>1){SetCvar(StrParam(s:"PlayerEXP_",i:PlayerNumber()),LevelUpTable[EXPRate][NewPlayerLevel-1]);}

        }

        if(PlayerInGame(PlayerNumber()))ACS_NamedExecuteAlways("ActiveStatUpdate",0);
        }
        //Else{ACS_NamedExecuteAlways("StatusMenuUpdate",0);}

}

Script "PKMN_MassLevelup" (Void)
{
    if(GetCvar("PKMN_NoEXP")==1||GetCvar("PKMN_NoRoundLeveling")==1){Terminate;}
    int PlayerDEXNum;
    int EXPRate;
    int CurLevel;
    int Gamestate;

    Until(Gamestate == 1) {Gamestate = GetGamemodeState(); Delay(1);}

    for(int i = 0; i < 64; i++)
    {
        if(ClassifyActor(i+1000) & ACTOR_ALIVE) {ACS_NamedExecuteAlways("PKMN_PlayerLevelup",0,i);}
        else{
            PlayerDEXNum = GetCvar(StrParam(s:"PlayerDEXNum_",i:i));
            EXPRate = StatDEX[PlayerDEXNum][9];
            CurLevel = GetCVar(StrParam(s:"PlayerLevel_",i:i));
            SetCVar(StrParam(s:"PlayerLevel_",i:i),CurLevel+1);
            if(CurLevel+1 < PKMN_MaxLVL){SetCvar(StrParam(s:"PlayerEXP_",i:i),LevelUpTable[EXPRate][CurLevel+1]);}
        }
    }    
}

Script "PKMN_PlayerLevelup" (int Pnum)
{
    SetActivator(Pnum+1000);
    ACS_NamedExecuteAlways("PKMN_Levelup",0);
}

Script "PKMN_Levelup" (void)
{
int CurLevel = GetCVar(StrParam(s:"PlayerLevel_",i:PlayerNumber()));

if(CurLevel<PKMN_MaxLVL)
    {
    ACS_NamedExecuteAlways("SetPKMNLevel",0,CurLevel+1,1);
    if(PlayerInGame(PlayerNumber()))GiveInventory("LevelupCue",1);
    }
}

Script "FragEXPAward" (int VictimPN)
{
if(EXPTime() == 0){Terminate;}
//Int SelfTID = ActivatorTID();

    int PlayerLVL = GetCvar(StrParam(s:"PlayerLevel_",i:VictimPN));
    int PlayerDexNum = GetCvar(StrParam(s:"PlayerDEXNum_",i:VictimPN));
    int VictimFrags = ACS_NamedExecuteWithResult("GetPlayerFrags",VictimPN);
    int VictimBounty = StatDEX[PlayerDexNum][6];
int EXPAward = VictimBounty*PlayerLVL;
int BPAward = (PlayerLVL*PKMN_MaxLVL)+(VictimBounty*VictimFrags);

// -=- Clamp BP
if(BPAward < 10){BPAward = 10;}
if(BPAward > 500){BPAward = 500;}

//SetActivatorToTarget(0);
//int KillerTID = ActivatorTID();
// --=- no exp if you kill yourself
//if(VictimTID == KillerTID){Terminate;}

ACS_NamedExecuteAlways("GiveEXP",0,EXPAward);
ACS_NamedExecuteWithResult("GivePlayerBP",Playernumber(),BPAward);

TeamEXPAward[PlayerTeam()] += EXPAward/2;
TeamBPAward[PlayerTeam()] += BPAward/2;
//log(s:GetTeamProperty(PlayerTeam(),TPROP_Name), s:" Team EXP Award ", i:TeamEXPAward[PlayerTeam()]);
}

script "ResetRoundLevel1" OPEN
{
    SetCVar("PKMN_RoundLevel",0);
}

script "ResetRoundLevel2" UNLOADING
{
    SetCVar("PKMN_RoundLevel",0);
}

script "IncrementRoundLevel" (Void)
{
    if(GetCvar("PKMN_NoEXP")==1||GetCvar("PKMN_NoRoundLeveling")==1){Terminate;}
    SetCVar("PKMN_RoundLevel",GetCvar("PKMN_RoundLevel")+1);
}

#endregion -=-=-=-=-=-=-=-
//#endregion -=-=-=-=-=-=-=-

//#region -=- Evolutions -=-
#region -=- Evolutions -=-
Script "SetDexNumber" (int Dexnum)
{
    SetCVar(StrParam(s:"PlayerDEXNum_",i:PlayerNumber()),Dexnum);
    ACS_NamedExecuteAlways("SetPokeSkin",0,Dexnum);
}


Script "EvolveTrigger" (void)
{
if(StatusTime() == 0){print(s:"Unsafe to Evolve Right now"); Terminate;}


//First, Check if you're Eevee, Run a different script if you are
if(GetActorClass(0) == "Eevee")
    {Terminate;}
//Gather Data
int PlayerDex = GetCVar(StrParam(s:"PlayerDEXNum_",i:PlayerNumber()));
int PlayerLVL = GetCvar(StrParam(s:"PlayerLevel_",i:PlayerNumber()));
int EvoPaths;
//Check if you are capable of evolution
if(EvoDEX[PlayerDex][0] == 0)
    {
        print(s:"No Further Evolutions");
        Terminate;
    }
Else if (EvoDEX[PlayerDex][0] > 0){EvoPaths = EvoDEX[PlayerDex][0];}

if(PlayerLVL >=  EvoDEX[PlayerDex][1])
    {
        //Evolution approved

        If (EvoPaths > 1)
            {
            //If player has more than one path, Generate a small menu to denote the paths you can take
            
            ACS_NamedExecuteAlways("PromptBranchEvo_Standard",0);
            }
        Else If (EvoPaths == 1)
            {
            //No paths to choose from, just evolve straight away.
            ACS_NamedExecuteAlways("PlayerFormChange",0,EvoDEX[PlayerDex][2]);
            GiveInventory("EvolutionFX",1);
            print(s:"Evolved!");
            }
    }
    else
    {
        print(s:"Cannot evolve untill level ",i:EvoDEX[PlayerDex][1]);
    }
}

Script "PlayerFormChange" (Int NextForm)
{
ACS_NamedExecuteAlways("ChangeBaseStat",0,NextForm);
ACS_NamedExecuteAlways("SetDexNumber",0,NextForm);
ACS_NamedExecuteAlways("SetPlayerType",0);
}

#endregion
//#endregion

